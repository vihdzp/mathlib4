/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
module

public import Mathlib.Topology.Sheaves.LocalPredicate
public import Mathlib.Topology.Sheaves.Stalks
public import Mathlib.Topology.Sheaves.Skyscraper

/-!
# Sheafification of `Type`-valued presheaves

We construct the sheafification of a `Type`-valued presheaf,
as the subsheaf of dependent functions into the stalks
consisting of functions which are locally germs.

We show that the stalks of the sheafification are isomorphic to the original stalks,
via `stalkToFiber` which evaluates a germ of a dependent function at a point.

We construct a morphism `toSheafify` from a presheaf to (the underlying presheaf of)
its sheafification, given by sending a section to its collection of germs.

## Future work
Show that the map induced on stalks by `toSheafify` is the inverse of `stalkToFiber`.

Show sheafification is a functor from presheaves to sheaves,
and that it is the left adjoint of the forgetful functor,
following <https://stacks.math.columbia.edu/tag/007X>.
-/

@[expose] public section

assert_not_exists CommRingCat


universe v u

noncomputable section

open TopCat Opposite TopologicalSpace CategoryTheory

variable {X : TopCat.{v}} (F : Presheaf (Type v) X)

namespace TopCat.Presheaf

namespace Sheafify

/--
The prelocal predicate on functions into the stalks, asserting that the function is equal to a germ.
-/
def isGerm : PrelocalPredicate fun x => F.stalk x where
  pred {U} f := ‚àÉ g : F.obj (op U), ‚àÄ x : U, f x = F.germ U x.1 x.2 g
  res := fun i _ ‚ü®g, p‚ü© => ‚ü®F.map i.op g, fun x ‚Ü¶ (p (i x)).trans (F.germ_res_apply i x x.2 g).symm‚ü©

/-- The local predicate on functions into the stalks,
asserting that the function is locally equal to a germ.
-/
def isLocallyGerm : LocalPredicate fun x => F.stalk x :=
  (isGerm F).sheafify

end Sheafify

/-- The sheafification of a `Type`-valued presheaf, defined as the functions into the stalks which
are locally equal to germs.
-/
def sheafify : Sheaf (Type v) X :=
  subsheafToTypes (Sheafify.isLocallyGerm F)

/-- The morphism from a presheaf to its sheafification,
sending each section to its germs.
(This forms the unit of the adjunction.)
-/
def toSheafify : F ‚ü∂ F.sheafify.1 where
  app U f := ‚ü®fun x => F.germ _ x x.2 f, PrelocalPredicate.sheafifyOf ‚ü®f, fun x => rfl‚ü©‚ü©
  naturality U U' f := by
    ext x
    apply Subtype.ext -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): Added `apply`
    ext ‚ü®u, m‚ü©
    exact germ_res_apply F f.unop u m x

/-- The natural morphism from the stalk of the sheafification to the original stalk.
In `sheafifyStalkIso` we show this is an isomorphism.
-/
def stalkToFiber (x : X) : F.sheafify.presheaf.stalk x ‚ü∂ F.stalk x :=
  TopCat.stalkToFiber (Sheafify.isLocallyGerm F) x

theorem stalkToFiber_surjective (x : X) : Function.Surjective (F.stalkToFiber x) := by
  apply TopCat.stalkToFiber_surjective
  intro t
  obtain ‚ü®U, m, s, rfl‚ü© := F.germ_exist _ t
  use ‚ü®U, m‚ü©
  fconstructor
  ¬∑ exact fun y => F.germ _ _ y.2 s
  ¬∑ exact ‚ü®PrelocalPredicate.sheafifyOf ‚ü®s, fun _ => rfl‚ü©, rfl‚ü©

theorem stalkToFiber_injective (x : X) : Function.Injective (F.stalkToFiber x) := by
  apply TopCat.stalkToFiber_injective
  intro U V fU hU fV hV e
  rcases hU ‚ü®x, U.2‚ü© with ‚ü®U', mU, iU, gU, wU‚ü©
  rcases hV ‚ü®x, V.2‚ü© with ‚ü®V', mV, iV, gV, wV‚ü©
  have wUx := wU ‚ü®x, mU‚ü©
  dsimp at wUx; rw [wUx] at e; clear wUx
  have wVx := wV ‚ü®x, mV‚ü©
  dsimp at wVx; rw [wVx] at e; clear wVx
  rcases F.germ_eq x mU mV gU gV e with ‚ü®W, mW, iU', iV', (e' : F.map iU'.op gU = F.map iV'.op gV)‚ü©
  use ‚ü®W ‚äì (U' ‚äì V'), ‚ü®mW, mU, mV‚ü©‚ü©
  refine ‚ü®?_, ?_, ?_‚ü©
  ¬∑ change W ‚äì (U' ‚äì V') ‚ü∂ U.val
    exact Opens.infLERight _ _ ‚â´ Opens.infLELeft _ _ ‚â´ iU
  ¬∑ change W ‚äì (U' ‚äì V') ‚ü∂ V.val
    exact Opens.infLERight _ _ ‚â´ Opens.infLERight _ _ ‚â´ iV
  ¬∑ intro w
    specialize wU ‚ü®w.1, w.2.2.1‚ü©
    specialize wV ‚ü®w.1, w.2.2.2‚ü©
    refine wU.trans <| .trans ?_ wV.symm
    rw [‚Üê F.germ_res iU' w w.2.1, ‚Üê F.germ_res iV' w w.2.1,
      CategoryTheory.types_comp_apply, CategoryTheory.types_comp_apply, e']

/-- The isomorphism between a stalk of the sheafification and the original stalk.
-/
def sheafifyStalkIso (x : X) : F.sheafify.presheaf.stalk x ‚âÖ F.stalk x :=
  (Equiv.ofBijective _ ‚ü®stalkToFiber_injective _ _, stalkToFiber_surjective _ _‚ü©).toIso

-- PROJECT functoriality, and that sheafification is the left adjoint of the forgetful functor.
end TopCat.Presheaf

namespace TopCat.Presheaf

variable (p‚ÇÄ : X) (C : Type u) [Category.{v} C] [Limits.HasColimits C]
  [Limits.HasTerminal C] (ùìï : Presheaf C X) [HasWeakSheafify (Opens.grothendieckTopology X) C]

/-- Given a presheaf `ùìï`, the induced map on stalks of `CategoryTheory.toSheafify`, `ùìï‚Çì ‚ü∂ ùìï‚Å∫‚Çì`,
is an isomorphism -/
theorem stalkFunctor_map_unit_toSheafify_isIso : IsIso ((Presheaf.stalkFunctor C p‚ÇÄ).map
    (CategoryTheory.toSheafify (Opens.grothendieckTopology X) ùìï)) := by
  classical
  exact Adjunction.isIso_map_unit_of_isLeftAdjoint_comp (sheafificationAdjunction _ C)
    (skyscraperSheafForgetAdjunction p‚ÇÄ)

end TopCat.Presheaf

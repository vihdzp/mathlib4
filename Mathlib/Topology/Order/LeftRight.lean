/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
module

public import Mathlib.Order.Antichain
public import Mathlib.Topology.ContinuousOn
public import Mathlib.Order.Interval.Set.UnorderedInterval

/-!
# Left and right continuity

In this file we prove a few lemmas about left and right continuous functions:

* `continuousWithinAt_Ioi_iff_Ici`: two definitions of right continuity
  (with `(a, âˆ)` and with `[a, âˆ)`) are equivalent;
* `continuousWithinAt_Iio_iff_Iic`: two definitions of left continuity
  (with `(-âˆ, a)` and with `(-âˆ, a]`) are equivalent;
* `continuousAt_iff_continuous_left_right`, `continuousAt_iff_continuous_left'_right'` :
  a function is continuous at `a` if and only if it is left and right continuous at `a`.

## Tags

left continuous, right continuous
-/

@[expose] public section


open Set Filter Topology

section Preorder

variable {Î± : Type*} [TopologicalSpace Î±] [Preorder Î±]

@[to_dual frequently_gt_nhds]
lemma frequently_lt_nhds (a : Î±) [NeBot (ğ“[<] a)] : âˆƒá¶  x in ğ“ a, x < a :=
  frequently_iff_neBot.2 â€¹_â€º

@[to_dual exists_gt]
theorem Filter.Eventually.exists_lt {a : Î±} [NeBot (ğ“[<] a)] {p : Î± â†’ Prop}
    (h : âˆ€á¶  x in ğ“ a, p x) : âˆƒ b < a, p b :=
  ((frequently_lt_nhds a).and_eventually h).exists

@[to_dual nhdsWithin_Iic_neBot]
theorem nhdsWithin_Ici_neBot {a b : Î±} (Hâ‚‚ : a â‰¤ b) : NeBot (ğ“[Ici a] b) :=
  nhdsWithin_neBot_of_mem Hâ‚‚

@[to_dual nhdsGE_neBot]
instance nhdsLE_neBot (a : Î±) : NeBot (ğ“[â‰¤] a) := nhdsWithin_Iic_neBot (le_refl a)

@[to_dual]
theorem nhdsLT_le_nhdsNE (a : Î±) : ğ“[<] a â‰¤ ğ“[â‰ ] a :=
  nhdsWithin_mono a fun _ => ne_of_lt

-- TODO: add instances for `NeBot (ğ“[<] x)` on (indexed) product types

lemma IsAntichain.interior_eq_empty [âˆ€ x : Î±, (ğ“[<] x).NeBot] {s : Set Î±}
    (hs : IsAntichain (Â· â‰¤ Â·) s) : interior s = âˆ… := by
  refine eq_empty_of_forall_notMem fun x hx â†¦ ?_
  have : âˆ€á¶  y in ğ“ x, y âˆˆ s := mem_interior_iff_mem_nhds.1 hx
  rcases this.exists_lt with âŸ¨y, hyx, hysâŸ©
  exact hs hys (interior_subset hx) hyx.ne hyx.le

lemma IsAntichain.interior_eq_empty' [âˆ€ x : Î±, (ğ“[>] x).NeBot] {s : Set Î±}
    (hs : IsAntichain (Â· â‰¤ Â·) s) : interior s = âˆ… :=
  have : âˆ€ x : Î±áµ’áµˆ, NeBot (ğ“[<] x) := â€¹_â€º
  hs.to_dual.interior_eq_empty

end Preorder

section PartialOrder

variable {Î± Î² : Type*} [TopologicalSpace Î±] [PartialOrder Î±] [TopologicalSpace Î²]

@[to_dual]
theorem continuousWithinAt_Ioi_iff_Ici {a : Î±} {f : Î± â†’ Î²} :
    ContinuousWithinAt f (Ioi a) a â†” ContinuousWithinAt f (Ici a) a := by
  simp only [â† Ici_diff_left, continuousWithinAt_diff_self]

@[to_dual]
theorem continuousWithinAt_inter_Ioi_iff_Ici {a : Î±} {f : Î± â†’ Î²} {s : Set Î±} :
    ContinuousWithinAt f (s âˆ© Ioi a) a â†” ContinuousWithinAt f (s âˆ© Ici a) a := by
  simp [â† Ici_diff_left, â† inter_diff_assoc, continuousWithinAt_diff_self]

end PartialOrder

section TopologicalSpace

variable {Î± Î² : Type*} [TopologicalSpace Î±] [LinearOrder Î±] [TopologicalSpace Î²] {s : Set Î±}

@[to_dual]
theorem nhdsLE_sup_nhdsGE (a : Î±) : ğ“[â‰¤] a âŠ” ğ“[â‰¥] a = ğ“ a := by
  rw [â† nhdsWithin_union, Iic_union_Ici, nhdsWithin_univ]

@[to_dual]
theorem nhdsWithinLE_sup_nhdsWithinGE (a : Î±) : ğ“[s âˆ© Iic a] a âŠ” ğ“[s âˆ© Ici a] a = ğ“[s] a := by
  rw [â† nhdsWithin_union, â† inter_union_distrib_left, Iic_union_Ici, inter_univ]

@[to_dual]
theorem nhdsLT_sup_nhdsGE (a : Î±) : ğ“[<] a âŠ” ğ“[â‰¥] a = ğ“ a := by
  rw [â† nhdsWithin_union, Iio_union_Ici, nhdsWithin_univ]

@[to_dual]
theorem nhdsWithinLT_sup_nhdsWithinGE (a : Î±) : ğ“[s âˆ© Iio a] a âŠ” ğ“[s âˆ© Ici a] a = ğ“[s] a := by
  rw [â† nhdsWithin_union, â† inter_union_distrib_left, Iio_union_Ici, inter_univ]

@[to_dual]
theorem nhdsLE_sup_nhdsGT (a : Î±) : ğ“[â‰¤] a âŠ” ğ“[>] a = ğ“ a := by
  rw [â† nhdsWithin_union, Iic_union_Ioi, nhdsWithin_univ]

@[to_dual]
theorem nhdsWithinLE_sup_nhdsWithinGT (a : Î±) : ğ“[s âˆ© Iic a] a âŠ” ğ“[s âˆ© Ioi a] a = ğ“[s] a := by
  rw [â† nhdsWithin_union, â† inter_union_distrib_left, Iic_union_Ioi, inter_univ]

@[to_dual]
theorem nhdsLT_sup_nhdsGT (a : Î±) : ğ“[<] a âŠ” ğ“[>] a = ğ“[â‰ ] a := by
  rw [â† nhdsWithin_union, Iio_union_Ioi]

@[to_dual]
theorem nhdsWithinLT_sup_nhdsWithinGT (a : Î±) :
    ğ“[s âˆ© Iio a] a âŠ” ğ“[s âˆ© Ioi a] a = ğ“[s \ {a}] a := by
  rw [â† nhdsWithin_union, â† inter_union_distrib_left, Iio_union_Ioi, compl_eq_univ_diff,
    inter_sdiff_left_comm, univ_inter]

@[to_dual]
lemma nhdsGT_sup_nhdsWithin_singleton (a : Î±) :
    ğ“[>] a âŠ” ğ“[{a}] a = ğ“[â‰¥] a := by
  simp only [union_singleton, Ioi_insert, â† nhdsWithin_union]

lemma nhdsWithin_uIoo_left_le_nhdsNE {a b : Î±} : ğ“[uIoo a b] a â‰¤ ğ“[â‰ ] a :=
  nhdsWithin_mono _ (by simp)

lemma nhdsWithin_uIoo_right_le_nhdsNE {a b : Î±} : ğ“[uIoo a b] b â‰¤ ğ“[â‰ ] b :=
  nhdsWithin_mono _ (by simp)

@[to_dual none]
theorem continuousAt_iff_continuous_left_right {a : Î±} {f : Î± â†’ Î²} :
    ContinuousAt f a â†” ContinuousWithinAt f (Iic a) a âˆ§ ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, ContinuousAt, â† tendsto_sup, nhdsLE_sup_nhdsGE]

@[to_dual none]
theorem continuousAt_iff_continuous_left'_right' {a : Î±} {f : Î± â†’ Î²} :
    ContinuousAt f a â†” ContinuousWithinAt f (Iio a) a âˆ§ ContinuousWithinAt f (Ioi a) a := by
  rw [continuousWithinAt_Ioi_iff_Ici, continuousWithinAt_Iio_iff_Iic,
    continuousAt_iff_continuous_left_right]

@[to_dual none]
theorem continuousWithinAt_iff_continuous_left_right {a : Î±} {f : Î± â†’ Î²} :
    ContinuousWithinAt f s a â†”
      ContinuousWithinAt f (s âˆ© Iic a) a âˆ§ ContinuousWithinAt f (s âˆ© Ici a) a := by
  simp only [ContinuousWithinAt, â† tendsto_sup, nhdsWithinLE_sup_nhdsWithinGE]

@[to_dual none]
theorem continuousWithinAt_iff_continuous_left'_right' {a : Î±} {f : Î± â†’ Î²} :
    ContinuousWithinAt f s a â†”
      ContinuousWithinAt f (s âˆ© Iio a) a âˆ§ ContinuousWithinAt f (s âˆ© Ioi a) a := by
  rw [continuousWithinAt_inter_Ioi_iff_Ici, continuousWithinAt_inter_Iio_iff_Iic,
    continuousWithinAt_iff_continuous_left_right]

end TopologicalSpace

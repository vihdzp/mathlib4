/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
module

public import Mathlib.Topology.Order.LeftRight
public import Mathlib.Topology.Separation.Hausdorff

/-!
# Order-closed topologies

In this file we introduce 3 typeclass mixins that relate topology and order structures:

- `ClosedIicTopology` says that all the intervals $(-‚àû, a]$ (formally, `Set.Iic a`)
  are closed sets;
- `ClosedIciTopology` says that all the intervals $[a, +‚àû)$ (formally, `Set.Ici a`)
  are closed sets;
- `OrderClosedTopology` says that the set of points `(x, y)` such that `x ‚â§ y`
  is closed in the product topology.

The last predicate implies the first two.

We prove many basic properties of such topologies.

## Main statements

This file contains the proofs of the following facts.
For exact requirements
(`OrderClosedTopology` vs `ClosedIciTopology` vs `ClosedIicTopology`,
`Preorder` vs `PartialOrder` vs `LinearOrder`, etc.)
see their statements.

### Open / closed sets

* `isOpen_lt` : if `f` and `g` are continuous functions, then `{x | f x < g x}` is open;
* `isOpen_Iio`, `isOpen_Ioi`, `isOpen_Ioo` : open intervals are open;
* `isClosed_le` : if `f` and `g` are continuous functions, then `{x | f x ‚â§ g x}` is closed;
* `isClosed_Iic`, `isClosed_Ici`, `isClosed_Icc` : closed intervals are closed;
* `frontier_le_subset_eq`, `frontier_lt_subset_eq` : frontiers of both `{x | f x ‚â§ g x}`
  and `{x | f x < g x}` are included by `{x | f x = g x}`;

### Convergence and inequalities

* `le_of_tendsto_of_tendsto` : if `f` converges to `a`, `g` converges to `b`, and eventually
  `f x ‚â§ g x`, then `a ‚â§ b`
* `le_of_tendsto`, `ge_of_tendsto` : if `f` converges to `a` and eventually `f x ‚â§ b`
  (resp., `b ‚â§ f x`), then `a ‚â§ b` (resp., `b ‚â§ a`); we also provide primed versions
  that assume the inequalities to hold for all `x`.

### Min, max, `sSup` and `sInf`

* `Continuous.min`, `Continuous.max`: pointwise `min`/`max` of two continuous functions is
  continuous.
* `Tendsto.min`, `Tendsto.max` : if `f` tends to `a` and `g` tends to `b`, then their pointwise
  `min`/`max` tend to `min a b` and `max a b`, respectively.
-/

@[expose] public section

open Set Filter TopologicalSpace
open OrderDual (toDual)
open scoped Topology

universe u v w
variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- If `Œ±` is a topological space and a preorder, `ClosedIicTopology Œ±` means that `Iic a` is
closed for all `a : Œ±`. -/
class ClosedIicTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- For any `a`, the set `(-‚àû, a]` is closed. -/
  isClosed_Iic (a : Œ±) : IsClosed (Iic a)

/-- If `Œ±` is a topological space and a preorder, `ClosedIciTopology Œ±` means that `Ici a` is
closed for all `a : Œ±`. -/
class ClosedIciTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- For any `a`, the set `[a, +‚àû)` is closed. -/
  isClosed_Ici (a : Œ±) : IsClosed (Ici a)

attribute [to_dual existing] ClosedIicTopology ClosedIicTopology.isClosed_Iic

/-- A topology on a set which is both a topological space and a preorder is _order-closed_ if the
set of points `(x, y)` with `x ‚â§ y` is closed in the product space. We introduce this as a mixin.
This property is satisfied for the order topology on a linear order, but it can be satisfied more
generally, and suffices to derive many interesting properties relating order and topology. -/
class OrderClosedTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- The set `{ (x, y) | x ‚â§ y }` is a closed set. -/
  isClosed_le' : IsClosed { p : Œ± √ó Œ± | p.1 ‚â§ p.2 }

instance [TopologicalSpace Œ±] [h : FirstCountableTopology Œ±] : FirstCountableTopology Œ±·µí·µà := h
instance [TopologicalSpace Œ±] [h : SecondCountableTopology Œ±] : SecondCountableTopology Œ±·µí·µà := h
instance [TopologicalSpace Œ±] [h : SeparableSpace Œ±] : SeparableSpace Œ±·µí·µà := h

theorem Dense.orderDual [TopologicalSpace Œ±] {s : Set Œ±} (hs : Dense s) :
    Dense (OrderDual.ofDual ‚Åª¬π' s) :=
  hs

section General
variable [TopologicalSpace Œ±] [Preorder Œ±] {s : Set Œ±}

@[to_dual]
protected lemma BddAbove.of_closure : BddAbove (closure s) ‚Üí BddAbove s :=
  BddAbove.mono subset_closure

end General

section ClosedIicTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Set Œ±}

@[to_dual]
theorem isClosed_Iic : IsClosed (Iic a) :=
  ClosedIicTopology.isClosed_Iic a

@[to_dual]
instance : ClosedIciTopology Œ±·µí·µà where
  isClosed_Ici _ := isClosed_Iic (Œ± := Œ±)

@[to_dual (attr := simp)]
theorem closure_Iic (a : Œ±) : closure (Iic a) = Iic a :=
  isClosed_Iic.closure_eq

@[to_dual ge_of_tendsto_of_frequently]
theorem le_of_tendsto_of_frequently {x : Filter Œ≤} (lim : Tendsto f x (ùìù a))
    (h : ‚àÉ·∂† c in x, f c ‚â§ b) : a ‚â§ b :=
  isClosed_Iic.mem_of_frequently_of_tendsto h lim

@[to_dual ge_of_tendsto]
theorem le_of_tendsto {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ·∂† c in x, f c ‚â§ b) : a ‚â§ b :=
  isClosed_Iic.mem_of_tendsto lim h

@[to_dual ge_of_tendsto']
theorem le_of_tendsto' {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ c, f c ‚â§ b) : a ‚â§ b :=
  le_of_tendsto lim (Eventually.of_forall h)

@[to_dual (attr := simp)]
lemma upperBounds_closure (s : Set Œ±) : upperBounds (closure s : Set Œ±) = upperBounds s :=
  ext fun a ‚Ü¶ by simp_rw [mem_upperBounds_iff_subset_Iic, isClosed_Iic.closure_subset_iff]

@[to_dual (attr := simp)]
lemma bddAbove_closure : BddAbove (closure s) ‚Üî BddAbove s := by
  simp_rw [BddAbove, upperBounds_closure]

@[to_dual]
protected alias ‚ü®_, BddAbove.closure‚ü© := bddAbove_closure

@[to_dual (attr := simp) disjoint_nhds_atTop_iff]
theorem disjoint_nhds_atBot_iff : Disjoint (ùìù a) atBot ‚Üî ¬¨IsBot a := by
  constructor
  ¬∑ intro hd hbot
    rw [hbot.atBot_eq, disjoint_principal_right] at hd
    exact mem_of_mem_nhds hd le_rfl
  ¬∑ simp only [IsBot, not_forall]
    rintro ‚ü®b, hb‚ü©
    refine disjoint_of_disjoint_of_mem disjoint_compl_left ?_ (Iic_mem_atBot b)
    exact isClosed_Iic.isOpen_compl.mem_nhds hb

@[to_dual]
theorem IsLUB.range_of_tendsto {F : Filter Œ≤} [F.NeBot] (hle : ‚àÄ i, f i ‚â§ a)
    (hlim : Tendsto f F (ùìù a)) : IsLUB (range f) a :=
  ‚ü®forall_mem_range.mpr hle, fun _c hc ‚Ü¶ le_of_tendsto' hlim fun i ‚Ü¶ hc <| mem_range_self i‚ü©

end Preorder

section NoBotOrder

variable [Preorder Œ±] [NoBotOrder Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±] {a : Œ±}
  {l : Filter Œ≤} [NeBot l] {f : Œ≤ ‚Üí Œ±}

@[to_dual disjoint_nhds_atTop]
theorem disjoint_nhds_atBot (a : Œ±) : Disjoint (ùìù a) atBot := by simp

@[to_dual (attr := simp) inf_nhds_atTop]
theorem inf_nhds_atBot (a : Œ±) : ùìù a ‚äì atBot = ‚ä• := (disjoint_nhds_atBot a).eq_bot

@[to_dual]
theorem not_tendsto_nhds_of_tendsto_atBot (hf : Tendsto f l atBot) (a : Œ±) : ¬¨Tendsto f l (ùìù a) :=
  hf.not_tendsto (disjoint_nhds_atBot a).symm

@[to_dual]
theorem not_tendsto_atBot_of_tendsto_nhds (hf : Tendsto f l (ùìù a)) : ¬¨Tendsto f l atBot :=
  hf.not_tendsto (disjoint_nhds_atBot a)

end NoBotOrder

theorem iSup_eq_of_forall_le_of_tendsto {Œπ : Type*} {F : Filter Œπ} [Filter.NeBot F]
    [ConditionallyCompleteLattice Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hle : ‚àÄ i, f i ‚â§ a) (hlim : Filter.Tendsto f F (ùìù a)) :
    ‚®Ü i, f i = a :=
  have := F.nonempty_of_neBot
  (IsLUB.range_of_tendsto hle hlim).ciSup_eq

theorem iUnion_Iic_eq_Iio_of_lt_of_tendsto {Œπ : Type*} {F : Filter Œπ} [F.NeBot]
    [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hlt : ‚àÄ i, f i < a) (hlim : Tendsto f F (ùìù a)) :
    ‚ãÉ i : Œπ, Iic (f i) = Iio a := by
  have obs : a ‚àâ range f := by
    rw [mem_range]
    rintro ‚ü®i, rfl‚ü©
    exact (hlt i).false
  rw [‚Üê biUnion_range, (IsLUB.range_of_tendsto (le_of_lt <| hlt ¬∑) hlim).biUnion_Iic_eq_Iio obs]

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [ClosedIicTopology Œ±] [TopologicalSpace Œ≤]
  {a b c : Œ±} {f : Œ± ‚Üí Œ≤}

@[to_dual]
theorem isOpen_Ioi : IsOpen (Ioi a) := by
  rw [‚Üê compl_Iic]
  exact isClosed_Iic.isOpen_compl

@[to_dual (attr := simp)]
theorem interior_Ioi : interior (Ioi a) = Ioi a :=
  isOpen_Ioi.interior_eq

@[to_dual]
theorem Ioi_mem_nhds (h : a < b) : Ioi a ‚àà ùìù b := IsOpen.mem_nhds isOpen_Ioi h

@[to_dual eventually_lt_nhds]
theorem eventually_gt_nhds (hab : b < a) : ‚àÄ·∂† x in ùìù a, b < x := Ioi_mem_nhds hab

@[to_dual]
theorem Ici_mem_nhds (h : a < b) : Ici a ‚àà ùìù b :=
  mem_of_superset (Ioi_mem_nhds h) Ioi_subset_Ici_self

@[to_dual eventually_le_nhds]
theorem eventually_ge_nhds (hab : b < a) : ‚àÄ·∂† x in ùìù a, b ‚â§ x := Ici_mem_nhds hab

@[to_dual eventually_lt_const]
theorem Filter.Tendsto.eventually_const_lt {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : u < v)
    (h : Filter.Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, u < f a :=
  h.eventually <| eventually_gt_nhds hv

@[to_dual eventually_le_const]
theorem Filter.Tendsto.eventually_const_le {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : u < v)
    (h : Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, u ‚â§ f a :=
  h.eventually <| eventually_ge_nhds hv

@[to_dual exists_lt]
protected theorem Dense.exists_gt [NoMaxOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, x < y :=
  hs.exists_mem_open isOpen_Ioi (exists_gt x)

@[to_dual exists_le]
protected theorem Dense.exists_ge [NoMaxOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, x ‚â§ y :=
  (hs.exists_gt x).imp fun _ h ‚Ü¶ ‚ü®h.1, h.2.le‚ü©

@[to_dual exists_le']
theorem Dense.exists_ge' {s : Set Œ±} (hs : Dense s) (htop : ‚àÄ x, IsTop x ‚Üí x ‚àà s) (x : Œ±) :
    ‚àÉ y ‚àà s, x ‚â§ y := by
  by_cases hx : IsTop x
  ¬∑ exact ‚ü®x, htop x hx, le_rfl‚ü©
  ¬∑ simp only [IsTop, not_forall, not_le] at hx
    rcases hs.exists_mem_open isOpen_Ioi hx with ‚ü®y, hys, hy : x < y‚ü©
    exact ‚ü®y, hys, hy.le‚ü©

/-!
### Left neighborhoods on a `ClosedIicTopology`

Limits to the left of real functions are defined in terms of neighborhoods to the left, either open
or closed, i.e., members of `ùìù[<] a` and `ùìù[‚â§] a`. Here we prove that all left-neighborhoods of a
point are equal, and we prove other useful characterizations which require the stronger hypothesis
`OrderTopology Œ±` in another file.
-/

/-!
#### Point excluded
-/

@[to_dual]
theorem Ioo_mem_nhdsLT (H : a < b) : Ioo a b ‚àà ùìù[<] b := by
  simpa only [‚Üê Iio_inter_Ioi] using inter_mem_nhdsWithin _ (Ioi_mem_nhds H)

@[to_dual]
theorem Ioo_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Ioo a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT H.1) <| Ioo_subset_Ioo_right H.2

@[to_dual]
protected theorem CovBy.nhdsLT (h : a ‚ãñ b) : ùìù[<] b = ‚ä• :=
  empty_mem_iff_bot.mp <| h.Ioo_eq ‚ñ∏ Ioo_mem_nhdsLT h.1

@[to_dual]
protected theorem PredOrder.nhdsLT [PredOrder Œ±] : ùìù[<] a = ‚ä• := by
  if h : IsMin a then simp [h.Iio_eq]
  else exact (Order.pred_covBy_of_not_isMin h).nhdsLT

@[to_dual]
theorem PredOrder.nhdsGT_eq_nhdsNE [PredOrder Œ±] (a : Œ±) : ùìù[>] a = ùìù[‚â†] a := by
  rw [‚Üê nhdsLT_sup_nhdsGT, PredOrder.nhdsLT, bot_sup_eq]

@[to_dual]
theorem PredOrder.nhdsGE_eq_nhds [PredOrder Œ±] (a : Œ±) : ùìù[‚â•] a = ùìù a := by
  rw [‚Üê nhdsLT_sup_nhdsGE, PredOrder.nhdsLT, bot_sup_eq]

@[to_dual]
theorem Ico_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Ico a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Ico_self

@[to_dual]
theorem Ico_mem_nhdsLT (H : a < b) : Ico a b ‚àà ùìù[<] b := Ico_mem_nhdsLT_of_mem ‚ü®H, le_rfl‚ü©

@[to_dual]
theorem Ioc_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Ioc a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Ioc_self

@[to_dual]
theorem Ioc_mem_nhdsLT (H : a < b) : Ioc a b ‚àà ùìù[<] b := Ioc_mem_nhdsLT_of_mem ‚ü®H, le_rfl‚ü©

@[to_dual]
theorem Icc_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Icc a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Icc_self

@[to_dual]
theorem Icc_mem_nhdsLT (H : a < b) : Icc a b ‚àà ùìù[<] b := Icc_mem_nhdsLT_of_mem ‚ü®H, le_rfl‚ü©

@[to_dual (attr := simp)]
theorem nhdsWithin_Ico_eq_nhdsLT (h : a < b) : ùìù[Ico a b] b = ùìù[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h

@[to_dual (attr := simp)]
theorem nhdsWithin_Ioo_eq_nhdsLT (h : a < b) : ùìù[Ioo a b] b = ùìù[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h

@[to_dual (attr := simp)]
theorem continuousWithinAt_Ico_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ico a b) b ‚Üî ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsLT h]

@[to_dual (attr := simp)]
theorem continuousWithinAt_Ioo_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ioo a b) b ‚Üî ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsLT h]

/-!
#### Point included
-/

@[to_dual]
protected theorem CovBy.nhdsLE (H : a ‚ãñ b) : ùìù[‚â§] b = pure b := by
  rw [‚Üê Iio_insert, nhdsWithin_insert, H.nhdsLT, sup_bot_eq]

@[to_dual]
protected theorem PredOrder.nhdsLE [PredOrder Œ±] : ùìù[‚â§] b = pure b := by
  rw [‚Üê Iio_insert, nhdsWithin_insert, PredOrder.nhdsLT, sup_bot_eq]

@[to_dual]
theorem Ioc_mem_nhdsLE (H : a < b) : Ioc a b ‚àà ùìù[‚â§] b :=
  inter_mem (nhdsWithin_le_nhds <| Ioi_mem_nhds H) self_mem_nhdsWithin

@[to_dual]
theorem Ioo_mem_nhdsLE_of_mem (H : b ‚àà Ioo a c) : Ioo a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsLE H.1) <| Ioc_subset_Ioo_right H.2

@[to_dual]
theorem Ico_mem_nhdsLE_of_mem (H : b ‚àà Ioo a c) : Ico a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioo_mem_nhdsLE_of_mem H) Ioo_subset_Ico_self

@[to_dual]
theorem Ioc_mem_nhdsLE_of_mem (H : b ‚àà Ioc a c) : Ioc a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsLE H.1) <| Ioc_subset_Ioc_right H.2

@[to_dual]
theorem Icc_mem_nhdsLE_of_mem (H : b ‚àà Ioc a c) : Icc a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsLE_of_mem H) Ioc_subset_Icc_self

@[to_dual]
theorem Icc_mem_nhdsLE (H : a < b) : Icc a b ‚àà ùìù[‚â§] b := Icc_mem_nhdsLE_of_mem ‚ü®H, le_rfl‚ü©

@[to_dual (attr := simp)]
theorem nhdsWithin_Icc_eq_nhdsLE (h : a < b) : ùìù[Icc a b] b = ùìù[‚â§] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h

@[to_dual (attr := simp)]
theorem nhdsWithin_Ioc_eq_nhdsLE (h : a < b) : ùìù[Ioc a b] b = ùìù[‚â§] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h

@[to_dual (attr := simp)]
theorem continuousWithinAt_Icc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Icc a b) b ‚Üî ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsLE h]

@[to_dual (attr := simp)]
theorem continuousWithinAt_Ioc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Ioc a b) b ‚Üî ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsLE h]

end LinearOrder

end ClosedIicTopology

section ClosedIciTopology

-- TODO: we're missing some to_dual tags for conditionally complete lattices

@[to_dual existing]
theorem iInf_eq_of_forall_le_of_tendsto {Œπ : Type*} {F : Filter Œπ} [F.NeBot]
    [ConditionallyCompleteLattice Œ±] [TopologicalSpace Œ±] [ClosedIciTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hle : ‚àÄ i, a ‚â§ f i) (hlim : Tendsto f F (ùìù a)) :
    ‚®Ö i, f i = a :=
  iSup_eq_of_forall_le_of_tendsto (Œ± := Œ±·µí·µà) hle hlim

@[to_dual existing]
theorem iUnion_Ici_eq_Ioi_of_lt_of_tendsto {Œπ : Type*} {F : Filter Œπ} [F.NeBot]
    [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±] [ClosedIciTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hlt : ‚àÄ i, a < f i) (hlim : Tendsto f F (ùìù a)) :
    ‚ãÉ i : Œπ, Ici (f i) = Ioi a :=
  iUnion_Iic_eq_Iio_of_lt_of_tendsto (Œ± := Œ±·µí·µà) hlt hlim

section OrderClosedTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [t : OrderClosedTopology Œ±]

namespace Subtype

-- todo: add `OrderEmbedding.orderClosedTopology`
instance {p : Œ± ‚Üí Prop} : OrderClosedTopology (Subtype p) :=
  have this : Continuous fun p : Subtype p √ó Subtype p => ((p.fst : Œ±), (p.snd : Œ±)) :=
    continuous_subtype_val.prodMap continuous_subtype_val
  OrderClosedTopology.mk (t.isClosed_le'.preimage this)

end Subtype

@[to_dual existing isClosed_le']
theorem OrderClosedTopology.isClosed_le'' : IsClosed { p : Œ± √ó Œ± | p.2 ‚â§ p.1 } :=
  (isClosed_le' (Œ± := Œ±)).preimage continuous_swap

@[to_dual isClosed_le_prod']
theorem isClosed_le_prod : IsClosed { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } :=
  t.isClosed_le'

@[to_dual self (reorder := f g, hf hg)]
theorem isClosed_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { b | f b ‚â§ g b } :=
  continuous_iff_isClosed.mp (hf.prodMk hg) _ isClosed_le_prod

@[to_dual]
instance : ClosedIicTopology Œ± where
  isClosed_Iic _ := isClosed_le continuous_id continuous_const

instance : OrderClosedTopology Œ±·µí·µà :=
  ‚ü®isClosed_le_prod' (Œ± := Œ±)‚ü©

@[to_dual self]
theorem isClosed_Icc {a b : Œ±} : IsClosed (Icc a b) :=
  IsClosed.inter isClosed_Ici isClosed_Iic

@[to_dual self, simp]
theorem closure_Icc (a b : Œ±) : closure (Icc a b) = Icc a b :=
  isClosed_Icc.closure_eq

@[to_dual self (reorder := f g, a‚ÇÅ a‚ÇÇ, hf hg)]
theorem le_of_tendsto_of_tendsto_of_frequently {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±}
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : ‚àÉ·∂† x in b, f x ‚â§ g x) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  t.isClosed_le'.mem_of_frequently_of_tendsto h (hf.prodMk_nhds hg)

@[to_dual self (reorder := f g, a‚ÇÅ a‚ÇÇ, hf hg)]
theorem le_of_tendsto_of_tendsto {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} [NeBot b]
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : f ‚â§·∂†[b] g) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  le_of_tendsto_of_tendsto_of_frequently hf hg <| Eventually.frequently h

@[to_dual self (reorder := f g, a‚ÇÅ a‚ÇÇ, hf hg)]
alias tendsto_le_of_eventuallyLE := le_of_tendsto_of_tendsto

@[to_dual self (reorder := f g, a‚ÇÅ a‚ÇÇ, hf hg)]
theorem le_of_tendsto_of_tendsto' {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} [NeBot b]
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : ‚àÄ x, f x ‚â§ g x) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  le_of_tendsto_of_tendsto hf hg (Eventually.of_forall h)

@[to_dual self (reorder := f g, hf hg), simp]
theorem closure_le_eq [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    closure { b | f b ‚â§ g b } = { b | f b ‚â§ g b } :=
  (isClosed_le hf hg).closure_eq

@[to_dual self (reorder := f g, hf hg)]
theorem closure_lt_subset_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) : closure { b | f b < g b } ‚äÜ { b | f b ‚â§ g b } :=
  (closure_minimal fun _ => le_of_lt) <| isClosed_le hf hg

@[to_dual self (reorder := f g, hf hg)]
theorem ContinuousWithinAt.closure_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} {x : Œ≤}
    (hx : x ‚àà closure s) (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x)
    (h : ‚àÄ y ‚àà s, f y ‚â§ g y) : f x ‚â§ g x :=
  show (f x, g x) ‚àà { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } from
    OrderClosedTopology.isClosed_le'.closure_subset ((hf.prodMk hg).mem_closure hx h)

/-- If `s` is a closed set and two functions `f` and `g` are continuous on `s`,
then the set `{x ‚àà s | f x ‚â§ g x}` is a closed set. -/
@[to_dual self (reorder := f g, hf hg)]
theorem IsClosed.isClosed_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) : IsClosed ({ x ‚àà s | f x ‚â§ g x }) :=
  (hf.prodMk hg).preimage_isClosed_of_isClosed hs OrderClosedTopology.isClosed_le'

@[to_dual self (reorder := f g, hf hg)]
theorem le_on_closure [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (h : ‚àÄ x ‚àà s, f x ‚â§ g x)
    (hf : ContinuousOn f (closure s)) (hg : ContinuousOn g (closure s)) ‚¶Éx‚¶Ñ (hx : x ‚àà closure s) :
    f x ‚â§ g x :=
  have : s ‚äÜ { y ‚àà closure s | f y ‚â§ g y } := fun y hy => ‚ü®subset_closure hy, h y hy‚ü©
  (closure_minimal this (isClosed_closure.isClosed_le hf hg) hx).2

@[to_dual hypograph]
theorem IsClosed.epigraph [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Œ≤ √ó Œ± | p.1 ‚àà s ‚àß f p.1 ‚â§ p.2 } :=
  (hs.preimage continuous_fst).isClosed_le (hf.comp continuousOn_fst Subset.rfl) continuousOn_snd

/-- The set of monotone functions on a set is closed. -/
theorem isClosed_monotoneOn [Preorder Œ≤] {s : Set Œ≤} : IsClosed {f : Œ≤ ‚Üí Œ± | MonotoneOn f s} := by
  simp only [isClosed_iff_clusterPt, clusterPt_principal_iff_frequently]
  intro g hg a ha b hb hab
  have hmain (x) : Tendsto (fun f' ‚Ü¶ f' x) (ùìù g) (ùìù (g x)) := continuousAt_apply x _
  exact le_of_tendsto_of_tendsto_of_frequently (hmain a) (hmain b) (hg.mono fun g h ‚Ü¶ h ha hb hab)

/-- The set of monotone functions is closed. -/
theorem isClosed_monotone [Preorder Œ≤] : IsClosed {f : Œ≤ ‚Üí Œ± | Monotone f} := by
  simp_rw [‚Üê monotoneOn_univ]
  exact isClosed_monotoneOn

/-- The set of antitone functions on a set is closed. -/
theorem isClosed_antitoneOn [Preorder Œ≤] {s : Set Œ≤} : IsClosed {f : Œ≤ ‚Üí Œ± | AntitoneOn f s} :=
  isClosed_monotoneOn (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤)

/-- The set of antitone functions is closed. -/
theorem isClosed_antitone [Preorder Œ≤] : IsClosed {f : Œ≤ ‚Üí Œ± | Antitone f} :=
  isClosed_monotone (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤)

end Preorder

section PartialOrder

variable [TopologicalSpace Œ±] [PartialOrder Œ±] [t : OrderClosedTopology Œ±]

-- see Note [lower instance priority]
instance (priority := 90) OrderClosedTopology.to_t2Space : T2Space Œ± :=
  t2_iff_isClosed_diagonal.2 <| by
    simpa only [diagonal, le_antisymm_iff] using
      t.isClosed_le'.inter (isClosed_le continuous_snd continuous_fst)

end PartialOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderClosedTopology Œ±]

@[to_dual self (reorder := f g, hf hg)]
theorem isOpen_lt [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { b | f b < g b } := by
  simpa only [lt_iff_not_ge] using (isClosed_le hg hf).isOpen_compl

@[to_dual isOpen_lt_prod']
theorem isOpen_lt_prod : IsOpen { p : Œ± √ó Œ± | p.1 < p.2 } :=
  isOpen_lt continuous_fst continuous_snd

variable {a b : Œ±}

@[to_dual self]
theorem isOpen_Ioo : IsOpen (Ioo a b) :=
  IsOpen.inter isOpen_Ioi isOpen_Iio

@[to_dual self, simp]
theorem interior_Ioo : interior (Ioo a b) = Ioo a b :=
  isOpen_Ioo.interior_eq

@[to_dual self]
theorem Ioo_subset_closure_interior : Ioo a b ‚äÜ closure (interior (Ioo a b)) := by
  simp only [interior_Ioo, subset_closure]

@[to_dual self (reorder := a b, ha hb)]
theorem Ioo_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ioo a b ‚àà ùìù x :=
  IsOpen.mem_nhds isOpen_Ioo ‚ü®ha, hb‚ü©

@[to_dual (reorder := ha hb)]
theorem Ioc_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ioc a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ioc_self

@[to_dual self (reorder := a b, ha hb)]
theorem Icc_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Icc a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Icc_self

/-- The only order closed topology on a linear order which is a `PredOrder` and a `SuccOrder`
is the discrete topology.

This theorem is not an instance,
because it causes searches for `PredOrder` and `SuccOrder` with their `Preorder` arguments
and very rarely matches. -/
@[to_dual self (reorder := 5 6)]
theorem DiscreteTopology.of_predOrder_succOrder [PredOrder Œ±] [SuccOrder Œ±] :
    DiscreteTopology Œ± := by
  refine discreteTopology_iff_nhds.mpr fun a ‚Ü¶ ?_
  rw [‚Üê nhdsWithin_univ, ‚Üê Iic_union_Ioi, nhdsWithin_union, PredOrder.nhdsLE, SuccOrder.nhdsGT,
    sup_bot_eq]

end LinearOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderClosedTopology Œ±] {f g : Œ≤ ‚Üí Œ±}

section

variable [TopologicalSpace Œ≤]

@[to_dual self (reorder := f g, hf hg)]
theorem lt_subset_interior_le (hf : Continuous f) (hg : Continuous g) :
    { b | f b < g b } ‚äÜ interior { b | f b ‚â§ g b } :=
  (interior_maximal fun _ => le_of_lt) <| isOpen_lt hf hg

@[to_dual frontier_ge_subset_eq]
theorem frontier_le_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b ‚â§ g b } ‚äÜ { b | f b = g b } := by
  rw [frontier_eq_closure_inter_closure, closure_le_eq hf hg]
  rintro b ‚ü®hb‚ÇÅ, hb‚ÇÇ‚ü©
  refine le_antisymm hb‚ÇÅ (closure_lt_subset_le hg hf ?_)
  convert hb‚ÇÇ using 2; simp only [not_le.symm]; rfl

@[to_dual]
theorem frontier_Iic_subset (a : Œ±) : frontier (Iic a) ‚äÜ {a} :=
  frontier_le_subset_eq (@continuous_id Œ± _) continuous_const

@[to_dual frontier_gt_subset_eq]
theorem frontier_lt_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b < g b } ‚äÜ { b | f b = g b } := by
  simpa only [‚Üê not_lt, ‚Üê compl_setOf, frontier_compl, eq_comm] using frontier_le_subset_eq hg hf

@[to_dual none]
theorem continuous_if_le [TopologicalSpace Œ≥] [‚àÄ x, Decidable (f x ‚â§ g x)] {f' g' : Œ≤ ‚Üí Œ≥}
    (hf : Continuous f) (hg : Continuous g) (hf' : ContinuousOn f' { x | f x ‚â§ g x })
    (hg' : ContinuousOn g' { x | g x ‚â§ f x }) (hfg : ‚àÄ x, f x = g x ‚Üí f' x = g' x) :
    Continuous fun x => if f x ‚â§ g x then f' x else g' x := by
  refine continuous_if (fun a ha => hfg _ (frontier_le_subset_eq hf hg ha)) ?_ (hg'.mono ?_)
  ¬∑ rwa [(isClosed_le hf hg).closure_eq]
  ¬∑ simp only [not_le]
    exact closure_lt_subset_le hg hf

@[to_dual none]
theorem Continuous.if_le [TopologicalSpace Œ≥] [‚àÄ x, Decidable (f x ‚â§ g x)] {f' g' : Œ≤ ‚Üí Œ≥}
    (hf' : Continuous f') (hg' : Continuous g') (hf : Continuous f) (hg : Continuous g)
    (hfg : ‚àÄ x, f x = g x ‚Üí f' x = g' x) : Continuous fun x => if f x ‚â§ g x then f' x else g' x :=
  continuous_if_le hf hg hf'.continuousOn hg'.continuousOn hfg

@[to_dual self (reorder := f g, y z, hf hg)]
theorem Filter.Tendsto.eventually_lt {l : Filter Œ≥} {f g : Œ≥ ‚Üí Œ±} {y z : Œ±} (hf : Tendsto f l (ùìù y))
    (hg : Tendsto g l (ùìù z)) (hyz : y < z) : ‚àÄ·∂† x in l, f x < g x :=
  let ‚ü®_a, ha, _b, hb, h‚ü© := hyz.exists_disjoint_Iio_Ioi
  (hg.eventually (Ioi_mem_nhds hb)).mp <| (hf.eventually (Iio_mem_nhds ha)).mono fun _ h‚ÇÅ h‚ÇÇ =>
    h _ h‚ÇÅ _ h‚ÇÇ

@[to_dual self (reorder := f g, hf hg)]
nonrec theorem ContinuousAt.eventually_lt {x‚ÇÄ : Œ≤} (hf : ContinuousAt f x‚ÇÄ) (hg : ContinuousAt g x‚ÇÄ)
    (hfg : f x‚ÇÄ < g x‚ÇÄ) : ‚àÄ·∂† x in ùìù x‚ÇÄ, f x < g x :=
  hf.eventually_lt hg hfg

@[to_dual (attr := continuity, fun_prop)]
protected theorem Continuous.min (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b => min (f b) (g b) := by
  simp only [min_def]
  exact hf.if_le hg hf hg fun x => id

end

@[to_dual]
theorem continuous_min : Continuous fun p : Œ± √ó Œ± => min p.1 p.2 :=
  continuous_fst.min continuous_snd

@[to_dual]
protected theorem Filter.Tendsto.max {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (hf : Tendsto f b (ùìù a‚ÇÅ))
    (hg : Tendsto g b (ùìù a‚ÇÇ)) : Tendsto (fun b => max (f b) (g b)) b (ùìù (max a‚ÇÅ a‚ÇÇ)) :=
  (continuous_max.tendsto (a‚ÇÅ, a‚ÇÇ)).comp (hf.prodMk_nhds hg)

@[to_dual]
protected theorem Filter.Tendsto.max_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => max a (f i)) l (ùìù a) := by
  simpa only [sup_idem] using (tendsto_const_nhds (x := a)).max h

@[to_dual]
protected theorem Filter.Tendsto.max_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => max (f i) a) l (ùìù a) := by
  simp_rw [max_comm _ a]
  exact h.max_right

-- TODO: why does `to_dual` fail?
theorem Filter.tendsto_nhds_max_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[>] a)) :
    Tendsto (fun i => max a (f i)) l (ùìù[>] a) := by
  obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := tendsto_nhdsWithin_iff.mp h
  exact tendsto_nhdsWithin_iff.mpr ‚ü®h‚ÇÅ.max_right, h‚ÇÇ.mono fun i hi => lt_max_of_lt_right hi‚ü©

@[to_dual existing]
theorem Filter.tendsto_nhds_min_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[<] a)) :
    Tendsto (fun i => min a (f i)) l (ùìù[<] a) :=
  Filter.tendsto_nhds_max_right (Œ± := Œ±·µí·µà) h

@[to_dual]
theorem Filter.tendsto_nhds_max_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[>] a)) :
    Tendsto (fun i => max (f i) a) l (ùìù[>] a) := by
  simp_rw [max_comm _ a]
  exact Filter.tendsto_nhds_max_right h

@[to_dual none]
theorem Dense.exists_between [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) {x y : Œ±} (h : x < y) :
    ‚àÉ z ‚àà s, z ‚àà Ioo x y :=
  hs.exists_mem_open isOpen_Ioo (nonempty_Ioo.2 h)

-- TODO: why does `to_dual` fail?
theorem Dense.Ioi_eq_biUnion [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    Ioi x = ‚ãÉ y ‚àà s ‚à© Ioi x, Ioi y := by
  refine Subset.antisymm (fun z hz ‚Ü¶ ?_) (iUnion‚ÇÇ_subset fun y hy ‚Ü¶ Ioi_subset_Ioi (le_of_lt hy.2))
  rcases hs.exists_between hz with ‚ü®y, hys, hxy, hyz‚ü©
  exact mem_iUnion‚ÇÇ.2 ‚ü®y, ‚ü®hys, hxy‚ü©, hyz‚ü©

@[to_dual existing]
theorem Dense.Iio_eq_biUnion [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    Iio x = ‚ãÉ y ‚àà s ‚à© Iio x, Iio y :=
  Dense.Ioi_eq_biUnion (Œ± := Œ±·µí·µà) hs x

end LinearOrder

end OrderClosedTopology

instance [Preorder Œ±] [TopologicalSpace Œ±] [OrderClosedTopology Œ±] [Preorder Œ≤] [TopologicalSpace Œ≤]
    [OrderClosedTopology Œ≤] : OrderClosedTopology (Œ± √ó Œ≤) :=
  ‚ü®(isClosed_le continuous_fst.fst continuous_snd.fst).inter
    (isClosed_le continuous_fst.snd continuous_snd.snd)‚ü©

instance {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [‚àÄ i, Preorder (Œ± i)] [‚àÄ i, TopologicalSpace (Œ± i)]
    [‚àÄ i, OrderClosedTopology (Œ± i)] : OrderClosedTopology (‚àÄ i, Œ± i) := by
  constructor
  simp only [Pi.le_def, setOf_forall]
  exact isClosed_iInter fun i => isClosed_le (continuous_apply i).fst' (continuous_apply i).snd'

instance Pi.orderClosedTopology' [Preorder Œ≤] [TopologicalSpace Œ≤] [OrderClosedTopology Œ≤] :
    OrderClosedTopology (Œ± ‚Üí Œ≤) :=
  inferInstance

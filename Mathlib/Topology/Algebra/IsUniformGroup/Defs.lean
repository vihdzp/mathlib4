/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl, Anatole Dedecker
-/
module

public import Mathlib.Topology.UniformSpace.Basic
public import Mathlib.Topology.Algebra.Group.Basic

/-!
# Uniform structure on topological groups

Given a topological group `G`, one can naturally build two uniform structures
(the "left" and "right" ones) on `G` inducing its topology.
This file defines typeclasses for groups equipped with either of these uniform structures, as well
as a separate typeclass for the (very common) case where the given uniform structure
coincides with **both** the left and right uniform structures.

## Main declarations

* `IsRightUniformGroup` and `IsRightUniformAddGroup`: Multiplicative and additive topological groups
  endowed with the associated right uniform structure. This means that two points `x` and `y`
  are close precisely when `y * xâ»Â¹` is close to `1` / `y + (-x)` close to `0`.
* `IsLeftUniformGroup` and `IsLeftUniformAddGroup`: Multiplicative and additive topological groups
  endowed with the associated left uniform structure. This means that two points `x` and `y`
  are close precisely when `xâ»Â¹ * y` is close to `1` / `(-x) + y` close to `0`.
* `IsUniformGroup` and `IsUniformAddGroup`: Multiplicative and additive uniform groups,
  i.e., groups with uniformly continuous `(*)` and `(â»Â¹)` / `(+)` and `(-)`. This corresponds
  to the conjunction of the two conditions above, although this result is not in Mathlib yet.

## Main results

* `IsTopologicalAddGroup.rightUniformSpace` and `comm_topologicalAddGroup_is_uniform` can be used
  to construct a canonical uniformity for a topological additive group.

See `Mathlib/Topology/Algebra/IsUniformGroup/Basic.lean` for further results.

## Implementation Notes

Since the most frequent use case is `G` being a commutative additive groups, `Mathlib` originally
did essentially all the theory under the assumption `IsUniformGroup G`.
For this reason, you may find results stated under this assumption even though they may hold
under either `IsRightUniformGroup G` or `IsLeftUniformGroup G`.
-/

@[expose] public section

assert_not_exists Cauchy

noncomputable section

open Uniformity Topology Filter Pointwise

section LeftRight

open Filter Set

variable {G Gâ‚— Gáµ£ Hâ‚— Háµ£ X : Type*}

/-- A **right-uniform additive group** is a topological additive group endowed with the associated
right uniform structure: the uniformity filter `ğ“¤ G` is the inverse image of `ğ“ 0` by the map
`(x, y) â†¦ y + (-x)`.

In other words, we declare that two points `x` and `y` are infinitely close
precisely when `y + (-x)` is infinitely close to `0`. -/
class IsRightUniformAddGroup (G : Type*) [UniformSpace G] [AddGroup G] : Prop
    extends IsTopologicalAddGroup G where
  uniformity_eq :
    ğ“¤ G = comap (fun x : G Ã— G â†¦ x.2 + (-x.1)) (ğ“ 0)

/-- A **right-uniform group** is a topological group endowed with the associated
right uniform structure: the uniformity filter `ğ“¤ G` is the inverse image of `ğ“ 1` by the map
`(x, y) â†¦ y * xâ»Â¹`.

In other words, we declare that two points `x` and `y` are infinitely close
precisely when `y * xâ»Â¹` is infinitely close to `1`. -/
@[to_additive]
class IsRightUniformGroup (G : Type*) [UniformSpace G] [Group G] : Prop
    extends IsTopologicalGroup G where
  uniformity_eq :
    ğ“¤ G = comap (fun x : G Ã— G â†¦ x.2 * x.1â»Â¹) (ğ“ 1)

/-- A **left-uniform additive group** is a topological additive group endowed with the associated
left uniform structure: the uniformity filter `ğ“¤ G` is the inverse image of `ğ“ 0` by the map
`(x, y) â†¦ (-x) + y`.

In other words, we declare that two points `x` and `y` are infinitely close
precisely when `(-x) + y` is infinitely close to `0`. -/
class IsLeftUniformAddGroup (G : Type*) [UniformSpace G] [AddGroup G] : Prop
    extends IsTopologicalAddGroup G where
  uniformity_eq :
    ğ“¤ G = comap (fun x : G Ã— G â†¦ (-x.1) + x.2) (ğ“ 0)

/-- A **left-uniform group** is a topological group endowed with the associated
left uniform structure: the uniformity filter `ğ“¤ G` is the inverse image of `ğ“ 1` by the map
`(x, y) â†¦ xâ»Â¹ * y`.

In other words, we declare that two points `x` and `y` are infinitely close
precisely when `xâ»Â¹ * y` is infinitely close to `1`. -/
@[to_additive]
class IsLeftUniformGroup (G : Type*) [UniformSpace G] [Group G] : Prop
    extends IsTopologicalGroup G where
  uniformity_eq :
    ğ“¤ G = comap (fun x : G Ã— G â†¦ x.1â»Â¹ * x.2) (ğ“ 1)

attribute [instance 10] IsRightUniformAddGroup.toIsTopologicalAddGroup
attribute [instance 10] IsRightUniformGroup.toIsTopologicalGroup
attribute [instance 10] IsLeftUniformAddGroup.toIsTopologicalAddGroup
attribute [instance 10] IsLeftUniformGroup.toIsTopologicalGroup

variable [UniformSpace Gâ‚—] [UniformSpace Gáµ£] [Group Gâ‚—] [Group Gáµ£]
variable [UniformSpace Hâ‚—] [UniformSpace Háµ£] [Group Hâ‚—] [Group Háµ£]
variable [IsLeftUniformGroup Gâ‚—] [IsRightUniformGroup Gáµ£]
variable [IsLeftUniformGroup Hâ‚—] [IsRightUniformGroup Háµ£]
variable [UniformSpace X]

variable (Gâ‚— Gáµ£)

@[to_additive]
lemma uniformity_eq_comap_mul_inv_nhds_one :
    ğ“¤ Gáµ£ = comap (fun x : Gáµ£ Ã— Gáµ£ â†¦ x.2 * x.1â»Â¹) (ğ“ 1) :=
  IsRightUniformGroup.uniformity_eq

@[to_additive]
lemma uniformity_eq_comap_inv_mul_nhds_one :
    ğ“¤ Gâ‚— = comap (fun x : Gâ‚— Ã— Gâ‚— â†¦ x.1â»Â¹ * x.2) (ğ“ 1) :=
  IsLeftUniformGroup.uniformity_eq

@[to_additive]
lemma uniformity_eq_comap_mul_inv_nhds_one_swapped :
    ğ“¤ Gáµ£ = comap (fun x : Gáµ£ Ã— Gáµ£ â†¦ x.1 * x.2â»Â¹) (ğ“ 1) := by
  rw [â† comap_swap_uniformity, uniformity_eq_comap_mul_inv_nhds_one, comap_comap, Function.comp_def]
  simp

@[to_additive]
lemma uniformity_eq_comap_inv_mul_nhds_one_swapped :
    ğ“¤ Gâ‚— = comap (fun x : Gâ‚— Ã— Gâ‚— â†¦ x.2â»Â¹ * x.1) (ğ“ 1) := by
  rw [â† comap_swap_uniformity, uniformity_eq_comap_inv_mul_nhds_one, comap_comap, Function.comp_def]
  simp

@[to_additive]
theorem uniformity_eq_comap_nhds_one : ğ“¤ Gáµ£ = comap (fun x : Gáµ£ Ã— Gáµ£ => x.2 / x.1) (ğ“ 1) := by
  simp_rw [div_eq_mul_inv]
  exact uniformity_eq_comap_mul_inv_nhds_one Gáµ£

@[to_additive]
theorem uniformity_eq_comap_nhds_one_swapped :
    ğ“¤ Gáµ£ = comap (fun x : Gáµ£ Ã— Gáµ£ => x.1 / x.2) (ğ“ 1) := by
  rw [â† comap_swap_uniformity, uniformity_eq_comap_nhds_one, comap_comap, Function.comp_def]
  simp

end LeftRight

section IsUniformGroup

open Filter Set

variable {Î± : Type*} {Î² : Type*}

/-- A uniform group is a group in which multiplication and inversion are uniformly continuous.

`IsUniformGroup G` is equivalent to the fact that `G` is a topological group, and the uniformity
coincides with **both** the associated left and right uniformities
(see `IsUniformGroup.isRightUniformGroup`, `IsUniformGroup.isLeftUniformGroup` and
`IsUniformGroup.of_left_right`).

Since there are topological groups where these two uniformities do **not** coincide,
not all topological groups admit a uniform group structure in this sense. This is however the
case for commutative groups, which are the main motivation for the existence of this
typeclass. -/
class IsUniformGroup (Î± : Type*) [UniformSpace Î±] [Group Î±] : Prop where
  uniformContinuous_div : UniformContinuous fun p : Î± Ã— Î± => p.1 / p.2

/-- A uniform additive group is an additive group in which addition and negation are
uniformly continuous.

`IsUniformAddGroup G` is equivalent to the fact that `G` is a topological additive group, and the
uniformity coincides with **both** the associated left and right uniformities
(see `IsUniformAddGroup.isRightUniformAddGroup`, `IsUniformAddGroup.isLeftUniformAddGroup` and
`IsUniformAddGroup.of_left_right`).

Since there are topological groups where these two uniformities do **not** coincide,
not all topological groups admit a uniform group structure in this sense. This is however the
case for commutative groups, which are the main motivation for the existence of this
typeclass. -/
class IsUniformAddGroup (Î± : Type*) [UniformSpace Î±] [AddGroup Î±] : Prop where
  uniformContinuous_sub : UniformContinuous fun p : Î± Ã— Î± => p.1 - p.2

attribute [to_additive] IsUniformGroup

@[to_additive]
theorem IsUniformGroup.mk' {Î±} [UniformSpace Î±] [Group Î±]
    (hâ‚ : UniformContinuous fun p : Î± Ã— Î± => p.1 * p.2) (hâ‚‚ : UniformContinuous fun p : Î± => pâ»Â¹) :
    IsUniformGroup Î± :=
  âŸ¨by simpa only [div_eq_mul_inv] using
    hâ‚.comp (uniformContinuous_fst.prodMk (hâ‚‚.comp uniformContinuous_snd))âŸ©

variable [UniformSpace Î±] [Group Î±] [IsUniformGroup Î±]

@[to_additive]
theorem uniformContinuous_div : UniformContinuous fun p : Î± Ã— Î± => p.1 / p.2 :=
  IsUniformGroup.uniformContinuous_div

@[to_additive]
theorem UniformContinuous.div [UniformSpace Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±} (hf : UniformContinuous f)
    (hg : UniformContinuous g) : UniformContinuous fun x => f x / g x :=
  uniformContinuous_div.comp (hf.prodMk hg)

@[to_additive]
theorem UniformContinuous.inv [UniformSpace Î²] {f : Î² â†’ Î±} (hf : UniformContinuous f) :
    UniformContinuous fun x => (f x)â»Â¹ := by
  have : UniformContinuous fun x => 1 / f x := uniformContinuous_const.div hf
  simp_all

@[to_additive]
theorem uniformContinuous_inv : UniformContinuous fun x : Î± => xâ»Â¹ :=
  uniformContinuous_id.inv

@[to_additive]
theorem UniformContinuous.mul [UniformSpace Î²] {f : Î² â†’ Î±} {g : Î² â†’ Î±} (hf : UniformContinuous f)
    (hg : UniformContinuous g) : UniformContinuous fun x => f x * g x := by
  have : UniformContinuous fun x => f x / (g x)â»Â¹ := hf.div hg.inv
  simp_all

@[to_additive]
theorem Finset.uniformContinuous_prod {Î± Î² Î¹ : Type*} [UniformSpace Î±] [CommGroup Î±]
    [IsUniformGroup Î±] [UniformSpace Î²] {f : Î¹ â†’ Î² â†’ Î±} (s : Finset Î¹)
    (h : âˆ€ i âˆˆ s, UniformContinuous (f i)) :
    UniformContinuous (âˆ i âˆˆ s, f i Â·) := by
  induction s using Finset.cons_induction with
  | empty => simpa using uniformContinuous_const
  | cons a s ha ih =>
    simp_rw [Finset.mem_cons, forall_eq_or_imp] at h
    simpa [Finset.prod_cons] using h.1.mul (ih h.2)

@[to_additive]
theorem uniformContinuous_mul : UniformContinuous fun p : Î± Ã— Î± => p.1 * p.2 :=
  uniformContinuous_fst.mul uniformContinuous_snd

@[to_additive]
theorem UniformContinuous.mul_const [UniformSpace Î²] {f : Î² â†’ Î±} (hf : UniformContinuous f)
    (a : Î±) : UniformContinuous fun x â†¦ f x * a :=
  hf.mul uniformContinuous_const

@[to_additive]
theorem UniformContinuous.const_mul [UniformSpace Î²] {f : Î² â†’ Î±} (hf : UniformContinuous f)
    (a : Î±) : UniformContinuous fun x â†¦ a * f x :=
  uniformContinuous_const.mul hf

@[to_additive]
theorem uniformContinuous_mul_left (a : Î±) : UniformContinuous fun b : Î± => a * b :=
  uniformContinuous_id.const_mul _

@[to_additive]
theorem uniformContinuous_mul_right (a : Î±) : UniformContinuous fun b : Î± => b * a :=
  uniformContinuous_id.mul_const _

@[to_additive]
theorem UniformContinuous.div_const [UniformSpace Î²] {f : Î² â†’ Î±} (hf : UniformContinuous f)
    (a : Î±) : UniformContinuous fun x â†¦ f x / a :=
  hf.div uniformContinuous_const

@[to_additive]
theorem uniformContinuous_div_const (a : Î±) : UniformContinuous fun b : Î± => b / a :=
  uniformContinuous_id.div_const _

@[to_additive]
theorem Filter.Tendsto.uniformity_mul {Î¹ : Type*} {f g : Î¹ â†’ Î± Ã— Î±} {l : Filter Î¹}
    (hf : Tendsto f l (ğ“¤ Î±)) (hg : Tendsto g l (ğ“¤ Î±)) :
    Tendsto (f * g) l (ğ“¤ Î±) :=
  have : Tendsto (fun (p : (Î± Ã— Î±) Ã— (Î± Ã— Î±)) â†¦ p.1 * p.2) (ğ“¤ Î± Ã—Ë¢ ğ“¤ Î±) (ğ“¤ Î±) := by
    simpa [UniformContinuous, uniformity_prod_eq_prod] using uniformContinuous_mul (Î± := Î±)
  this.comp (hf.prodMk hg)

@[to_additive]
theorem Filter.Tendsto.uniformity_inv {Î¹ : Type*} {f : Î¹ â†’ Î± Ã— Î±} {l : Filter Î¹}
    (hf : Tendsto f l (ğ“¤ Î±)) :
    Tendsto (fâ»Â¹) l (ğ“¤ Î±) :=
  have : Tendsto (Â· â»Â¹) (ğ“¤ Î±) (ğ“¤ Î±) := uniformContinuous_inv
  this.comp hf

@[to_additive]
theorem Filter.Tendsto.uniformity_inv_iff {Î¹ : Type*} {f : Î¹ â†’ Î± Ã— Î±} {l : Filter Î¹} :
    Tendsto (fâ»Â¹) l (ğ“¤ Î±) â†” Tendsto f l (ğ“¤ Î±) :=
  âŸ¨fun H â†¦ inv_inv f â–¸ H.uniformity_inv, Filter.Tendsto.uniformity_invâŸ©

@[to_additive]
theorem Filter.Tendsto.uniformity_div {Î¹ : Type*} {f g : Î¹ â†’ Î± Ã— Î±} {l : Filter Î¹}
    (hf : Tendsto f l (ğ“¤ Î±)) (hg : Tendsto g l (ğ“¤ Î±)) :
    Tendsto (f / g) l (ğ“¤ Î±) := by
  rw [div_eq_mul_inv]
  exact hf.uniformity_mul hg.uniformity_inv

/-- If `f : Î¹ â†’ G Ã— G` converges to the uniformity, then any `g : Î¹ â†’ G Ã— G` converges to the
uniformity iff `f * g` does. This is often useful when `f` is valued in the diagonal,
in which case its convergence is automatic. -/
@[to_additive /-- If `f : Î¹ â†’ G Ã— G` converges to the uniformity, then any `g : Î¹ â†’ G Ã— G`
converges to the uniformity iff `f + g` does. This is often useful when `f` is valued in the
diagonal, in which case its convergence is automatic. -/]
theorem Filter.Tendsto.uniformity_mul_iff_right {Î¹ : Type*} {f g : Î¹ â†’ Î± Ã— Î±} {l : Filter Î¹}
    (hf : Tendsto f l (ğ“¤ Î±)) :
    Tendsto (f * g) l (ğ“¤ Î±) â†” Tendsto g l (ğ“¤ Î±) :=
  âŸ¨fun hfg â†¦ by simpa using hf.uniformity_inv.uniformity_mul hfg, hf.uniformity_mulâŸ©

/-- If `g : Î¹ â†’ G Ã— G` converges to the uniformity, then any `f : Î¹ â†’ G Ã— G` converges to the
uniformity iff `f * g` does. This is often useful when `g` is valued in the diagonal,
in which case its convergence is automatic. -/
@[to_additive /-- If `g : Î¹ â†’ G Ã— G` converges to the uniformity, then any `f : Î¹ â†’ G Ã— G`
converges to the uniformity iff `f + g` does. This is often useful when `g` is valued in the
diagonal, in which case its convergence is automatic. -/]
theorem Filter.Tendsto.uniformity_mul_iff_left {Î¹ : Type*} {f g : Î¹ â†’ Î± Ã— Î±} {l : Filter Î¹}
    (hg : Tendsto g l (ğ“¤ Î±)) :
    Tendsto (f * g) l (ğ“¤ Î±) â†” Tendsto f l (ğ“¤ Î±) :=
  âŸ¨fun hfg â†¦ by simpa using hfg.uniformity_mul hg.uniformity_inv, fun hf â†¦ hf.uniformity_mul hgâŸ©

@[to_additive UniformContinuous.const_nsmul]
theorem UniformContinuous.pow_const [UniformSpace Î²] {f : Î² â†’ Î±} (hf : UniformContinuous f) :
    âˆ€ n : â„•, UniformContinuous fun x => f x ^ n
  | 0 => by
    simp_rw [pow_zero]
    exact uniformContinuous_const
  | n + 1 => by
    simp_rw [pow_succ']
    exact hf.mul (hf.pow_const n)

@[to_additive uniformContinuous_const_nsmul]
theorem uniformContinuous_pow_const (n : â„•) : UniformContinuous fun x : Î± => x ^ n :=
  uniformContinuous_id.pow_const n

@[to_additive UniformContinuous.const_zsmul]
theorem UniformContinuous.zpow_const [UniformSpace Î²] {f : Î² â†’ Î±} (hf : UniformContinuous f) :
    âˆ€ n : â„¤, UniformContinuous fun x => f x ^ n
  | (n : â„•) => by
    simp_rw [zpow_natCast]
    exact hf.pow_const _
  | Int.negSucc n => by
    simp_rw [zpow_negSucc]
    exact (hf.pow_const _).inv

@[to_additive uniformContinuous_const_zsmul]
theorem uniformContinuous_zpow_const (n : â„¤) : UniformContinuous fun x : Î± => x ^ n :=
  uniformContinuous_id.zpow_const n

@[to_additive]
instance (priority := 10) IsUniformGroup.to_topologicalGroup : IsTopologicalGroup Î± where
  continuous_mul := uniformContinuous_mul.continuous
  continuous_inv := uniformContinuous_inv.continuous

@[to_additive]
theorem uniformity_translate_mul (a : Î±) : ((ğ“¤ Î±).map fun x : Î± Ã— Î± => (x.1 * a, x.2 * a)) = ğ“¤ Î± :=
  le_antisymm (uniformContinuous_id.mul uniformContinuous_const)
    (calc
      ğ“¤ Î± =
          ((ğ“¤ Î±).map fun x : Î± Ã— Î± => (x.1 * aâ»Â¹, x.2 * aâ»Â¹)).map fun x : Î± Ã— Î± =>
            (x.1 * a, x.2 * a) := by simp [Filter.map_map, Function.comp_def]
      _ â‰¤ (ğ“¤ Î±).map fun x : Î± Ã— Î± => (x.1 * a, x.2 * a) :=
        Filter.map_mono (uniformContinuous_id.mul uniformContinuous_const))

namespace MulOpposite

@[to_additive]
instance : IsUniformGroup Î±áµáµ’áµ– :=
  âŸ¨uniformContinuous_op.comp
      ((uniformContinuous_unop.comp uniformContinuous_snd).inv.mul <|
        uniformContinuous_unop.comp uniformContinuous_fst)âŸ©

end MulOpposite

section

variable (Î±)

@[to_additive]
instance IsUniformGroup.isRightUniformGroup : IsRightUniformGroup Î± where
  uniformity_eq := by
    refine eq_of_forall_le_iff fun ğ“• â†¦ ?_
    rw [nhds_eq_comap_uniformity, comap_comap, â† tendsto_iff_comap,
      â† (tendsto_diag_uniformity Prod.fst ğ“•).uniformity_mul_iff_left, â† tendsto_id']
    congrm Tendsto ?_ _ _
    ext <;> simp

@[to_additive]
instance IsUniformGroup.isLeftUniformGroup : IsLeftUniformGroup Î± where
  uniformity_eq := by
    refine eq_of_forall_le_iff fun ğ“• â†¦ ?_
    rw [nhds_eq_comap_uniformity, comap_comap, â† tendsto_iff_comap,
      â† (tendsto_diag_uniformity Prod.fst ğ“•).uniformity_mul_iff_right, â† tendsto_id']
    congrm Tendsto ?_ _ _
    ext <;> simp

@[to_additive]
theorem IsUniformGroup.ext {G : Type*} [Group G] {u v : UniformSpace G} (hu : @IsUniformGroup G u _)
    (hv : @IsUniformGroup G v _)
    (h : @nhds _ u.toTopologicalSpace 1 = @nhds _ v.toTopologicalSpace 1) : u = v :=
  UniformSpace.ext <| by
    rw [(have := hu; uniformity_eq_comap_nhds_one), (have := hv; uniformity_eq_comap_nhds_one), h]

@[to_additive]
theorem IsUniformGroup.ext_iff {G : Type*} [Group G] {u v : UniformSpace G}
    (hu : @IsUniformGroup G u _) (hv : @IsUniformGroup G v _) :
    u = v â†” @nhds _ u.toTopologicalSpace 1 = @nhds _ v.toTopologicalSpace 1 :=
  âŸ¨fun h => h â–¸ rfl, hu.ext hvâŸ©

variable {Î±}

@[to_additive]
theorem IsUniformGroup.uniformity_countably_generated [(ğ“ (1 : Î±)).IsCountablyGenerated] :
    (ğ“¤ Î±).IsCountablyGenerated := by
  rw [uniformity_eq_comap_nhds_one]
  exact Filter.comap.isCountablyGenerated _ _

end

section OfLeftAndRight

variable [UniformSpace Î²] [Group Î²] [IsLeftUniformGroup Î²] [IsRightUniformGroup Î²]

open Prod (snd) in
/-- Note: this assumes `[IsLeftUniformGroup Î²] [IsRightUniformGroup Î²]` instead of the more typical
(and equivalent) `[IsUniformGroup Î²]` because this is used in the proof of said equivalence. -/
@[to_additive /-- Note: this assumes `[IsLeftUniformAddGroup Î²] [IsRightUniformAddGroup Î²]`
instead of the more typical (and equivalent) `[IsUniformAddGroup Î²]` because this is used
in the proof of said equivalence. -/]
theorem comap_conj_nhds_one :
    comap (fun gx : Î² Ã— Î² â†¦ gx.1 * gx.2 * gx.1â»Â¹) (ğ“ 1) = comap snd (ğ“ 1) := by
  let dr : Î² Ã— Î² â†’ Î² := fun xy â†¦ xy.2 * xy.1â»Â¹
  let dl : Î² Ã— Î² â†’ Î² := fun xy â†¦ xy.1â»Â¹ * xy.2
  let conj : Î² Ã— Î² â†’ Î² := fun gx â†¦ gx.1 * gx.2 * gx.1â»Â¹
  let Ï† : Î² Ã— Î² â‰ƒ Î² Ã— Î² := (Equiv.refl Î²).prodShear (fun b â†¦ (Equiv.mulLeft b).symm)
  have conj_Ï† : conj âˆ˜ Ï† = dr := by
    ext; simp [conj, Ï†, dr]
  have snd_Ï† : snd âˆ˜ Ï† = dl := by
    ext; simp [Ï†, dl]
  rw [â† (comap_injective Ï†.surjective).eq_iff, comap_comap, comap_comap, conj_Ï†, snd_Ï†,
      â† uniformity_eq_comap_inv_mul_nhds_one, â† uniformity_eq_comap_mul_inv_nhds_one]

open Prod (snd) in
/-- Note: this assumes `[IsLeftUniformGroup Î²] [IsRightUniformGroup Î²]` instead of the more typical
(and equivalent) `[IsUniformGroup Î²]` because this is used in the proof of said equivalence. -/
@[to_additive /-- Note: this assumes `[IsLeftUniformAddGroup Î²] [IsRightUniformAddGroup Î²]`
instead of the more typical (and equivalent) `[IsUniformAddGroup Î²]` because this is used
in the proof of said equivalence. -/]
theorem tendsto_conj_nhds_one :
    Tendsto (fun gx : Î² Ã— Î² â†¦ gx.1 * gx.2 * gx.1â»Â¹) (comap snd (ğ“ 1)) (ğ“ 1) := by
  rw [tendsto_iff_comap, comap_conj_nhds_one]

/-- Note: this assumes `[IsLeftUniformGroup Î²] [IsRightUniformGroup Î²]` instead of the more typical
(and equivalent) `[IsUniformGroup Î²]` because this is used in the proof of said equivalence. -/
@[to_additive /-- Note: this assumes `[IsLeftUniformAddGroup Î²] [IsRightUniformAddGroup Î²]`
instead of the more typical (and equivalent) `[IsUniformAddGroup Î²]` because this is used
in the proof of said equivalence. -/]
theorem Filter.Tendsto.conj_nhds_one {Î¹ : Type*} {l : Filter Î¹} {x : Î¹ â†’ Î²}
    (hx : Tendsto x l (ğ“ 1)) (g : Î¹ â†’ Î²) :
    Tendsto (g * x * gâ»Â¹) l (ğ“ 1) := by
  have : Tendsto (fun i â†¦ (g i, x i)) l (comap Prod.snd (ğ“ 1)) := by
    rwa [tendsto_comap_iff]
  -- `exact` works but is quite slow...
  convert tendsto_conj_nhds_one.comp this

theorem IsUniformGroup.of_left_right : IsUniformGroup Î² where
  uniformContinuous_div := by
    let Ï† : (Î² Ã— Î²) Ã— (Î² Ã— Î²) â†’ Î² := fun âŸ¨âŸ¨xâ‚, xâ‚‚âŸ©, âŸ¨yâ‚, yâ‚‚âŸ©âŸ© â†¦ xâ‚‚ * yâ‚‚â»Â¹ * yâ‚ * xâ‚â»Â¹
    let Ïˆ : (Î² Ã— Î²) Ã— (Î² Ã— Î²) â†’ Î² := fun âŸ¨âŸ¨xâ‚, xâ‚‚âŸ©, âŸ¨yâ‚, yâ‚‚âŸ©âŸ© â†¦ (xâ‚â»Â¹ * xâ‚‚) * (yâ‚‚â»Â¹ * yâ‚)
    let g : (Î² Ã— Î²) Ã— (Î² Ã— Î²) â†’ Î² := fun âŸ¨âŸ¨xâ‚, xâ‚‚âŸ©, âŸ¨yâ‚, yâ‚‚âŸ©âŸ© â†¦ xâ‚
    suffices Tendsto Ï† (ğ“¤ Î² Ã—Ë¢ ğ“¤ Î²) (ğ“ 1) by
      rw [UniformContinuous, uniformity_eq_comap_mul_inv_nhds_one Î², tendsto_comap_iff,
        uniformity_prod_eq_prod, tendsto_map'_iff]
      simpa [Function.comp_def, div_eq_mul_inv, â† mul_assoc]
    have Ï†_Ïˆ_conj : Ï† = g * Ïˆ * gâ»Â¹ := by
      ext
      simp [Ï†, Ïˆ, g, mul_assoc]
    have Ïˆ_tendsto : Tendsto Ïˆ (ğ“¤ Î² Ã—Ë¢ ğ“¤ Î²) (ğ“ 1) := by
      rw [â† one_mul 1]
      refine .mul ?_ ?_
      Â· rw [uniformity_eq_comap_inv_mul_nhds_one]
        exact tendsto_comap.comp tendsto_fst
      Â· rw [uniformity_eq_comap_inv_mul_nhds_one_swapped]
        exact tendsto_comap.comp tendsto_snd
    exact Ï†_Ïˆ_conj â–¸ Ïˆ_tendsto.conj_nhds_one g

theorem isUniformGroup_iff_left_right {Î³ : Type*} [Group Î³] [UniformSpace Î³] :
    IsUniformGroup Î³ â†” IsLeftUniformGroup Î³ âˆ§ IsRightUniformGroup Î³ :=
  âŸ¨fun _ â†¦ âŸ¨inferInstance, inferInstanceâŸ©, fun âŸ¨_, _âŸ© â†¦ .of_left_rightâŸ©

theorem eventually_forall_conj_nhds_one {p : Î± â†’ Prop}
    (hp : âˆ€á¶  x in ğ“ 1, p x) :
    âˆ€á¶  x in ğ“ 1, âˆ€ g, p (g * x * gâ»Â¹) := by
  simpa using tendsto_conj_nhds_one.eventually hp

end OfLeftAndRight

@[to_additive]
theorem Filter.HasBasis.uniformity_of_nhds_one {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set Î±}
    (h : (ğ“ (1 : Î±)).HasBasis p U) :
    (ğ“¤ Î±).HasBasis p fun i => { x : Î± Ã— Î± | x.2 / x.1 âˆˆ U i } := by
  rw [uniformity_eq_comap_nhds_one]
  exact h.comap _

@[to_additive]
theorem Filter.HasBasis.uniformity_of_nhds_one_inv_mul {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set Î±}
    (h : (ğ“ (1 : Î±)).HasBasis p U) :
    (ğ“¤ Î±).HasBasis p fun i => { x : Î± Ã— Î± | x.1â»Â¹ * x.2 âˆˆ U i } := by
  rw [uniformity_eq_comap_inv_mul_nhds_one]
  exact h.comap _

@[to_additive]
theorem Filter.HasBasis.uniformity_of_nhds_one_swapped {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set Î±}
    (h : (ğ“ (1 : Î±)).HasBasis p U) :
    (ğ“¤ Î±).HasBasis p fun i => { x : Î± Ã— Î± | x.1 / x.2 âˆˆ U i } := by
  rw [uniformity_eq_comap_nhds_one_swapped]
  exact h.comap _

@[to_additive]
theorem Filter.HasBasis.uniformity_of_nhds_one_inv_mul_swapped {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set Î±}
    (h : (ğ“ (1 : Î±)).HasBasis p U) :
    (ğ“¤ Î±).HasBasis p fun i => { x : Î± Ã— Î± | x.2â»Â¹ * x.1 âˆˆ U i } := by
  rw [uniformity_eq_comap_inv_mul_nhds_one_swapped]
  exact h.comap _

@[to_additive]
theorem uniformContinuous_of_tendsto_one {hom : Type*} [UniformSpace Î²] [Group Î²] [IsUniformGroup Î²]
    [FunLike hom Î± Î²] [MonoidHomClass hom Î± Î²] {f : hom} (h : Tendsto f (ğ“ 1) (ğ“ 1)) :
    UniformContinuous f := by
  have :
    ((fun x : Î² Ã— Î² => x.2 / x.1) âˆ˜ fun x : Î± Ã— Î± => (f x.1, f x.2)) = fun x : Î± Ã— Î± =>
      f (x.2 / x.1) := by ext; simp only [Function.comp_apply, map_div]
  rw [UniformContinuous, uniformity_eq_comap_nhds_one Î±, uniformity_eq_comap_nhds_one Î²,
    tendsto_comap_iff, this]
  exact Tendsto.comp h tendsto_comap

/-- A group homomorphism (a bundled morphism of a type that implements `MonoidHomClass`) between
two uniform groups is uniformly continuous provided that it is continuous at one. See also
`continuous_of_continuousAt_one`. -/
@[to_additive /-- An additive group homomorphism (a bundled morphism of a type that implements
`AddMonoidHomClass`) between two uniform additive groups is uniformly continuous provided that it
is continuous at zero. See also `continuous_of_continuousAt_zero`. -/]
theorem uniformContinuous_of_continuousAt_one {hom : Type*} [UniformSpace Î²] [Group Î²]
    [IsUniformGroup Î²] [FunLike hom Î± Î²] [MonoidHomClass hom Î± Î²]
    (f : hom) (hf : ContinuousAt f 1) :
    UniformContinuous f :=
  uniformContinuous_of_tendsto_one (by simpa using hf.tendsto)

@[to_additive]
theorem MonoidHom.uniformContinuous_of_continuousAt_one [UniformSpace Î²] [Group Î²]
    [IsUniformGroup Î²] (f : Î± â†’* Î²) (hf : ContinuousAt f 1) : UniformContinuous f :=
  _root_.uniformContinuous_of_continuousAt_one f hf

/-- A homomorphism from a uniform group to a discrete uniform group is continuous if and only if
its kernel is open. -/
@[to_additive /-- A homomorphism from a uniform additive group to a discrete uniform additive group
is continuous if and only if its kernel is open. -/]
theorem IsUniformGroup.uniformContinuous_iff_isOpen_ker {hom : Type*} [UniformSpace Î²]
    [DiscreteTopology Î²] [Group Î²] [IsUniformGroup Î²] [FunLike hom Î± Î²] [MonoidHomClass hom Î± Î²]
    {f : hom} :
    UniformContinuous f â†” IsOpen ((f : Î± â†’* Î²).ker : Set Î±) := by
  refine âŸ¨fun hf => ?_, fun hf => ?_âŸ©
  Â· apply (isOpen_discrete ({1} : Set Î²)).preimage hf.continuous
  Â· apply uniformContinuous_of_continuousAt_one
    rw [ContinuousAt, nhds_discrete Î², map_one, tendsto_pure]
    exact hf.mem_nhds (map_one f)

@[to_additive]
theorem uniformContinuous_monoidHom_of_continuous {hom : Type*} [UniformSpace Î²] [Group Î²]
    [IsUniformGroup Î²] [FunLike hom Î± Î²] [MonoidHomClass hom Î± Î²] {f : hom} (h : Continuous f) :
    UniformContinuous f :=
  uniformContinuous_of_tendsto_one <|
    suffices Tendsto f (ğ“ 1) (ğ“ (f 1)) by rwa [map_one] at this
    h.tendsto 1

@[to_additive]
theorem MonoidHom.isUniformInducing_of_isInducing {Hom : Type*} [UniformSpace Î²] [Group Î²]
    [IsUniformGroup Î²] [FunLike Hom Î± Î²] [MonoidHomClass Hom Î± Î²] {f : Hom} (h : IsInducing f) :
    IsUniformInducing f where
  comap_uniformity := by
    simp [uniformity_eq_comap_nhds_one, comap_comap, Function.comp_def, h.nhds_eq_comap]

end IsUniformGroup

section IsTopologicalGroup

open Filter

variable (G : Type*) [Group G] [TopologicalSpace G] [IsTopologicalGroup G]

/-- The right uniformity on a topological group (as opposed to the left uniformity).

Warning: in general the right and left uniformities do not coincide and so one does not obtain a
`IsUniformGroup` structure. Two important special cases where they _do_ coincide are for
commutative groups (see `isUniformGroup_of_commGroup`) and for compact groups (see
`IsUniformGroup.of_compactSpace`). -/
@[to_additive (attr := instance_reducible)
/-- The right uniformity on a topological additive group (as opposed to the left
uniformity).

Warning: in general the right and left uniformities do not coincide and so one does not obtain a
`IsUniformAddGroup` structure. Two important special cases where they _do_ coincide are for
commutative additive groups (see `isUniformAddGroup_of_addCommGroup`) and for compact
additive groups (see `IsUniformAddGroup.of_compactSpace`). -/]
def IsTopologicalGroup.rightUniformSpace : UniformSpace G where
  uniformity := comap (fun p : G Ã— G => p.2 * p.1â»Â¹) (ğ“ 1)
  symm :=
    have : Tendsto (fun p : G Ã— G â†¦ (p.2 * p.1â»Â¹)â»Â¹) (comap (fun p : G Ã— G â†¦ p.2 * p.1â»Â¹) (ğ“ 1))
      (ğ“ 1â»Â¹) := tendsto_id.inv.comp tendsto_comap
    by simpa [tendsto_comap_iff]
  comp := Tendsto.le_comap fun U H â†¦ by
    rcases exists_nhds_one_split H with âŸ¨V, V_nhds, V_mulâŸ©
    refine mem_map.2 (mem_of_superset (mem_lift' <| preimage_mem_comap V_nhds) ?_)
    rintro âŸ¨x, yâŸ© âŸ¨z, hzâ‚, hzâ‚‚âŸ©
    simpa using V_mul _ hzâ‚‚ _ hzâ‚
  nhds_eq_comap_uniformity _ := by
    simp only [comap_comap, Function.comp_def, nhds_translation_mul_inv]

@[deprecated (since := "2025-09-26")]
alias IsTopologicalAddGroup.toUniformSpace := IsTopologicalAddGroup.rightUniformSpace
@[to_additive existing, deprecated (since := "2025-09-26")]
alias IsTopologicalGroup.toUniformSpace := IsTopologicalGroup.rightUniformSpace

attribute [local instance] IsTopologicalGroup.rightUniformSpace

@[to_additive]
theorem uniformity_eq_comap_nhds_one' : ğ“¤ G = comap (fun p : G Ã— G => p.2 * p.1â»Â¹) (ğ“ (1 : G)) :=
  rfl

end IsTopologicalGroup


section IsTopologicalGroup

open Filter

variable (G : Type*) [Group G] [TopologicalSpace G] [IsTopologicalGroup G]

/-- The left uniformity on a topological group (as opposed to the right uniformity).

Warning: in general the right and left uniformities do not coincide and so one does not obtain a
`IsUniformGroup` structure. Two important special cases where they _do_ coincide are for
commutative groups (see `isUniformGroup_of_commGroup`) and for compact groups (see
`IsUniformGroup.of_compactSpace`). -/
@[to_additive (attr := instance_reducible)
/-- The left uniformity on a topological additive group (as opposed to the right
uniformity).

Warning: in general the right and left uniformities do not coincide and so one does not obtain a
`IsUniformAddGroup` structure. Two important special cases where they _do_ coincide are for
commutative additive groups (see `isUniformAddGroup_of_addCommGroup`) and for compact
additive groups (see `IsUniformAddGroup.of_compactSpace`). -/]
def IsTopologicalGroup.leftUniformSpace : UniformSpace G where
  uniformity := comap (fun p : G Ã— G => p.1â»Â¹ * p.2) (ğ“ 1)
  symm :=
    have : Tendsto (fun p : G Ã— G â†¦ (p.1â»Â¹ * p.2)â»Â¹) (comap (fun p : G Ã— G â†¦ p.1â»Â¹ * p.2) (ğ“ 1))
      (ğ“ 1â»Â¹) := tendsto_id.inv.comp tendsto_comap
    by simpa [tendsto_comap_iff]
  comp := Tendsto.le_comap fun U H â†¦ by
    rcases exists_nhds_one_split H with âŸ¨V, V_nhds, V_mulâŸ©
    refine mem_map.2 (mem_of_superset (mem_lift' <| preimage_mem_comap V_nhds) ?_)
    rintro âŸ¨x, yâŸ© âŸ¨z, hzâ‚, hzâ‚‚âŸ©
    simpa using V_mul _ hzâ‚ _ hzâ‚‚
  nhds_eq_comap_uniformity _ := by
    simp only [comap_comap, Function.comp_def, nhds_translation_inv_mul]

attribute [local instance] IsTopologicalGroup.leftUniformSpace

@[to_additive]
theorem uniformity_eq_comap_nhds_one_left :
    ğ“¤ G = comap (fun p : G Ã— G => p.1â»Â¹ * p.2) (ğ“ (1 : G)) :=
  rfl

end IsTopologicalGroup

section TopologicalCommGroup

universe u v w x

open Filter

variable (G : Type*) [CommGroup G] [TopologicalSpace G] [IsTopologicalGroup G]

section

attribute [local instance] IsTopologicalGroup.rightUniformSpace

variable {G}

@[to_additive]
theorem isUniformGroup_of_commGroup : IsUniformGroup G := by
  constructor
  have : (fun (x : (G Ã— G) Ã— (G Ã— G)) â†¦ x.1.2 * x.2.2â»Â¹ * (x.2.1 * x.1.1â»Â¹)) =
    (fun (p : G Ã— G) â†¦ p.1 * p.2â»Â¹)
      âˆ˜ (fun (p : (G Ã— G) Ã— (G Ã— G)) â†¦ (p.1.2 * p.1.1â»Â¹, p.2.2 * p.2.1â»Â¹)) := by
    ext x
    simp only [Function.comp_apply, mul_inv_rev, inv_inv]
    rw [mul_assoc, mul_comm x.2.2â»Â¹, mul_comm x.2.1]
    simp [mul_assoc]
  simp only [UniformContinuous, div_eq_mul_inv, uniformity_prod_eq_prod,
    uniformity_eq_comap_nhds_one', prod_comap_comap_eq, â† nhds_prod_eq, tendsto_comap_iff,
    Function.comp_def, mul_inv_rev, inv_inv, tendsto_map'_iff]
  rw [this]
  apply Tendsto.comp ?_ tendsto_comap
  nth_rewrite 3 [show (1 : G) = 1 * 1â»Â¹ by simp]
  apply Continuous.tendsto (by fun_prop)

alias comm_topologicalGroup_is_uniform := isUniformGroup_of_commGroup

end

@[to_additive]
theorem IsUniformGroup.rightUniformSpace_eq {G : Type*} [u : UniformSpace G] [Group G]
    [IsUniformGroup G] : IsTopologicalGroup.rightUniformSpace G = u := by
  ext : 1
  rw [uniformity_eq_comap_nhds_one' G, uniformity_eq_comap_mul_inv_nhds_one]

@[deprecated (since := "2025-09-26")]
alias IsUniformAddGroup.toUniformSpace_eq := IsUniformAddGroup.rightUniformSpace_eq
@[to_additive existing, deprecated (since := "2025-09-26")]
alias IsUniformGroup.toUniformSpace_eq := IsUniformGroup.rightUniformSpace_eq

end TopologicalCommGroup

/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
module

public import Mathlib.Topology.Constructions.SumProd
public import Mathlib.Algebra.Group.Basic

/-!
# Topological monoids - definitions

In this file we define three mixin typeclasses:

- `ContinuousMul M` says that the multiplication on `M` is continuous as a function on `M Ã— M`;
- `ContinuousAdd M` says that the addition on `M` is continuous as a function on `M Ã— M`.
- `SeparatelyContinuousMul M` says that the multiplication on `M` is continuous in each argument
  separately. This is strictly weaker than `ContinuousMul M`, but arises frequently in practice in
  functional analysis where one often considers topologies weaker than the norm topology. In these
  topologies it is frequently the case that the multiplication is not jointly continuous, but is
  continuous in each argument separately.

These classes are `Prop`-valued mixins,
i.e., they take data (`TopologicalSpace`, `Mul`/`Add`) as arguments
instead of extending typeclasses with these fields.

We also provide convenience dot notation lemmas like `Filter.Tendsto.mul` and `ContinuousAt.add`.
-/

@[expose] public section

open scoped Topology

/-- Basic hypothesis to talk about a topological additive monoid or a topological additive
semigroup. A topological additive monoid over `M`, for example, is obtained by requiring both the
instances `AddMonoid M` and `ContinuousAdd M`.

Continuity in each argument separately can be stated using `SeparatelyContinuousAdd Î±`. If one wants
only continuity in either the left or right argument, but not both one can use
`ContinuousConstVAdd Î± Î±`/`ContinuousConstVAdd Î±áµáµ’áµ– Î±`. -/
class ContinuousAdd (M : Type*) [TopologicalSpace M] [Add M] : Prop where
  continuous_add : Continuous fun p : M Ã— M => p.1 + p.2

/-- Basic hypothesis to talk about a topological monoid or a topological semigroup.
A topological monoid over `M`, for example, is obtained by requiring both the instances `Monoid M`
and `ContinuousMul M`.

Continuity in each argument separately can be stated using `SeparatelyContinuousMul Î±`. If one wants
only continuity in either the left or right argument, but not both one can use
`ContinuousConstSMul Î± Î±`/`ContinuousConstSMul Î±áµáµ’áµ– Î±`. -/
@[to_additive]
class ContinuousMul (M : Type*) [TopologicalSpace M] [Mul M] : Prop where
  continuous_mul : Continuous fun p : M Ã— M => p.1 * p.2

/-- A type class encoding that addition is continuous in each argument. This is weaker than
`ContinuousAdd`. -/
class SeparatelyContinuousAdd (M : Type*) [TopologicalSpace M] [Add M] : Prop where
  continuous_const_add {a : M} : Continuous (a + Â·)
  continuous_add_const {a : M} : Continuous (Â· + a)

/-- A type class encoding that addition is continuous in each argument. This is weaker than
`ContinuousMul`. -/
@[to_additive]
class SeparatelyContinuousMul (M : Type*) [TopologicalSpace M] [Mul M] : Prop where
  continuous_const_mul {a : M} : Continuous (a * Â·)
  continuous_mul_const {a : M} : Continuous (Â· * a)

section ContinuousMul

variable {M : Type*} [TopologicalSpace M] [Mul M] [ContinuousMul M]

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_mul : Continuous fun p : M Ã— M => p.1 * p.2 :=
  ContinuousMul.continuous_mul

@[to_additive]
theorem Filter.Tendsto.mul {Î± : Type*} {f g : Î± â†’ M} {x : Filter Î±} {a b : M}
    (hf : Tendsto f x (ð“ a)) (hg : Tendsto g x (ð“ b)) : Tendsto (fun x â†¦ f x * g x) x (ð“ (a * b)) :=
  (continuous_mul.tendsto _).comp (hf.prodMk_nhds hg)

@[to_additive]
lemma Filter.tendsto_of_div_tendsto_one {Î± E : Type*} [CommGroup E] [TopologicalSpace E]
    [ContinuousMul E] {f g : Î± â†’ E} (m : E) {x : Filter Î±} (hf : Tendsto f x (ð“ m))
    (hfg : Tendsto (g / f) x (ð“ 1)) : Tendsto g x (ð“ m) := by
  simpa using Tendsto.mul hf hfg

variable {X : Type*} [TopologicalSpace X] {f g : X â†’ M} {s : Set X} {x : X}

@[to_fun (attr := to_additive (attr := continuity, fun_prop))]
theorem Continuous.mul (hf : Continuous f) (hg : Continuous g) :
    Continuous (f * g) :=
  continuous_mul.compâ‚‚ hf hg

@[to_fun (attr := to_additive (attr := fun_prop))]
theorem ContinuousWithinAt.mul (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (f * g) s x :=
  Filter.Tendsto.mul hf hg

@[to_fun (attr := to_additive (attr := fun_prop))]
theorem ContinuousAt.mul (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (f * g) x :=
  Filter.Tendsto.mul hf hg

@[to_fun (attr := to_additive (attr := fun_prop))]
theorem ContinuousOn.mul (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (f * g) s := fun x hx â†¦
  (hf x hx).mul (hg x hx)

end ContinuousMul

section

variable {M : Type*} [TopologicalSpace M] [Mul M]

@[to_additive]
instance [ContinuousMul M] : SeparatelyContinuousMul M where
  continuous_const_mul := continuous_const.mul continuous_id
  continuous_mul_const := continuous_id.mul continuous_const

variable [SeparatelyContinuousMul M]

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_const_mul (m : M) : Continuous (m * Â·) :=
  SeparatelyContinuousMul.continuous_const_mul

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_mul_const (m : M) : Continuous (Â· * m) :=
  SeparatelyContinuousMul.continuous_mul_const

@[to_additive]
theorem Filter.Tendsto.const_mul {Î± : Type*} {f : Î± â†’ M} {x : Filter Î±} {a : M}
    (b : M) (hf : Tendsto f x (ð“ a)) : Tendsto (b * f Â·) x (ð“ (b * a)) :=
  continuous_const_mul  b |>.tendsto _ |>.comp hf

@[to_additive]
theorem Filter.Tendsto.mul_const {Î± : Type*} {f : Î± â†’ M} {x : Filter Î±} {a : M}
    (b : M) (hf : Tendsto f x (ð“ a)) : Tendsto (f Â· * b) x (ð“ (a * b)) :=
  continuous_mul_const b |>.tendsto _ |>.comp hf

variable {X : Type*} [TopologicalSpace X] {f g : X â†’ M} {s : Set X} {x : X}

@[to_additive (attr := continuity, fun_prop)]
theorem Continuous.mul_const (hf : Continuous f) (b : M) : Continuous (f Â· * b) :=
  continuous_mul_const b |>.comp hf

@[to_additive (attr := continuity, fun_prop)]
theorem Continuous.const_mul (hf : Continuous f) (b : M) : Continuous (b * f Â·) :=
  continuous_const_mul b |>.comp hf

@[to_additive (attr := fun_prop)]
theorem ContinuousWithinAt.mul_const (hf : ContinuousWithinAt f s x) (b : M) :
    ContinuousWithinAt (f Â· * b) s x :=
  Filter.Tendsto.mul_const b hf

@[to_additive (attr := fun_prop)]
theorem ContinuousWithinAt.const_mul (hf : ContinuousWithinAt f s x) (b : M) :
    ContinuousWithinAt (b * f Â·) s x :=
  Filter.Tendsto.const_mul b hf

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.mul_const (hf : ContinuousAt f x) (b : M) :
    ContinuousAt (f Â· * b) x :=
  Filter.Tendsto.mul_const b hf

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.const_mul (hf : ContinuousAt f x) (b : M) :
    ContinuousAt (b * f Â·) x :=
  Filter.Tendsto.const_mul b hf

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.mul_const (hf : ContinuousOn f s) (b : M) :
    ContinuousOn (f Â· * b) s :=
  fun x hx â†¦ (hf x hx).mul_const b

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.const_mul (hf : ContinuousOn f s) (b : M) :
    ContinuousOn (b * f Â·) s :=
  fun x hx â†¦ (hf x hx).const_mul b

/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Violeta Hern√°ndez Palacios
-/
import Mathlib.Order.Cofinal
import Mathlib.Data.Set.Finite.Lattice
import Mathlib.SetTheory.Cardinal.Arithmetic

/-!
# Cofinality

This file contains the definition of cofinality of an order and an ordinal number.

## Main Definitions

* `Order.cof Œ±` is the cofinality of a preorder. This is the smallest cardinality of an `IsCofinal`
  set `s : Set Œ±`, meaning `‚àÄ x, ‚àÉ y ‚àà s, x ‚â§ y`.
* `Ordinal.cof o` is the cofinality of the ordinal `o` when viewed as a linear order.

## Main Statements

* `Cardinal.lt_power_cof`: A consequence of K√∂nig's theorem stating that `c < c ^ c.ord.cof` for
  `c ‚â• ‚Ñµ‚ÇÄ`.

## Implementation Notes

* The cofinality is defined for ordinals.
  If `c` is a cardinal number, its cofinality is `c.ord.cof`.
-/

noncomputable section

open Function Cardinal Set Order
open scoped Ordinal

universe u v

variable {Œ± Œπ : Type u} {Œ≤ : Type v} {a o o' : Ordinal.{u}}

/-! ### Cofinality of orders -/

namespace Order

/-- The of a preorder `Œ±` is the smallest cardinality of an `IsCofinal` subset. -/
def cof (Œ± : Type*) [Preorder Œ±] : Cardinal :=
  ‚®Ö s : { s : Set Œ± // IsCofinal s }, #s.1

theorem IsCofinal.cof_le [Preorder Œ±] {s : Set Œ±} (h : IsCofinal s) : cof Œ± ‚â§ #s :=
  ciInf_le' _ (Subtype.mk s h)

theorem cof_le (Œ± : Type*) [Preorder Œ±] : cof Œ± ‚â§ #Œ± := by
  simpa using IsCofinal.univ.cof_le

theorem le_cof_iff [Preorder Œ±] {c : Cardinal} :
    c ‚â§ cof Œ± ‚Üî ‚àÄ {s : Set Œ±}, IsCofinal s ‚Üí c ‚â§ #s := by
  rw [cof, le_ciInf_iff', Subtype.forall]

@[deprecated le_cof_iff (since := "2024-12-02")]
alias le_cof := le_cof_iff

theorem lt_cof [Preorder Œ±] {s : Set Œ±} : #s < cof Œ± ‚Üí ¬¨ IsCofinal s := by
  simpa using not_imp_not.2 IsCofinal.cof_le

/-- Any order has a cofinal subset whose cardinality is its cofinality. -/
theorem cof_eq (Œ± : Type*) [Preorder Œ±] : ‚àÉ s : Set Œ±, IsCofinal s ‚àß cof Œ± = #s := by
  obtain ‚ü®‚ü®s, hs‚ü©, h‚ü© := ciInf_mem fun s : { s : Set Œ± // IsCofinal s } ‚Ü¶ #s.1
  exact ‚ü®s, hs, h.symm‚ü©

/-- Any well-order has a cofinal subset whose order type is its cofinality. -/
theorem ord_cof_eq (Œ± : Type*) [LinearOrder Œ±] [WellFoundedLT Œ±] :
    ‚àÉ s : Set Œ±, IsCofinal s ‚àß (Order.cof Œ±).ord = typeLT s := by
  obtain ‚ü®s, hs, hŒ±‚ü© := cof_eq Œ±
  obtain ‚ü®r, _, hr‚ü© := ord_eq s
  have hr' := hs.trans (isCofinal_setOf_imp_lt r)
  refine ‚ü®_, hr', le_antisymm ?_ ?_‚ü©
  ¬∑ rw [ord_le]
    exact hr'.cof_le
  ¬∑ rw [hŒ±, hr, Ordinal.type_le_iff']
    refine ‚ü®RelEmbedding.ofMonotone (inclusion ?_) ?_‚ü©
    ¬∑ simp
    ¬∑ rintro ‚ü®_, ‚ü®x, hx, rfl‚ü©‚ü© ‚ü®_, ‚ü®y, _, rfl‚ü©‚ü© h
      obtain h' | h' | h' := trichotomous_of r x y
      ¬∑ exact h'
      ¬∑ refine (h.ne ?_).elim
        rwa [Subtype.mk_eq_mk, Subtype.val_inj]
      ¬∑ cases (hx _ h').not_gt h

end Order

namespace OrderIso

private theorem cof_le_lift [Preorder Œ±] [Preorder Œ≤] (f : Œ± ‚âÉo Œ≤) :
    Cardinal.lift.{v} (Order.cof Œ±) ‚â§ Cardinal.lift.{u} (Order.cof Œ≤) := by
  rw [Order.cof, Order.cof, lift_iInf, lift_iInf, le_ciInf_iff']
  exact fun ‚ü®s, hs‚ü© ‚Ü¶ csInf_le' ‚ü®‚ü®_, f.symm.map_cofinal hs‚ü©, mk_image_eq_lift _ _ f.symm.injective‚ü©

theorem cof_eq_lift [Preorder Œ±] [Preorder Œ≤] (f : Œ± ‚âÉo Œ≤) :
    Cardinal.lift.{v} (Order.cof Œ±) = Cardinal.lift.{u} (Order.cof Œ≤) :=
  have := f.toRelEmbedding.isRefl
  (f.cof_le_lift).antisymm (f.symm.cof_le_lift)

theorem cof_eq {Œ± Œ≤ : Type u} [Preorder Œ±] [Preorder Œ≤] (f : Œ± ‚âÉo Œ≤) : Order.cof Œ± = Order.cof Œ≤ :=
  lift_inj.1 f.cof_eq_lift

end OrderIso

namespace Order

@[simp]
theorem cof_eq_zero [Preorder Œ±] [IsEmpty Œ±] : cof Œ± = 0 := by
  rw [‚Üê le_zero_iff, ‚Üê mk_emptyCollection Œ±]
  exact (IsCofinal.of_isEmpty (‚àÖ : Set Œ±)).cof_le

@[simp]
theorem cof_eq_zero_iff [Preorder Œ±] : cof Œ± = 0 ‚Üî IsEmpty Œ± := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ cof_eq_zero‚ü©
  obtain ‚ü®s, hs, hŒ±‚ü© := cof_eq Œ±
  rw [hŒ±, mk_eq_zero_iff, isEmpty_subtype, ‚Üê eq_empty_iff_forall_notMem] at h
  rwa [h, isCofinal_empty_iff] at hs

@[simp]
theorem cof_ne_zero_iff [Preorder Œ±] : cof Œ± ‚â† 0 ‚Üî Nonempty Œ± := by
  simp [cof_eq_zero_iff.not]

@[simp]
theorem cof_ne_zero [Preorder Œ±] [h : Nonempty Œ±] : cof Œ± ‚â† 0 :=
  cof_ne_zero_iff.2 h

@[simp]
theorem cof_eq_one [Preorder Œ±] [OrderTop Œ±] : cof Œ± = 1 := by
  apply le_antisymm
  ¬∑ rw [‚Üê mk_singleton (‚ä§ : Œ±)]
    exact IsCofinal.singleton_top.cof_le
  ¬∑ rw [one_le_iff_ne_zero, cof_ne_zero_iff]
    exact top_nonempty Œ±

theorem cof_eq_one_iff [Preorder Œ±] : cof Œ± = 1 ‚Üî ‚àÉ a : Œ±, IsTop a := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®a, ha‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®s, hs, hŒ±‚ü© := cof_eq Œ±
    rw [h, eq_comm, mk_set_eq_one_iff] at hŒ±
    obtain ‚ü®x, rfl‚ü© := hŒ±
    rw [isCofinal_singleton] at hs
    use x
  ¬∑ have : OrderTop Œ± := @OrderTop.mk _ _ ‚ü®a‚ü© ha
    exact cof_eq_one

end Order

/-! ### Cofinality of ordinals -/

namespace Ordinal

variable [LinearOrder Œ±] [WellFoundedLT Œ±]

/-- The cofinality of an ordinal is the `Order.cof` of any well-order with a given order type. In
particular, `cof 0 = 0` and `cof (succ o) = 1`. -/
def cof (o : Ordinal.{u}) : Cardinal.{u} :=
  o.liftOnWellOrder (fun Œ± _ _ ‚Ü¶ Order.cof Œ±) fun _ _ _ _ _ _ h ‚Ü¶ by
    obtain ‚ü®e‚ü© := typeLT_eq.1 h
    exact e.cof_eq

@[simp]
theorem cof_type (Œ± : Type*) [LinearOrder Œ±] [WellFoundedLT Œ±] : (typeLT Œ±).cof = Order.cof Œ± :=
  liftOnWellOrder_type ..

@[simp]
theorem _root_.Order.cof_toType (o : Ordinal) : Order.cof o.toType = o.cof := by
  rw [‚Üê cof_type, type_toType]

@[deprecated cof_toType (since := "2025-08-19")]
theorem cof_eq_cof_toType (o : Ordinal) : o.cof = Order.cof o.toType :=
  (cof_toType o).symm

@[simp]
theorem _root_.Order.cof_Iio_ordinal (o : Ordinal.{u}) :
    Order.cof (Iio o) = Cardinal.lift.{u + 1} o.cof := by
  convert (enumIsoToType o).cof_eq_lift
  ¬∑ rw [Cardinal.lift_id'.{u, u + 1}]
  ¬∑ rw [cof_toType]

@[simp]
theorem lift_cof (o) : Cardinal.lift.{u, v} (cof o) = cof (Ordinal.lift.{u, v} o) := by
  refine inductionOnWellOrder o fun Œ± _ _ ‚Ü¶ ?_
  rw [‚Üê typeLT_uLift, cof_type, cof_type, ‚Üê Cardinal.lift_id'.{v, u} (Order.cof (ULift _)),
    ‚Üê Cardinal.lift_umax, ‚Üê OrderIso.uLift.cof_eq_lift]

theorem cof_le_card (o : Ordinal) : cof o ‚â§ card o := by
  rw [‚Üê cof_toType, ‚Üê mk_toType]
  exact cof_le _

theorem cof_ord_le (c : Cardinal) : c.ord.cof ‚â§ c := by
  simpa using cof_le_card c.ord

theorem ord_cof_le (o : Ordinal) : o.cof.ord ‚â§ o :=
  (ord_le_ord.2 (cof_le_card o)).trans (ord_card_le o)

@[simp]
protected theorem _root_.Order.cof_cof (Œ± : Type*) [LinearOrder Œ±] [WellFoundedLT Œ±] :
    (Order.cof Œ±).ord.cof = Order.cof Œ± := by
  obtain ‚ü®s, hs, hŒ±‚ü© := ord_cof_eq Œ±
  obtain ‚ü®t, ht, hŒ±'‚ü© := cof_eq s
  apply ((hs.trans ht).cof_le.trans_eq _).antisymm'
  ¬∑ apply_fun card at hŒ±
    simpa [hŒ±] using cof_ord_le _
  ¬∑ rw [mk_image_eq Subtype.val_injective, ‚Üê hŒ±', hŒ±, cof_type]

@[simp]
theorem cof_cof (o : Ordinal) : o.cof.ord.cof = o.cof := by
  rw [‚Üê cof_toType o, Order.cof_cof]

@[simp]
theorem cof_zero : cof 0 = 0 := by
  rw [‚Üê cof_toType, cof_eq_zero]

@[simp]
theorem cof_eq_zero_iff : cof o = 0 ‚Üî o = 0 := by
  rw [‚Üê cof_toType, Order.cof_eq_zero_iff, toType_empty_iff_eq_zero]

theorem cof_ne_zero_iff : cof o ‚â† 0 ‚Üî o ‚â† 0 :=
  cof_eq_zero_iff.not

@[simp]
theorem cof_succ (o : Ordinal) : cof (succ o) = 1 := by
  rw [‚Üê cof_toType, cof_eq_one]

@[simp]
theorem cof_nat_succ (n : ‚Ñï) : cof (n + 1) = 1 :=
  cof_succ n

@[simp]
theorem cof_eq_one_iff : cof o = 1 ‚Üî ¬¨ IsSuccPrelimit o := by
  rw [‚Üê cof_toType, not_isSuccPrelimit_iff']
  constructor
  ¬∑ rw [Order.cof_eq_one_iff]
    refine fun ‚ü®a, ha‚ü© ‚Ü¶ ‚ü®(enumIsoToType _).symm a, le_antisymm ?_ ?_‚ü©
    ¬∑ simp
    ¬∑ refine le_of_forall_lt fun b hb ‚Ü¶ ?_
      rw [lt_succ_iff]
      change ‚ü®b, hb‚ü© ‚â§ o.enumIsoToType.symm a
      rw [OrderIso.le_symm_apply]
      exact ha _
  ¬∑ aesop

@[simp]
theorem cof_le_one_iff : cof o ‚â§ 1 ‚Üî ¬¨ IsSuccLimit o := by
  rw [Cardinal.le_one_iff, cof_eq_zero_iff, cof_eq_one_iff, isSuccLimit_iff]
  tauto

@[simp]
theorem cof_lt_aleph0_iff : cof o < ‚Ñµ‚ÇÄ ‚Üî cof o ‚â§ 1 := by
  refine ‚ü®fun h ‚Ü¶ ?_, one_lt_aleph0.trans_le'‚ü©
  obtain ‚ü®n, hn‚ü© := Cardinal.lt_aleph0.1 h
  apply_fun cof ‚àò ord at hn
  cases n
  ¬∑ simp_all
  ¬∑ simp_rw [comp_apply, ord_nat, Nat.cast_succ, cof_nat_succ, cof_cof] at hn
    rw [hn]

@[simp]
theorem one_lt_cof_iff : 1 < cof o ‚Üî ‚Ñµ‚ÇÄ ‚â§ cof o := by
  rw [‚Üê not_lt, cof_lt_aleph0_iff, not_le]

theorem aleph0_le_cof_iff : ‚Ñµ‚ÇÄ ‚â§ cof o ‚Üî IsSuccLimit o := by
  rw [‚Üê one_lt_cof_iff, ‚Üê not_iff_not, not_lt, cof_le_one_iff]

theorem _root_.Order.cof_lt_aleph0_iff : Order.cof Œ± < ‚Ñµ‚ÇÄ ‚Üî Order.cof Œ± ‚â§ 1 := by
  rw [‚Üê cof_type, Ordinal.cof_lt_aleph0_iff]

theorem _root_.Order.one_lt_cof_iff : 1 < Order.cof Œ± ‚Üî ‚Ñµ‚ÇÄ ‚â§ Order.cof Œ± := by
  rw [‚Üê cof_type, Ordinal.one_lt_cof_iff]

/-! ### Cofinality of suprema and least strict upper bounds -/

/-- The range of an indexed supremum is cofinal within the supremum. -/
theorem isCofinal_range_iSup {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < ‚®Ü i, f i) :
    IsCofinal (range fun i ‚Ü¶ enumIsoToType _ ‚ü®_, H i‚ü©) := by
  intro x
  have H' := ((enumIsoToType _).symm x).2
  rw [mem_Iio, lt_ciSup_iff'] at H'
  ¬∑ obtain ‚ü®i, hi‚ü© := H'
    use enumIsoToType _ ‚ü®_, H i‚ü©
    simpa [‚Üê (enumIsoToType _).symm.le_iff_le] using hi.le
  ¬∑ use iSup f
    rintro _ ‚ü®i, rfl‚ü©
    exact (H i).le

theorem cof_iSup_le_lift {f : Œπ ‚Üí Ordinal.{v}} (H : ‚àÄ i, f i < ‚®Ü i, f i) :
    Cardinal.lift.{u} (cof (‚®Ü i, f i)) ‚â§ Cardinal.lift.{v} #Œπ := by
  rw [‚Üê cof_toType]
  exact (Cardinal.lift_le.2 (isCofinal_range_iSup H).cof_le).trans mk_range_le_lift

theorem cof_iSup_le {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < ‚®Ü i, f i) : cof (‚®Ü i, f i) ‚â§ #Œπ := by
  simpa using cof_iSup_le_lift H

theorem cof_iSup_Iio_le {f : Iio a ‚Üí Ordinal} (H : ‚àÄ i, f i < ‚®Ü i, f i) :
    cof (‚®Ü i, f i) ‚â§ a.card := by
  convert cof_iSup_le_lift H
  rw [Cardinal.lift_id'.{u, u + 1}, mk_Iio_ordinal, Cardinal.lift_le]

theorem iSup_lt_of_lt_cof_lift {f : Œπ ‚Üí Ordinal} {o : Ordinal.{v}} (H : ‚àÄ i, f i < o)
    (h : Cardinal.lift.{v} #Œπ < Cardinal.lift.{u} o.cof) : ‚®Ü i, f i < o := by
  apply (ciSup_le' fun i ‚Ü¶ (H i).le).lt_of_ne
  rintro rfl
  exact (cof_iSup_le_lift H).not_gt h

theorem iSup_lt_of_lt_cof {Œπ} {f : Œπ ‚Üí Ordinal} (H : ‚àÄ i, f i < o) (h : #Œπ < o.cof) :
    ‚®Ü i, f i < o := by
  apply iSup_lt_of_lt_cof_lift H
  simpa

theorem iSup_Iio_lt_of_lt_cof {f : Iio a ‚Üí Ordinal} (H : ‚àÄ i, f i < o) (h : a < o.cof.ord) :
    ‚®Ü i, f i < o := by
  apply iSup_lt_of_lt_cof_lift H
  rwa [Cardinal.lift_id'.{u, u + 1}, mk_Iio_ordinal, Cardinal.lift_lt, ‚Üê lt_ord]

/-! ### Fundamental sequences -/

/-- A fundamental sequence for an ordinal `a` is a strictly monotonic function from `Iio a.cof` to
`Iio a` with cofinal range. We provide `o = a.cof` explicitly to avoid type rewrites. -/
structure IsFundamentalSeq (f : Iio o ‚Üí Iio a) : Prop where
  /-- This, alongside the other conditions, implies `o = a.cof.ord`. -/
  le_cof : o ‚â§ a.cof.ord
  /-- A fundamental sequence is strictly monotonic. -/
  strictMono : StrictMono f
  /-- A fundamental sequence has cofinal range. -/
  isCofinal_range : IsCofinal (range f)

namespace IsFundamentalSeq

variable {f : Iio o ‚Üí Iio a}

theorem monotone (h : IsFundamentalSeq f) : Monotone f :=
  h.strictMono.monotone

theorem cof_eq (h : IsFundamentalSeq f) : o = a.cof.ord := by
  apply h.le_cof.antisymm
  have := h.isCofinal_range.cof_le.trans mk_range_le
  rwa [cof_Iio_ordinal, mk_Iio_ordinal, Cardinal.lift_le, ‚Üê ord_le] at this

theorem id_of_le_cof (h : o ‚â§ o.cof.ord) : IsFundamentalSeq (@id (Iio o)) :=
  ‚ü®h, strictMono_id, by simp‚ü©

/-- The empty sequence is a fundamental sequence for `0`. -/
protected theorem zero (f : Iio 0 ‚Üí Iio 0) : IsFundamentalSeq f :=
  ‚ü®by simp, isEmptyElim, isEmptyElim‚ü©

/-- The sequence `{o}` is a fundamental sequence for `succ o`. -/
protected theorem succ : IsFundamentalSeq fun _ : Iio 1 ‚Ü¶ ‚ü®o, lt_succ o‚ü© := by
  refine ‚ü®?_, Subsingleton.strictMono _, ?_‚ü© <;> simp [IsTop]

/-- The composition of fundamental sequences is a fundamental sequence. -/
theorem trans {g : Iio o' ‚Üí Iio o} (hf : IsFundamentalSeq f) (hg : IsFundamentalSeq g) :
    IsFundamentalSeq (f ‚àò g) := by
  refine ‚ü®?_, hf.strictMono.comp hg.strictMono, fun x ‚Ü¶ ?_‚ü©
  ¬∑ rw [hg.cof_eq, hf.cof_eq, cof_cof]
  ¬∑ obtain ‚ü®_, ‚ü®y, rfl‚ü©, hx‚ü© := hf.isCofinal_range x
    obtain ‚ü®_, ‚ü®z, rfl‚ü©, hy‚ü© := hg.isCofinal_range y
    exact ‚ü®_, mem_range_self z, hx.trans (hf.monotone hy)‚ü©

protected theorem iSup (hf : IsFundamentalSeq f) (ho : IsSuccLimit o) : ‚®Ü i, (f i).1 = a := by
  apply (ciSup_le' fun i ‚Ü¶ (f i).2.le).antisymm
  apply le_of_forall_lt fun x hx ‚Ü¶ ?_
  rw [lt_ciSup_iff']
  ¬∑ obtain ‚ü®_, ‚ü®y, rfl‚ü©, hy‚ü© := hf.isCofinal_range ‚ü®x, hx‚ü©
    exact ‚ü®‚ü®_, ho.succ_lt y.2‚ü©, hy.trans_lt (hf.strictMono (lt_succ y.1))‚ü©
  ¬∑ use a
    rintro _ ‚ü®i, rfl‚ü©
    exact (f i).2.le

end IsFundamentalSeq

/-- Every ordinal has a fundamental sequence. -/
theorem exists_isFundamentalSeq (o : Ordinal) :
    ‚àÉ f : Iio o.cof.ord ‚Üí Iio o, IsFundamentalSeq f := by
  obtain ‚ü®s, hs, ho‚ü© := ord_cof_eq o.toType
  rw [cof_toType] at ho
  rw [ho]
  let g := OrderIso.ofRelIsoLT (enum (Œ± := s) (¬∑ < ¬∑))
  refine ‚ü®fun x ‚Ü¶ (enumIsoToType _).symm (g x), ho.ge, ?_, fun x ‚Ü¶ ?_‚ü©
  ¬∑ exact (OrderIso.strictMono _).comp g.strictMono
  ¬∑ obtain ‚ü®y, hy, hx‚ü© := hs (enumIsoToType o x)
    refine ‚ü®(enumIsoToType o).symm y, ‚ü®g.symm ‚ü®y, hy‚ü©, ?_‚ü©, ?_‚ü© <;>
      simp [‚Üê o.enumIsoToType.le_iff_le, hx]

theorem IsNormal.cof_le {f : Ordinal ‚Üí Ordinal} (hf : IsNormal f) : cof o ‚â§ cof (f o) := by
  obtain rfl | ‚ü®a, rfl‚ü© | ho := zero_or_succ_or_isSuccLimit o
  ¬∑ simp
  ¬∑ rw [cof_succ, Cardinal.one_le_iff_ne_zero, cof_ne_zero_iff]
    exact (hf.strictMono (lt_succ a)).ne_bot
  ¬∑ obtain ‚ü®g, hg‚ü© := exists_isFundamentalSeq (f o)
    have H (x : Iio (f o)) : ‚àÉ y : Iio o, x < f y := by simpa using (hf.limit_lt ho).1 x.2
    choose s hs using H
    have hs' : ‚®Ü i, (s (g i)).1 = o := by
      apply (ciSup_le' fun x ‚Ü¶ (s (g x)).2.le).antisymm
      apply le_of_forall_lt fun x hx ‚Ü¶ ?_
      rw [lt_ciSup_iff']
      ¬∑ obtain ‚ü®_, ‚ü®y, rfl‚ü©, h : f x ‚â§ g y‚ü© := hg.isCofinal_range ‚ü®f x, hf.strictMono hx‚ü©
        exact ‚ü®y, hf.lt_iff.1 <| h.trans_lt (hs (g y))‚ü©
      ¬∑ use o
        rintro _ ‚ü®x, rfl‚ü©
        exact (s (g x)).2.le
    convert cof_iSup_Iio_le (f := fun x ‚Ü¶ s (g x)) _ using 1
    ¬∑ rw [hs']
    ¬∑ rw [card_ord]
    ¬∑ simpa only [hs'] using fun x ‚Ü¶ (s (g x)).2

/-- If `g` is a fundamental sequence for `o` and `f` is normal, then `f ‚àò g` is a fundamental
sequence for `f o`. -/
protected theorem IsNormal.isFundamentalSeq {f : Ordinal ‚Üí Ordinal} (hf : IsNormal f)
    (ho : IsSuccLimit o) {g : Iio a ‚Üí Iio o} (hg : IsFundamentalSeq g) :
    IsFundamentalSeq fun x : Iio a ‚Ü¶ ‚ü®f (g x), hf.strictMono (g x).2‚ü© := by
  refine ‚ü®?_, fun x y h ‚Ü¶ hf.strictMono (hg.strictMono h), fun x ‚Ü¶ ?_‚ü©
  ¬∑ rw [hg.cof_eq, ord_le_ord]
    exact hf.cof_le
  ¬∑ obtain ‚ü®y, hy, hx‚ü© := (hf.limit_lt ho).1 x.2
    obtain ‚ü®_, ‚ü®z, rfl‚ü©, hz‚ü© := hg.isCofinal_range ‚ü®y, hy‚ü©
    exact ‚ü®_, mem_range_self z, hx.le.trans (hf.monotone hz)‚ü©

theorem IsNormal.cof_eq {f : Ordinal ‚Üí Ordinal} (hf : IsNormal f) (ho : IsSuccLimit o) :
    cof (f o) = cof o := by
  obtain ‚ü®g, hg‚ü© := exists_isFundamentalSeq o
  exact (ord_injective (hf.isFundamentalSeq ho hg).cof_eq).symm

@[simp]
theorem cof_add {b : Ordinal} (h : b ‚â† 0) : cof (a + b) = cof b := by
  obtain rfl | ‚ü®c, rfl‚ü© | hb := zero_or_succ_or_isSuccLimit b
  ¬∑ contradiction
  ¬∑ rw [add_succ, cof_succ, cof_succ]
  ¬∑ exact (isNormal_add_right a).cof_eq hb

@[simp]
theorem cof_preOmega {o : Ordinal} (ho : IsSuccLimit o) : (preOmega o).cof = o.cof :=
  isNormal_preOmega.cof_eq ho

@[simp]
theorem cof_omega {o : Ordinal} (ho : IsSuccLimit o) : (œâ_ o).cof = o.cof :=
  isNormal_omega.cof_eq ho

@[simp]
theorem cof_omega0 : cof œâ = ‚Ñµ‚ÇÄ := by
  apply (aleph0_le_cof_iff.2 isSuccLimit_omega0).antisymm'
  rw [‚Üê card_omega0]
  apply cof_le_card

@[simp]
theorem cof_univ : cof univ.{u, v} = Cardinal.univ.{u, v} := by
  apply le_antisymm (cof_le_card _)
  obtain ‚ü®s, hs, ho‚ü© := cof_eq Ordinal.{u}
  rw [‚Üê not_bddAbove_iff_isCofinal, bddAbove_iff_small, small_iff_lift_mk_lt_univ,
    Cardinal.lift_id, ‚Üê ho, not_lt, ‚Üê Cardinal.lift_le.{v}, Cardinal.lift_univ,
    Cardinal.univ_umax] at hs
  rwa [card_univ, univ, ‚Üê lift_cof, cof_type]

end Ordinal

namespace Cardinal
open Ordinal

/-! ### Results on sets -/

-- TODO: these should be rewritten in terms of the `IsCofinal` predicate.

theorem mk_bounded_subset {Œ± : Type*} (h : ‚àÄ x < #Œ±, 2 ^ x < #Œ±) {r : Œ± ‚Üí Œ± ‚Üí Prop}
    [IsWellOrder Œ± r] (hr : (#Œ±).ord = type r) : #{ s : Set Œ± // Bounded r s } = #Œ± := by
  rcases eq_or_ne #Œ± 0 with (ha | ha)
  ¬∑ rw [ha]
    haveI := mk_eq_zero_iff.1 ha
    rw [mk_eq_zero_iff]
    constructor
    rintro ‚ü®s, hs‚ü©
    exact (not_unbounded_iff s).2 hs (unbounded_of_isEmpty s)
  have h' : IsStrongLimit #Œ± := ‚ü®ha, @h‚ü©
  have ha := h'.aleph0_le
  apply le_antisymm
  ¬∑ have : { s : Set Œ± | Bounded r s } = ‚ãÉ i, ùí´{ j | r j i } := setOf_exists _
    rw [‚Üê coe_setOf, this]
    refine mk_iUnion_le_sum_mk.trans ((sum_le_iSup (fun i => #(ùí´{ j | r j i }))).trans
      ((mul_le_max_of_aleph0_le_left ha).trans ?_))
    rw [max_eq_left]
    apply ciSup_le' _
    intro i
    rw [mk_powerset]
    apply (h'.two_power_lt _).le
    rw [coe_setOf, card_typein, ‚Üê lt_ord, hr]
    apply typein_lt_type
  ¬∑ refine @mk_le_of_injective Œ± _ (fun x => Subtype.mk {x} ?_) ?_
    ¬∑ apply bounded_singleton
      rw [‚Üê hr]
      apply isSuccLimit_ord ha
    ¬∑ intro a b hab
      simpa [singleton_eq_singleton_iff] using hab

/-! ### Consequences of K√∂nig's lemma -/

theorem lt_power_cof {c : Cardinal.{u}} : ‚Ñµ‚ÇÄ ‚â§ c ‚Üí c < c ^ c.ord.cof := by
  classical
  induction c using inductionOn with | h Œ±
  intro h
  obtain ‚ü®r, _, hr‚ü© := ord_eq Œ±
  let := linearOrderOfSTO r
  obtain ‚ü®s, hs, hŒ±‚ü© := cof_eq Œ±
  have H := sum_lt_prod (fun a : s ‚Ü¶ #(Iic a.1)) (fun _ ‚Ü¶ #Œ±) fun i ‚Ü¶ ?_
  ¬∑ simp only [prod_const, lift_id, ‚Üê mk_sigma, ‚Üê hŒ±, hr] at H ‚ä¢
    refine H.trans_le' ‚ü®Embedding.ofSurjective (fun x ‚Ü¶ x.2.1) fun a ‚Ü¶ ?_‚ü©
    obtain ‚ü®b, hb, ha‚ü© := hs a
    exact ‚ü®‚ü®‚ü®b, hb‚ü©, ‚ü®a, ha‚ü©‚ü©, rfl‚ü©
  ¬∑ have := typein_lt_type r i
    rw [‚Üê hr, lt_ord, ‚Üê card_typein] at this
    simp_rw [‚Üê Iio_insert]
    exact mk_insert_le.trans_lt (add_lt_of_lt h this (one_lt_aleph0.trans_le h))

theorem lt_cof_power {a b : Cardinal} (ha : ‚Ñµ‚ÇÄ ‚â§ a) (b1 : 1 < b) : a < (b ^ a).ord.cof := by
  have b0 : b ‚â† 0 := (zero_lt_one.trans b1).ne'
  apply lt_imp_lt_of_le_imp_le (power_le_power_left <| power_ne_zero a b0)
  rw [‚Üê power_mul, mul_eq_self ha]
  exact lt_power_cof (ha.trans <| (cantor' _ b1).le)

end Cardinal

/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
module

public import Mathlib.Analysis.InnerProductSpace.Basic
public import Mathlib.Analysis.Normed.Operator.BoundedLinearMaps

/-!
# Continuity of inner product

We show that the inner product is continuous, `continuous_inner`.

## Tags

inner product space, Hilbert space, norm

-/

@[expose] public section

noncomputable section

open RCLike Real Filter Topology ComplexConjugate Finsupp
open LinearMap renaming BilinForm â†’ BilinForm

variable {ğ•œ E F : Type*} [RCLike ğ•œ]


section Continuous

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-!
### Continuity of the inner product
-/

/-- When an inner product space `E` over `ğ•œ` is considered as a real normed space, its inner
product satisfies `IsBoundedBilinearMap`.

In order to state these results, we need a `NormedSpace â„ E` instance. We will later establish
such an instance by restriction-of-scalars, `InnerProductSpace.rclikeToReal ğ•œ E`, but this
instance may be not definitionally equal to some other â€œnaturalâ€ instance. So, we assume
`[NormedSpace â„ E]`.
-/
theorem _root_.isBoundedBilinearMap_inner [NormedSpace â„ E] [IsScalarTower â„ ğ•œ E] :
    IsBoundedBilinearMap â„ fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  { add_left := inner_add_left
    smul_left := fun r x y => by
      simp only [â† algebraMap_smul ğ•œ r x, algebraMap_eq_ofReal, inner_smul_real_left]
    add_right := inner_add_right
    smul_right := fun r x y => by
      simp only [â† algebraMap_smul ğ•œ r y, algebraMap_eq_ofReal, inner_smul_real_right]
    bound :=
      âŸ¨1, zero_lt_one, fun x y => by
        rw [one_mul]
        exact norm_inner_le_norm x yâŸ© }

theorem continuous_inner : Continuous fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ğ•œ E
  letI : IsScalarTower â„ ğ•œ E := RestrictScalars.isScalarTower _ _ _
  isBoundedBilinearMap_inner.continuous

variable {Î± : Type*}

theorem Filter.Tendsto.inner {f g : Î± â†’ E} {l : Filter Î±} {x y : E} (hf : Tendsto f l (ğ“ x))
    (hg : Tendsto g l (ğ“ y)) : Tendsto (fun t => âŸªf t, g tâŸ«) l (ğ“ âŸªx, yâŸ«) :=
  (continuous_inner.tendsto _).comp (hf.prodMk_nhds hg)

variable [TopologicalSpace Î±] {f g : Î± â†’ E} {x : Î±} {s : Set Î±}

theorem ContinuousWithinAt.inner (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun t => âŸªf t, g tâŸ«) s x :=
  Filter.Tendsto.inner hf hg

@[fun_prop]
theorem ContinuousAt.inner (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun t => âŸªf t, g tâŸ«) x :=
  Filter.Tendsto.inner hf hg

@[fun_prop]
theorem ContinuousOn.inner (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun t => âŸªf t, g tâŸ«) s := fun x hx => (hf x hx).inner (hg x hx)

@[continuity, fun_prop]
theorem Continuous.inner (hf : Continuous f) (hg : Continuous g) : Continuous fun t => âŸªf t, g tâŸ« :=
  continuous_iff_continuousAt.2 fun _x => by fun_prop

end Continuous

open Submodule

variable {ğ•œ E F Î¹ : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace â„ F]
variable {x y : E} {S : Set E} {f : Î¹ â†’ E}

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable (ğ•œ) in
theorem Dense.eq_zero_of_inner_left (hS : Dense S) (h : âˆ€ v âˆˆ S, âŸªx, vâŸ« = 0) : x = 0 := by
  let K := span ğ•œ S
  have hK : Dense (K : Set E) := hS.mono subset_span
  have : (âŸªx, Â·âŸ«) = 0 := (continuous_const.inner continuous_id).ext_on
    hK continuous_const fun v â†¦ Submodule.span_induction h (by simp)
      (by simp +contextual [inner_add_right]) (by simp +contextual [inner_smul_right])
  simpa using congr_fun this x

variable (ğ•œ) in
theorem Dense.eq_zero_of_inner_right (hS : Dense S) (h : âˆ€ v âˆˆ S, âŸªv, xâŸ« = 0) : x = 0 :=
  hS.eq_zero_of_inner_left ğ•œ fun v hv â†¦ by rw! [â† inner_conj_symm]; simp [-inner_conj_symm, h, hv]

variable (ğ•œ) in
theorem Dense.eq_of_inner_left (hS : Dense S) (h : âˆ€ v âˆˆ S, âŸªx, vâŸ« = âŸªy, vâŸ«) : x = y := by
  rw [â† sub_eq_zero]; exact hS.eq_zero_of_inner_left ğ•œ (by simpa [inner_sub_left, sub_eq_zero])

variable (ğ•œ) in
theorem Dense.eq_of_inner_right (hS : Dense S) (h : âˆ€ v âˆˆ S, âŸªv, xâŸ« = âŸªv, yâŸ«) : x = y := by
  rw [â† sub_eq_zero]; exact hS.eq_zero_of_inner_right ğ•œ (by simpa [inner_sub_right, sub_eq_zero])

nonrec theorem DenseRange.eq_zero_of_inner_left (hf : DenseRange f) (h : âˆ€ i, âŸªx, f iâŸ« = 0) :
    x = 0 := hf.eq_zero_of_inner_left ğ•œ (by simpa)

nonrec theorem DenseRange.eq_zero_of_inner_right (hf : DenseRange f) (h : âˆ€ i, âŸªf i, xâŸ« = 0) :
    x = 0 := hf.eq_zero_of_inner_right ğ•œ (by simpa)

/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
module

public import Mathlib.CategoryTheory.Sites.Descent.DescentData
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.ChosenPullback

/-!
# Descent data when we have pullbacks

In this file, given a pseudofunctor `F` from `LocallyDiscrete C·µí·µñ` to `Cat`,
a family of maps `f i : X i ‚ü∂ S` in the category `C`, chosen pullbacks `sq`
and threefold wide pullbacks `sq‚ÇÉ` for these morphisms, we define a
category `F.DescentData' sq sq‚ÇÉ` of objects over the `X i`
equipped with a descent data relative to the morphisms `f i : X i ‚ü∂ S`, where
the data and compatibilities are expressed using the chosen pullbacks.

## TODO
* show that this category is equivalent to `F.DescentData f`.

-/

@[expose] public section

universe t v' v u' u

namespace CategoryTheory

open Opposite Limits

namespace Pseudofunctor

open LocallyDiscreteOpToCat

variable {C : Type u} [Category.{v} C] (F : Pseudofunctor (LocallyDiscrete C·µí·µñ) Cat.{v', u'})
  {Œπ : Type t} {S : C} {X : Œπ ‚Üí C} {f : ‚àÄ i, X i ‚ü∂ S}
  (sq : ‚àÄ i j, ChosenPullback (f i) (f j))
  (sq‚ÇÉ : ‚àÄ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ), ChosenPullback‚ÇÉ (sq i‚ÇÅ i‚ÇÇ) (sq i‚ÇÇ i‚ÇÉ) (sq i‚ÇÅ i‚ÇÉ))

namespace DescentData'

variable {F sq}

section

variable {obj obj' : ‚àÄ (i : Œπ), F.obj (.mk (op (X i)))}
  (hom : ‚àÄ (i j : Œπ), (F.map (sq i j).p‚ÇÅ.op.toLoc).toFunctor.obj (obj i) ‚ü∂
    (F.map (sq i j).p‚ÇÇ.op.toLoc).toFunctor.obj (obj' j))

/-- Variant of `Pseudofunctor.LocallyDiscreteOpToCat.pullHom` when we have
chosen pullbacks. -/
noncomputable def pullHom' ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
    (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q := by cat_disch) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q := by cat_disch) :
    (F.map f‚ÇÅ.op.toLoc).toFunctor.obj (obj i‚ÇÅ) ‚ü∂ (F.map f‚ÇÇ.op.toLoc).toFunctor.obj (obj' i‚ÇÇ) :=
  pullHom (hom i‚ÇÅ i‚ÇÇ) ((sq i‚ÇÅ i‚ÇÇ).isPullback.lift f‚ÇÅ f‚ÇÇ (by cat_disch)) f‚ÇÅ f‚ÇÇ

@[reassoc]
lemma pullHom'_eq_pullHom ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
    (p : Y ‚ü∂ (sq i‚ÇÅ i‚ÇÇ).pullback)
    (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q := by cat_disch) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q := by cat_disch)
    (hp‚ÇÅ : p ‚â´ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ = f‚ÇÅ := by cat_disch)
    (hp‚ÇÇ : p ‚â´ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ = f‚ÇÇ := by cat_disch) :
  pullHom' hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ =
    pullHom (hom i‚ÇÅ i‚ÇÇ) p f‚ÇÅ f‚ÇÇ := by
  obtain rfl : p = (sq i‚ÇÅ i‚ÇÇ).isPullback.lift f‚ÇÅ f‚ÇÇ (by rw [hf‚ÇÅ, hf‚ÇÇ]) := by
    apply (sq i‚ÇÅ i‚ÇÇ).isPullback.hom_ext <;> cat_disch
  rfl

@[reassoc]
lemma pullHom'‚ÇÅ‚ÇÇ_eq_pullHom_of_chosenPullback‚ÇÉ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ) :
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ =
      pullHom (hom i‚ÇÅ i‚ÇÇ) (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ‚ÇÇ _ _ :=
  pullHom'_eq_pullHom _ _ _ ..

@[reassoc]
lemma pullHom'‚ÇÅ‚ÇÉ_eq_pullHom_of_chosenPullback‚ÇÉ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ) :
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ =
      pullHom (hom i‚ÇÅ i‚ÇÉ) (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ‚ÇÉ _ _ :=
  pullHom'_eq_pullHom _ _ _ ..

@[reassoc]
lemma pullHom'‚ÇÇ‚ÇÉ_eq_pullHom_of_chosenPullback‚ÇÉ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ) :
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ =
      pullHom (hom i‚ÇÇ i‚ÇÉ) (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ‚ÇÉ _ _ :=
  pullHom'_eq_pullHom _ _ _ ..

@[reassoc]
lemma pullHom_pullHom' ‚¶ÉY Y' : C‚¶Ñ (g : Y' ‚ü∂ Y) (q : Y ‚ü∂ S) (q' : Y' ‚ü∂ S)
    ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ) (gf‚ÇÅ : Y' ‚ü∂ X i‚ÇÅ) (gf‚ÇÇ : Y' ‚ü∂ X i‚ÇÇ)
    (hq : g ‚â´ q = q' := by cat_disch)
    (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q := by cat_disch) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q := by cat_disch)
    (hgf‚ÇÅ : g ‚â´ f‚ÇÅ = gf‚ÇÅ := by cat_disch) (hgf‚ÇÇ : g ‚â´ f‚ÇÇ = gf‚ÇÇ := by cat_disch) :
    pullHom (pullHom' hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ) g gf‚ÇÅ gf‚ÇÇ =
      pullHom' hom q' gf‚ÇÅ gf‚ÇÇ := by
  let p := (sq i‚ÇÅ i‚ÇÇ).isPullback.lift f‚ÇÅ f‚ÇÇ (by cat_disch)
  rw [pullHom'_eq_pullHom _ _ _ _ p, pullHom'_eq_pullHom _ _ _ _ (g ‚â´ p)]
  dsimp [pullHom]
  simp only [Functor.map_comp, Category.assoc]
  rw [F.mapComp'‚ÇÄ‚ÇÇ‚ÇÉ_hom_comp_mapComp'_hom_whiskerRight_app_assoc
    _ _ _ _ _ _ (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, IsPullback.lift_fst])
    rfl (by cat_disch),
    F.mapComp'_inv_whiskerRight_mapComp'‚ÇÄ‚ÇÇ‚ÇÉ_inv_app _ _ _ _ _ _
      (by rw [‚Üê Quiver.Hom.comp_toLoc, ‚Üê op_comp, IsPullback.lift_snd]) rfl (by cat_disch)]
  simp

end

section

variable {obj : ‚àÄ (i : Œπ), F.obj (.mk (op (X i)))}
  (hom : ‚àÄ (i j : Œπ), (F.map (sq i j).p‚ÇÅ.op.toLoc).toFunctor.obj (obj i) ‚ü∂
    (F.map (sq i j).p‚ÇÇ.op.toLoc).toFunctor.obj (obj j))

@[simp]
lemma pullHom'_p‚ÇÅ_p‚ÇÇ (i‚ÇÅ i‚ÇÇ : Œπ) :
    pullHom' hom (sq i‚ÇÅ i‚ÇÇ).p (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ (by simp) (by simp) = hom i‚ÇÅ i‚ÇÇ := by
  simp [pullHom'_eq_pullHom hom (sq i‚ÇÅ i‚ÇÇ).p (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ (ùüô _)]

lemma pullHom'_self' (hom_self : ‚àÄ i, pullHom' hom (f i) (ùüô (X i)) (ùüô (X i)) = ùüô _)
    ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi : Œπ‚¶Ñ (g : Y ‚ü∂ X i) (hg : g ‚â´ f i = q := by cat_disch) :
    pullHom' hom q g g hg hg = ùüô _ := by
  simp [‚Üê pullHom_pullHom' hom g (f i) q (ùüô (X i)) (ùüô (X i)) g g hg, hom_self, pullHom]

variable {sq‚ÇÉ} in
@[reassoc]
lemma comp_pullHom'' (hom_comp : ‚àÄ (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ),
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ ‚â´
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ =
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ)
    ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ)
    (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ) (f‚ÇÉ : Y ‚ü∂ X i‚ÇÉ) (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q)
    (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q) (hf‚ÇÉ : f‚ÇÉ ‚â´ f i‚ÇÉ = q) :
    pullHom' hom q f‚ÇÅ f‚ÇÇ ‚â´ pullHom' hom q f‚ÇÇ f‚ÇÉ = pullHom' hom q f‚ÇÅ f‚ÇÉ := by
  obtain ‚ü®œÜ, _, _, _‚ü© := (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).exists_lift f‚ÇÅ f‚ÇÇ f‚ÇÉ q hf‚ÇÅ hf‚ÇÇ hf‚ÇÉ
  rw [‚Üê pullHom_pullHom'_assoc hom œÜ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p _ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ _ _,
    ‚Üê pullHom_pullHom' hom œÜ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p _ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ _ _,
    ‚Üê pullHom_pullHom' hom œÜ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p _ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ _ _]
  dsimp [pullHom]
  simp [‚Üê Functor.map_comp_assoc, hom_comp]

end

end DescentData'

open DescentData' in
/-- Given a pseudofunctor `F` from `LocallyDiscrete C·µí·µñ` to `Cat`, a family of
morphisms `f i : X i ‚ü∂ S`, chosen pullbacks `sq i j` of `f i` and `f j` for all `i` and `j`,
and chosen threefold wide pullbacks `sq‚ÇÉ`, this structure contains a description
of objects over the `X i` equipped with a descent data relative to the morphisms `f i`,
where the (iso)morphisms are compatibilities are considered over the chosen pullbacks. -/
structure DescentData' where
  /-- The objects over `X i` for all `i` -/
  obj (i : Œπ) : F.obj (.mk (op (X i)))
  /-- The compatibility morphisms after pulling back to the chosen pullbacks. It follows
  from the conditions `pullHom'_hom_self` and `pullHom'_hom_comp` that these morphisms
  are isomorphisms. -/
  hom : ‚àÄ (i j : Œπ), (F.map (sq i j).p‚ÇÅ.op.toLoc).toFunctor.obj (obj i) ‚ü∂
    (F.map (sq i j).p‚ÇÇ.op.toLoc).toFunctor.obj (obj j)
  pullHom'_hom_self : ‚àÄ i, pullHom' hom (f i) (ùüô (X i)) (ùüô (X i)) = ùüô _
  pullHom'_hom_comp (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ) :
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ ‚â´
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÇ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ =
    pullHom' hom (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÅ (sq‚ÇÉ i‚ÇÅ i‚ÇÇ i‚ÇÉ).p‚ÇÉ

namespace DescentData'

attribute [reassoc] pullHom'_hom_comp

variable {F sq sq‚ÇÉ}

@[simp]
lemma pullHom'_self (D : F.DescentData' sq sq‚ÇÉ)
    ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi : Œπ‚¶Ñ (g : Y ‚ü∂ X i) (hg : g ‚â´ f i = q := by cat_disch) :
    pullHom' D.hom q g g hg hg = ùüô _ :=
  pullHom'_self' _ D.pullHom'_hom_self _ _

@[reassoc (attr := simp)]
lemma comp_pullHom' (D : F.DescentData' sq sq‚ÇÉ)
    ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ)
    (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ) (f‚ÇÉ : Y ‚ü∂ X i‚ÇÉ) (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q := by cat_disch)
    (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q := by cat_disch) (hf‚ÇÉ : f‚ÇÉ ‚â´ f i‚ÇÉ = q := by cat_disch) :
    pullHom' D.hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ ‚â´ pullHom' D.hom q f‚ÇÇ f‚ÇÉ hf‚ÇÇ hf‚ÇÉ =
      pullHom' D.hom q f‚ÇÅ f‚ÇÉ hf‚ÇÅ hf‚ÇÉ :=
  comp_pullHom'' _ D.pullHom'_hom_comp _ _ _ _ hf‚ÇÅ hf‚ÇÇ hf‚ÇÉ

instance (D : F.DescentData' sq sq‚ÇÉ)
    {Y : C} (q : Y ‚ü∂ S) (i‚ÇÅ i‚ÇÇ : Œπ) (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ)
    (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ) (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q)
    (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q) :
    IsIso (pullHom' D.hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ) :=
  ‚ü®pullHom' D.hom q f‚ÇÇ f‚ÇÅ hf‚ÇÇ hf‚ÇÅ, by simp, by simp‚ü©

lemma pullHom'_eq_hom (D : F.DescentData' sq sq‚ÇÉ) (i‚ÇÅ i‚ÇÇ : Œπ) :
    pullHom' D.hom _ _ _ (by simp) = D.hom i‚ÇÅ i‚ÇÇ := by
  simp

instance (D : F.DescentData' sq sq‚ÇÉ) (i‚ÇÅ i‚ÇÇ : Œπ) :
    IsIso (D.hom i‚ÇÅ i‚ÇÇ) := by
  simpa using inferInstanceAs (IsIso (pullHom' D.hom (sq i‚ÇÅ i‚ÇÇ).p (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ))

/-- The type of morphisms in the category `F.DescentData' sq sq‚ÇÉ`. -/
@[ext]
structure Hom (D‚ÇÅ D‚ÇÇ : F.DescentData' sq sq‚ÇÉ) where
  /-- The morphisms between the `obj` fields of descent data. -/
  hom (i : Œπ) : D‚ÇÅ.obj i ‚ü∂ D‚ÇÇ.obj i
  comm (i‚ÇÅ i‚ÇÇ : Œπ) :
    (F.map (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ.op.toLoc).toFunctor.map (hom i‚ÇÅ) ‚â´ D‚ÇÇ.hom i‚ÇÅ i‚ÇÇ =
    D‚ÇÅ.hom i‚ÇÅ i‚ÇÇ ‚â´ (F.map (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ.op.toLoc).toFunctor.map (hom i‚ÇÇ) := by cat_disch

attribute [reassoc (attr := simp)] Hom.comm

instance : Quiver (F.DescentData' sq sq‚ÇÉ) where
  Hom := Hom

@[simps id_hom comp_hom]
instance : Category (F.DescentData' sq sq‚ÇÉ) where
  id _ := { hom _ := ùüô _ }
  comp f g := { hom i := f.hom i ‚â´ g.hom i }

attribute [reassoc] comp_hom

@[ext]
lemma hom_ext {D‚ÇÅ D‚ÇÇ : F.DescentData' sq sq‚ÇÉ} {f g : D‚ÇÅ ‚ü∂ D‚ÇÇ}
    (h : ‚àÄ i, f.hom i = g.hom i) : f = g :=
  Hom.ext (funext h)

@[reassoc]
lemma comm {D‚ÇÅ D‚ÇÇ : F.DescentData' sq sq‚ÇÉ} (œÜ : D‚ÇÅ ‚ü∂ D‚ÇÇ)
    ‚¶ÉY : C‚¶Ñ (q : Y ‚ü∂ S) ‚¶Éi‚ÇÅ i‚ÇÇ : Œπ‚¶Ñ (f‚ÇÅ : Y ‚ü∂ X i‚ÇÅ) (f‚ÇÇ : Y ‚ü∂ X i‚ÇÇ)
    (hf‚ÇÅ : f‚ÇÅ ‚â´ f i‚ÇÅ = q := by cat_disch) (hf‚ÇÇ : f‚ÇÇ ‚â´ f i‚ÇÇ = q := by cat_disch) :
    (F.map f‚ÇÅ.op.toLoc).toFunctor.map (œÜ.hom i‚ÇÅ) ‚â´ pullHom' D‚ÇÇ.hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ =
      pullHom' D‚ÇÅ.hom q f‚ÇÅ f‚ÇÇ hf‚ÇÅ hf‚ÇÇ ‚â´ (F.map f‚ÇÇ.op.toLoc).toFunctor.map (œÜ.hom i‚ÇÇ) := by
  obtain ‚ü®p, _, _‚ü©  := (sq i‚ÇÅ i‚ÇÇ).isPullback.exists_lift f‚ÇÅ f‚ÇÇ (by cat_disch)
  rw [‚Üê pullHom_pullHom' D‚ÇÇ.hom p (sq i‚ÇÅ i‚ÇÇ).p q  (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ f‚ÇÅ f‚ÇÇ,
    ‚Üê pullHom_pullHom' D‚ÇÅ.hom p (sq i‚ÇÅ i‚ÇÇ).p q  (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ f‚ÇÅ f‚ÇÇ,
    pullHom'_p‚ÇÅ_p‚ÇÇ, pullHom'_p‚ÇÅ_p‚ÇÇ]
  dsimp only [pullHom]
  rw [NatTrans.naturality_assoc]
  dsimp
  rw [‚Üê Functor.map_comp_assoc, œÜ.comm, Functor.map_comp_assoc, mapComp'_inv_naturality]
  simp only [Category.assoc]

/-- Constructor for isomorphisms in the category `F.DescentData' sq sq‚ÇÉ`. -/
@[simps]
def isoMk {D‚ÇÅ D‚ÇÇ : F.DescentData' sq sq‚ÇÉ} (e : ‚àÄ (i : Œπ), D‚ÇÅ.obj i ‚âÖ D‚ÇÇ.obj i)
    (comm : ‚àÄ (i‚ÇÅ i‚ÇÇ : Œπ), (F.map (sq i‚ÇÅ i‚ÇÇ).p‚ÇÅ.op.toLoc).toFunctor.map (e i‚ÇÅ).hom ‚â´ D‚ÇÇ.hom i‚ÇÅ i‚ÇÇ =
      D‚ÇÅ.hom i‚ÇÅ i‚ÇÇ ‚â´ (F.map (sq i‚ÇÅ i‚ÇÇ).p‚ÇÇ.op.toLoc).toFunctor.map (e i‚ÇÇ).hom := by cat_disch) :
    D‚ÇÅ ‚âÖ D‚ÇÇ where
  hom.hom i := (e i).hom
  hom.comm := comm
  inv.hom i := (e i).inv
  inv.comm i‚ÇÅ i‚ÇÇ := by
    rw [‚Üê cancel_mono ((F.map _).toFunctor.map (e i‚ÇÇ).hom), Category.assoc,
      Category.assoc, Iso.map_inv_hom_id, Category.comp_id,
      ‚Üê cancel_epi ((F.map _).toFunctor.map (e i‚ÇÅ).hom),
      Iso.map_hom_inv_id_assoc, comm i‚ÇÅ i‚ÇÇ]

end DescentData'

end Pseudofunctor

end CategoryTheory

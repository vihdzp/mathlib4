/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
module

public import Mathlib.CategoryTheory.Bicategory.Adjunction.Cat
public import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete
public import Mathlib.CategoryTheory.Monad.Adjunction

/-!
# Descent data as coalgebras

Let `F : LocallyDiscrete C·µí·µñ ‚•§·µñ Adj Cat` be a pseudofunctor
to the bicategory of adjunctions in `Cat`. In particular,
for any morphism `g : X ‚ü∂ Y` in `C`, we have an
adjunction `(g^*, g_*)` between a pullback functor and a
pushforward functor.

In this file, given a family of morphisms `f i : X i ‚ü∂ S` indexed
by a type `Œπ` in `C`, we introduce a category `F.DescentDataAsCoalgebra f`
of descent data relative to the morphisms `f i`, where the objects are
described as a family of objects `obj i` over `X i`, and the
morphisms relating them are described as morphisms
`obj i‚ÇÅ ‚ü∂ (f i‚ÇÅ)^* (f i‚ÇÇ)_* (obj i‚ÇÇ)`, similarly as
Eilenberg-Moore coalgebras. Indeed, when the index type `Œπ`
contains a unique element, we show that
`F.DescentDataAsCoalgebra (fun (i : Œπ) ‚Ü¶ f`
identifies to the category of coalgebras for the comonad attached
to the adjunction `(F.map f.op.toLoc).adj`.

## TODO (@joelriou, @chrisflav)
* Compare `DescentDataAsCoalgebra` with `DescentData` when suitable
pullbacks exist and certain base change morphisms are isomorphims

-/

@[expose] public section

universe t v' v u' u

namespace CategoryTheory

open Bicategory Opposite

namespace Pseudofunctor

variable {C : Type u} [Category.{v} C]
  {F : LocallyDiscrete C·µí·µñ ‚•§·µñ Adj Cat.{v', u'}}

variable (F) in
/-- Given a pseudofunctor `F : LocallyDiscrete C·µí·µñ ‚•§·µñ Adj Cat` and a family
of morphisms `f i : X i ‚ü∂ S` in `C`, this is the category of descent data for `F`
relative to the morphisms `f i` where the objects are defined as coalgebras:
the morphisms relating the various objects `obj i` over `X i` are of the
form `obj i‚ÇÅ ‚ü∂ (f i‚ÇÅ)^* (f i‚ÇÇ)_* (obj i‚ÇÇ)`. This category can be compared
to the corresponding category `DescentData` when suitable pullbacks exist
and certain base change morphisms are isomorphisms (TODO). -/
structure DescentDataAsCoalgebra
    {Œπ : Type t} {S : C} {X : Œπ ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ S) where
  /-- The objects over `X i` for all `i` -/
  obj (i : Œπ) : (F.obj (.mk (op (X i)))).obj
  /-- The compatibility morphisms. -/
  hom (i‚ÇÅ i‚ÇÇ : Œπ) : obj i‚ÇÅ ‚ü∂
    (F.map (f i‚ÇÅ).op.toLoc).l.toFunctor.obj
      ((F.map (f i‚ÇÇ).op.toLoc).r.toFunctor.obj (obj i‚ÇÇ))
  counit (i : Œπ) : hom i i ‚â´ (F.map (f i).op.toLoc).adj.counit.toNatTrans.app _ = ùüô _ :=
    by cat_disch
  coassoc (i‚ÇÅ i‚ÇÇ i‚ÇÉ : Œπ) :
    hom i‚ÇÅ i‚ÇÇ ‚â´ (F.map (f i‚ÇÅ).op.toLoc).l.toFunctor.map
      ((F.map (f i‚ÇÇ).op.toLoc).r.toFunctor.map (hom i‚ÇÇ i‚ÇÉ)) =
    hom i‚ÇÅ i‚ÇÉ ‚â´
      (F.map (f i‚ÇÅ).op.toLoc).l.toFunctor.map
        ((F.map (f i‚ÇÇ).op.toLoc).adj.unit.toNatTrans.app _) := by cat_disch

namespace DescentDataAsCoalgebra

attribute [reassoc (attr := simp)] counit coassoc

section

variable {Œπ : Type t} {S : C} {X : Œπ ‚Üí C} {f : ‚àÄ i, X i ‚ü∂ S}

/-- The type of morphisms in `DescentDataAsCoalgebra`. -/
@[ext]
structure Hom (D‚ÇÅ D‚ÇÇ : F.DescentDataAsCoalgebra f) where
  /-- The morphisms between the `obj` fields of descent data. -/
  hom (i : Œπ) : D‚ÇÅ.obj i ‚ü∂ D‚ÇÇ.obj i
  comm (i‚ÇÅ i‚ÇÇ : Œπ) :
    D‚ÇÅ.hom i‚ÇÅ i‚ÇÇ ‚â´
      (F.map (f i‚ÇÅ).op.toLoc).l.toFunctor.map
        ((F.map (f i‚ÇÇ).op.toLoc).r.toFunctor.map (hom i‚ÇÇ)) =
    hom i‚ÇÅ ‚â´ D‚ÇÇ.hom i‚ÇÅ i‚ÇÇ := by cat_disch

attribute [reassoc (attr := simp)] Hom.comm

instance : Category (F.DescentDataAsCoalgebra f) where
  Hom := Hom
  id _ := { hom _ := ùüô _ }
  comp f g := { hom i := f.hom i ‚â´ g.hom i }

@[ext]
lemma hom_ext {D‚ÇÅ D‚ÇÇ : F.DescentDataAsCoalgebra f} {œÜ œÜ' : D‚ÇÅ ‚ü∂ D‚ÇÇ}
    (h : ‚àÄ i, œÜ.hom i = œÜ'.hom i) : œÜ = œÜ' :=
  Hom.ext (funext h)

@[simp]
lemma id_hom (D : F.DescentDataAsCoalgebra f) (i : Œπ) :
    Hom.hom (ùüô D) i = ùüô _ := rfl

@[reassoc, simp]
lemma comp_hom {D‚ÇÅ D‚ÇÇ D‚ÇÉ : F.DescentDataAsCoalgebra f} (œÜ : D‚ÇÅ ‚ü∂ D‚ÇÇ) (œÜ' : D‚ÇÇ ‚ü∂ D‚ÇÉ) (i : Œπ) :
    (œÜ ‚â´ œÜ').hom i = œÜ.hom i ‚â´ œÜ'.hom i := rfl

/-- Constructor for isomorphisms in `DescentDataAsCoalgebra`. -/
@[simps]
def isoMk {D‚ÇÅ D‚ÇÇ : F.DescentDataAsCoalgebra f} (e : ‚àÄ (i : Œπ), D‚ÇÅ.obj i ‚âÖ D‚ÇÇ.obj i)
    (comm : ‚àÄ (i‚ÇÅ i‚ÇÇ : Œπ), D‚ÇÅ.hom i‚ÇÅ i‚ÇÇ ‚â´
      (F.map (f i‚ÇÅ).op.toLoc).l.toFunctor.map ((F.map (f i‚ÇÇ).op.toLoc).r.toFunctor.map (e i‚ÇÇ).hom) =
      (e i‚ÇÅ).hom ‚â´ D‚ÇÇ.hom i‚ÇÅ i‚ÇÇ := by cat_disch) :
    D‚ÇÅ ‚âÖ D‚ÇÇ where
  hom.hom i := (e i).hom
  hom.comm := comm
  inv.hom i := (e i).inv
  inv.comm i‚ÇÅ i‚ÇÇ := by
    rw [‚Üê cancel_epi (e i‚ÇÅ).hom, ‚Üê reassoc_of% (comm i‚ÇÅ i‚ÇÇ), ‚Üê Functor.map_comp, ‚Üê Functor.map_comp]
    simp

end

set_option backward.isDefEq.respectTransparency false in
variable (F) in
/-- When the index type `Œπ` contains a unique element, the category
`DescentDataAsCoalgebra` identifies to the category of coalgebras
over the comonoad corresponding to the adjunction
`(F.map f.op.toLoc).adj`. -/
@[simps! functor_obj_A functor_obj_a functor_map_f inverse_obj_obj inverse_obj_hom
  inverse_map_hom counitIso_hom_app_f counitIso_inv_app_f,
  simps! -isSimp unitIso_hom_app_hom unitIso_inv_app_hom]
def coalgebraEquivalence (Œπ : Type*) [Unique Œπ] {X S : C} (f : X ‚ü∂ S) :
    F.DescentDataAsCoalgebra (fun (_ : Œπ) ‚Ü¶ f) ‚âå
    (Adjunction.ofCat (F.map f.op.toLoc).adj).toComonad.Coalgebra where
  functor.obj D :=
    { A := D.obj default
      a := D.hom default default }
  functor.map œÜ := { f := œÜ.hom default }
  inverse.obj A :=
    { obj _ := A.A
      hom _ _ := A.a
      counit _ := A.counit
      coassoc _ _ _ := A.coassoc.symm }
  inverse.map œÜ :=
    { hom i := œÜ.f
      comm _ _ := œÜ.h }
  unitIso :=
    NatIso.ofComponents
      (fun D ‚Ü¶ isoMk (fun i ‚Ü¶ eqToIso (congr_arg D.obj (by subsingleton)))
        (fun i‚ÇÅ i‚ÇÇ ‚Ü¶ by
          obtain rfl := Subsingleton.elim i‚ÇÅ default
          obtain rfl := Subsingleton.elim i‚ÇÇ default
          simp)) (fun {D‚ÇÅ D‚ÇÇ} Œ± ‚Ü¶ by
      ext i
      obtain rfl := Subsingleton.elim i default
      simp)
  counitIso := Iso.refl _

end DescentDataAsCoalgebra

set_option backward.isDefEq.respectTransparency false in
variable (F) in
/-- The functor `(F.obj (.mk (op S))).obj ‚•§ F.DescentDataAsCoalgebra f`
when `f i : X i ‚ü∂ S` is a family of morphisms. -/
@[simps]
def toDescentDataAsCoalgebra
    {Œπ : Type t} {S : C} {X : Œπ ‚Üí C} (f : ‚àÄ i, X i ‚ü∂ S) :
    (F.obj (.mk (op S))).obj ‚•§ F.DescentDataAsCoalgebra f where
  obj M :=
    { obj i := (F.map (f i).op.toLoc).l.toFunctor.obj M
      hom i‚ÇÅ i‚ÇÇ :=
        (F.map (f i‚ÇÅ).op.toLoc).l.toFunctor.map
          ((F.map (f i‚ÇÇ).op.toLoc).adj.unit.toNatTrans.app _)
      coassoc i‚ÇÅ i‚ÇÇ i‚ÇÉ := by
        rw [‚Üê Functor.map_comp, ‚Üê Functor.map_comp, Adj.unit_naturality] }
  map g :=
    { hom i := (F.map (f i).op.toLoc).l.toFunctor.map g
      comm i‚ÇÅ i‚ÇÇ := by simp [‚Üê Functor.map_comp] }

section

variable (Œπ : Type*) [Unique Œπ] {X S : C} (f : X ‚ü∂ S)

/-- When `Œπ` contains a unique element and `f : X ‚ü∂ S` is a morphim,
the composition of `F.toDescentDataAsCoalgebra (fun (_ : Œπ) ‚Ü¶ f)`
and the functor of the equivalence
`DescentDataAsCoalgebra.coalgebraEquivalence F Œπ f` identifies to
`Comonad.comparison` applied to the adjunction corresponding to
`F.map f.op.toLoc`. -/
@[simps!]
def toDescentDataAsCoalgebraCompCoalgebraEquivalenceFunctorIso
    (Œπ : Type*) [Unique Œπ] {X S : C} (f : X ‚ü∂ S) :
    F.toDescentDataAsCoalgebra (fun (_ : Œπ) ‚Ü¶ f) ‚ãô
      (DescentDataAsCoalgebra.coalgebraEquivalence F Œπ f).functor ‚âÖ
    Comonad.comparison (Adjunction.ofCat (F.map f.op.toLoc).adj) :=
  Iso.refl _

lemma isEquivalence_toDescentDataAsCoalgebra_iff_isEquivalence_comonadComparison :
    (F.toDescentDataAsCoalgebra (fun (_ : Œπ) ‚Ü¶ f)).IsEquivalence ‚Üî
    (Comonad.comparison (Adjunction.ofCat (F.map f.op.toLoc).adj)).IsEquivalence := by
  rw [‚Üê Functor.isEquivalence_iff_of_iso
    (F.toDescentDataAsCoalgebraCompCoalgebraEquivalenceFunctorIso Œπ f)]
  exact ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶
    Functor.isEquivalence_of_comp_right _
      ((DescentDataAsCoalgebra.coalgebraEquivalence F Œπ f).functor)‚ü©

end

end Pseudofunctor

end CategoryTheory

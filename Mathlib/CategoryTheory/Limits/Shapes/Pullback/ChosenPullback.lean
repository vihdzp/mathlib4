/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.Basic

/-!
# Chosen pullbacks

Given two morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ S` and `f‚ÇÇ : X‚ÇÇ ‚ü∂ S`, we introduce
a structure `ChosenPullback f‚ÇÅ f‚ÇÇ` which contains the data of
pullback of `f‚ÇÅ` and `f‚ÇÇ`.

## TODO
* relate this to `ChosenPullbacksAlong` which is defined in
`LocallyCartesianClosed.ChosenPullbacksAlong`.

-/

@[expose] public section

universe v u

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]

/-- Given two morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ S` and `f‚ÇÇ : X‚ÇÇ ‚ü∂ S`, this is the choice
of a pullback of `f‚ÇÅ` and `f‚ÇÇ`. -/
structure ChosenPullback {X‚ÇÅ X‚ÇÇ S : C} (f‚ÇÅ : X‚ÇÅ ‚ü∂ S) (f‚ÇÇ : X‚ÇÇ ‚ü∂ S) where
  /-- the pullback -/
  pullback : C
  /-- the first projection -/
  p‚ÇÅ : pullback ‚ü∂ X‚ÇÅ
  /-- the second projection -/
  p‚ÇÇ : pullback ‚ü∂ X‚ÇÇ
  condition : p‚ÇÅ ‚â´ f‚ÇÅ = p‚ÇÇ ‚â´ f‚ÇÇ
  /-- `pullback` is a pullback of `f‚ÇÅ` and `f‚ÇÇ` -/
  isLimit : IsLimit (PullbackCone.mk _ _ condition)
  /-- the projection `pullback ‚ü∂ S` -/
  p : pullback ‚ü∂ S := p‚ÇÅ ‚â´ f‚ÇÅ
  hp‚ÇÅ : p‚ÇÅ ‚â´ f‚ÇÅ = p := by cat_disch

namespace ChosenPullback

section

variable {X‚ÇÅ X‚ÇÇ S : C} {f‚ÇÅ : X‚ÇÅ ‚ü∂ S} {f‚ÇÇ : X‚ÇÇ ‚ü∂ S}
  (h : ChosenPullback f‚ÇÅ f‚ÇÇ)

attribute [reassoc] condition

lemma isPullback : IsPullback h.p‚ÇÅ h.p‚ÇÇ f‚ÇÅ f‚ÇÇ where
  w := h.condition
  isLimit' := ‚ü®h.isLimit‚ü©

attribute [reassoc (attr := simp, grind =)] hp‚ÇÅ

@[reassoc (attr := simp, grind =)]
lemma hp‚ÇÇ : h.p‚ÇÇ ‚â´ f‚ÇÇ = h.p := by rw [‚Üê h.condition, hp‚ÇÅ]

@[ext]
lemma hom_ext {Y : C} {f g : Y ‚ü∂ h.pullback}
    (h‚ÇÅ : f ‚â´ h.p‚ÇÅ = g ‚â´ h.p‚ÇÅ) (h‚ÇÇ : f ‚â´ h.p‚ÇÇ = g ‚â´ h.p‚ÇÇ) :
    f = g :=
  h.isPullback.hom_ext h‚ÇÅ h‚ÇÇ

/-- Given `f‚ÇÅ : X‚ÇÅ ‚ü∂ S`, `f‚ÇÇ : X‚ÇÇ ‚ü∂ S`, `h : ChosenPullback f‚ÇÅ f‚ÇÇ` and morphisms
`g‚ÇÅ : Y ‚ü∂ X‚ÇÅ`, `g‚ÇÇ : Y ‚ü∂ X‚ÇÇ` and `b : Y ‚ü∂ S`, this structure contains a morphism
`Y ‚ü∂ h.pullback` such that `f ‚â´ h.p‚ÇÅ = g‚ÇÅ`, `f ‚â´ h.p‚ÇÇ = g‚ÇÇ` and `f ‚â´ h.p = b`.
(This is nonempty only when `g‚ÇÅ ‚â´ f‚ÇÅ = g‚ÇÇ ‚â´ f‚ÇÇ = b`.) -/
structure LiftStruct {Y : C} (g‚ÇÅ : Y ‚ü∂ X‚ÇÅ) (g‚ÇÇ : Y ‚ü∂ X‚ÇÇ) (b : Y ‚ü∂ S) where
  /-- a lifting to the pullback -/
  f : Y ‚ü∂ h.pullback
  f_p‚ÇÅ : f ‚â´ h.p‚ÇÅ = g‚ÇÅ := by cat_disch
  f_p‚ÇÇ : f ‚â´ h.p‚ÇÇ = g‚ÇÇ := by cat_disch
  f_p : f ‚â´ h.p = b := by cat_disch

namespace LiftStruct

attribute [reassoc (attr := simp, grind =)] f_p‚ÇÅ f_p‚ÇÇ f_p

variable {h} {Y : C} {g‚ÇÅ : Y ‚ü∂ X‚ÇÅ} {g‚ÇÇ : Y ‚ü∂ X‚ÇÇ} {b : Y ‚ü∂ S}

@[reassoc]
lemma w (l : h.LiftStruct g‚ÇÅ g‚ÇÇ b) : g‚ÇÅ ‚â´ f‚ÇÅ = g‚ÇÇ ‚â´ f‚ÇÇ := by
  simp only [‚Üê l.f_p‚ÇÅ, ‚Üê l.f_p‚ÇÇ, Category.assoc, h.condition]

instance : Subsingleton (h.LiftStruct g‚ÇÅ g‚ÇÇ b) where
  allEq := by
    rintro ‚ü®f, f_p‚ÇÅ, f_p‚ÇÇ, _‚ü© ‚ü®f', f'_p‚ÇÅ, f'_p‚ÇÇ, _‚ü©
    obtain rfl : f = f' := by cat_disch
    rfl

lemma nonempty (w : g‚ÇÅ ‚â´ f‚ÇÅ = g‚ÇÇ ‚â´ f‚ÇÇ) (hf‚ÇÅ : g‚ÇÅ ‚â´ f‚ÇÅ = b) :
    Nonempty (h.LiftStruct g‚ÇÅ g‚ÇÇ b) := by
  obtain ‚ü®l, h‚ÇÅ, h‚ÇÇ‚ü© := h.isPullback.exists_lift g‚ÇÅ g‚ÇÇ w
  exact ‚ü®{
    f := l
    f_p‚ÇÅ := h‚ÇÅ
    f_p‚ÇÇ := h‚ÇÇ
    f_p := by rw [‚Üê h.hp‚ÇÅ, ‚Üê hf‚ÇÅ, reassoc_of% h‚ÇÅ] }‚ü©

end LiftStruct

end

variable {X S : C} {f : X ‚ü∂ S} (h : ChosenPullback f f)

/-- Given `f : X ‚ü∂ S` and `h : ChosenPullback f f`, this is the type of
morphisms `l : X ‚ü∂ h.pullback` that are equal to the diagonal map. -/
abbrev Diagonal := h.LiftStruct (ùüô X) (ùüô X) f

instance : Nonempty h.Diagonal := by apply LiftStruct.nonempty <;> cat_disch

end ChosenPullback

variable {X‚ÇÅ X‚ÇÇ X‚ÇÉ S : C} {f‚ÇÅ : X‚ÇÅ ‚ü∂ S} {f‚ÇÇ : X‚ÇÇ ‚ü∂ S} {f‚ÇÉ : X‚ÇÉ ‚ü∂ S}
  (h‚ÇÅ‚ÇÇ : ChosenPullback f‚ÇÅ f‚ÇÇ) (h‚ÇÇ‚ÇÉ : ChosenPullback f‚ÇÇ f‚ÇÉ) (h‚ÇÅ‚ÇÉ : ChosenPullback f‚ÇÅ f‚ÇÉ)

/-- Given three morphisms `f‚ÇÅ : X‚ÇÅ ‚ü∂ S`, `f‚ÇÇ : X‚ÇÇ ‚ü∂ S` and `f‚ÇÉ : X‚ÇÉ ‚ü∂ S`, and chosen
pullbacks for `(f‚ÇÅ, f‚ÇÇ)`, `(f‚ÇÇ, f‚ÇÉ)` and `(f‚ÇÅ, f‚ÇÉ)`, this structure contains the data
of a wide pullback of the three morphisms `f‚ÇÅ`, `f‚ÇÇ` and `f‚ÇÉ`. -/
structure ChosenPullback‚ÇÉ where
  /-- A chosen pullback of `h‚ÇÅ‚ÇÇ.pullback` and `h‚ÇÇ‚ÇÉ.pullback` over `X‚ÇÇ`. -/
  chosenPullback : ChosenPullback h‚ÇÅ‚ÇÇ.p‚ÇÇ h‚ÇÇ‚ÇÉ.p‚ÇÅ
  /-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to `S`. -/
  p : chosenPullback.pullback ‚ü∂ S := chosenPullback.p‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ.p
  /-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to `X‚ÇÅ`. -/
  p‚ÇÅ : chosenPullback.pullback ‚ü∂ X‚ÇÅ := chosenPullback.p‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÅ
  /-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to `X‚ÇÉ`. -/
  p‚ÇÉ : chosenPullback.pullback ‚ü∂ X‚ÇÉ := chosenPullback.p‚ÇÇ ‚â´ h‚ÇÇ‚ÇÉ.p‚ÇÇ
  /-- A morphism from the wide pullback `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to the pullback of `f‚ÇÅ` and `f‚ÇÉ`
  that is compatible with projections. -/
  l : h‚ÇÅ‚ÇÉ.LiftStruct p‚ÇÅ p‚ÇÉ p
  hp‚ÇÅ : chosenPullback.p‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÅ = p‚ÇÅ := by cat_disch
  hp‚ÇÉ : chosenPullback.p‚ÇÇ ‚â´ h‚ÇÇ‚ÇÉ.p‚ÇÇ = p‚ÇÉ := by cat_disch

namespace ChosenPullback‚ÇÉ

variable {h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ} (h : ChosenPullback‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ)

/-- The chosen wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)`. -/
abbrev pullback := h.chosenPullback.pullback

/-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to the pullback of `f‚ÇÅ` and `f‚ÇÉ`. -/
def p‚ÇÅ‚ÇÉ : h.pullback ‚ü∂ h‚ÇÅ‚ÇÉ.pullback := h.l.f

/-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to the pullback of `f‚ÇÅ` and `f‚ÇÇ`. -/
def p‚ÇÅ‚ÇÇ : h.pullback ‚ü∂ h‚ÇÅ‚ÇÇ.pullback := h.chosenPullback.p‚ÇÅ

/-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to the pullback of `f‚ÇÇ` and `f‚ÇÉ`. -/
def p‚ÇÇ‚ÇÉ : h.pullback ‚ü∂ h‚ÇÇ‚ÇÉ.pullback := h.chosenPullback.p‚ÇÇ

/-- The projection from the wide pullback of `(f‚ÇÅ, f‚ÇÇ, f‚ÇÉ)` to `X‚ÇÇ`. -/
def p‚ÇÇ : h.pullback ‚ü∂ X‚ÇÇ := h.chosenPullback.p

@[reassoc (attr := simp)]
lemma p‚ÇÅ‚ÇÇ_p‚ÇÅ : h.p‚ÇÅ‚ÇÇ ‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÅ = h.p‚ÇÅ := by simp [p‚ÇÅ‚ÇÇ, hp‚ÇÅ]

@[reassoc (attr := simp)]
lemma p‚ÇÅ‚ÇÇ_p‚ÇÇ : h.p‚ÇÅ‚ÇÇ ‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÇ = h.p‚ÇÇ := by simp [p‚ÇÅ‚ÇÇ, p‚ÇÇ]

@[reassoc (attr := simp)]
lemma p‚ÇÇ‚ÇÉ_p‚ÇÇ : h.p‚ÇÇ‚ÇÉ ‚â´ h‚ÇÇ‚ÇÉ.p‚ÇÅ = h.p‚ÇÇ := by simp [p‚ÇÇ‚ÇÉ, p‚ÇÇ]

@[reassoc (attr := simp)]
lemma p‚ÇÇ‚ÇÉ_p‚ÇÉ : h.p‚ÇÇ‚ÇÉ ‚â´ h‚ÇÇ‚ÇÉ.p‚ÇÇ = h.p‚ÇÉ := by simp [p‚ÇÇ‚ÇÉ, hp‚ÇÉ]

@[reassoc (attr := simp)]
lemma p‚ÇÅ‚ÇÉ_p‚ÇÅ : h.p‚ÇÅ‚ÇÉ ‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÅ = h.p‚ÇÅ := by simp [p‚ÇÅ‚ÇÉ]

@[reassoc (attr := simp)]
lemma p‚ÇÅ‚ÇÉ_p‚ÇÉ : h.p‚ÇÅ‚ÇÉ ‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÇ = h.p‚ÇÉ := by simp [p‚ÇÅ‚ÇÉ]

@[reassoc (attr := simp)]
lemma w‚ÇÅ : h.p‚ÇÅ ‚â´ f‚ÇÅ = h.p := by
  simpa only [‚Üê hp‚ÇÅ, Category.assoc, h‚ÇÅ‚ÇÉ.hp‚ÇÅ, h.l.f_p] using h.l.f_p‚ÇÅ.symm =‚â´ f‚ÇÅ

@[reassoc (attr := simp)]
lemma w‚ÇÉ : h.p‚ÇÉ ‚â´ f‚ÇÉ = h.p := by
  simpa only [‚Üê hp‚ÇÉ, Category.assoc, h‚ÇÅ‚ÇÉ.hp‚ÇÇ, h.l.f_p] using h.l.f_p‚ÇÇ.symm =‚â´ f‚ÇÉ

@[reassoc (attr := simp)]
lemma w‚ÇÇ : h.p‚ÇÇ ‚â´ f‚ÇÇ = h.p := by
  rw [‚Üê p‚ÇÇ‚ÇÉ_p‚ÇÇ_assoc, h‚ÇÇ‚ÇÉ.condition, ‚Üê w‚ÇÉ, p‚ÇÇ‚ÇÉ_p‚ÇÉ_assoc]

@[reassoc (attr := simp)]
lemma p‚ÇÅ‚ÇÇ_p : h.p‚ÇÅ‚ÇÇ ‚â´ h‚ÇÅ‚ÇÇ.p = h.p := by
  rw [‚Üê h‚ÇÅ‚ÇÇ.hp‚ÇÇ, p‚ÇÅ‚ÇÇ_p‚ÇÇ_assoc, w‚ÇÇ]

@[reassoc (attr := simp)]
lemma p‚ÇÇ‚ÇÉ_p : h.p‚ÇÇ‚ÇÉ ‚â´ h‚ÇÇ‚ÇÉ.p = h.p := by
  rw [‚Üê h‚ÇÇ‚ÇÉ.hp‚ÇÇ, p‚ÇÇ‚ÇÉ_p‚ÇÉ_assoc, w‚ÇÉ]

@[reassoc (attr := simp)]
lemma p‚ÇÅ‚ÇÉ_p : h.p‚ÇÅ‚ÇÉ ‚â´ h‚ÇÅ‚ÇÉ.p = h.p := by
  rw [‚Üê h‚ÇÅ‚ÇÉ.hp‚ÇÅ, p‚ÇÅ‚ÇÉ_p‚ÇÅ_assoc, w‚ÇÅ]

lemma p‚ÇÅ‚ÇÇ_eq_lift : h.p‚ÇÅ‚ÇÇ = h‚ÇÅ‚ÇÇ.isPullback.lift h.p‚ÇÅ h.p‚ÇÇ (by simp) := by
  cat_disch

lemma p‚ÇÇ‚ÇÉ_eq_lift : h.p‚ÇÇ‚ÇÉ = h‚ÇÇ‚ÇÉ.isPullback.lift h.p‚ÇÇ h.p‚ÇÉ (by simp) := by
  cat_disch

lemma p‚ÇÅ‚ÇÉ_eq_lift : h.p‚ÇÅ‚ÇÉ = h‚ÇÅ‚ÇÉ.isPullback.lift h.p‚ÇÅ h.p‚ÇÉ (by simp) := by
  cat_disch

lemma exists_lift {Y : C} (g‚ÇÅ : Y ‚ü∂ X‚ÇÅ) (g‚ÇÇ : Y ‚ü∂ X‚ÇÇ) (g‚ÇÉ : Y ‚ü∂ X‚ÇÉ) (b : Y ‚ü∂ S)
    (hg‚ÇÅ : g‚ÇÅ ‚â´ f‚ÇÅ = b) (hg‚ÇÇ : g‚ÇÇ ‚â´ f‚ÇÇ = b) (hg‚ÇÉ : g‚ÇÉ ‚â´ f‚ÇÉ = b) :
    ‚àÉ (œÜ : Y ‚ü∂ h.pullback), œÜ ‚â´ h.p‚ÇÅ = g‚ÇÅ ‚àß œÜ ‚â´ h.p‚ÇÇ = g‚ÇÇ ‚àß œÜ ‚â´ h.p‚ÇÉ = g‚ÇÉ := by
  obtain ‚ü®œÜ‚ÇÅ‚ÇÇ, w‚ÇÅ, w‚ÇÇ‚ü© := h‚ÇÅ‚ÇÇ.isPullback.exists_lift g‚ÇÅ g‚ÇÇ (by cat_disch)
  obtain ‚ü®œÜ‚ÇÇ‚ÇÉ, w‚ÇÇ', w‚ÇÉ‚ü© := h‚ÇÇ‚ÇÉ.isPullback.exists_lift g‚ÇÇ g‚ÇÉ (by cat_disch)
  obtain ‚ü®œÜ, w‚ÇÅ‚ÇÇ, w‚ÇÇ‚ÇÉ‚ü© := h.chosenPullback.isPullback.exists_lift œÜ‚ÇÅ‚ÇÇ œÜ‚ÇÇ‚ÇÉ (by cat_disch)
  refine ‚ü®œÜ, ?_, ?_, ?_‚ü©
  ¬∑ rw [‚Üê w‚ÇÅ, ‚Üê w‚ÇÅ‚ÇÇ, Category.assoc, ‚Üê p‚ÇÅ‚ÇÇ, p‚ÇÅ‚ÇÇ_p‚ÇÅ]
  ¬∑ rw [‚Üê w‚ÇÇ, ‚Üê w‚ÇÅ‚ÇÇ, Category.assoc, ‚Üê p‚ÇÅ‚ÇÇ, p‚ÇÅ‚ÇÇ_p‚ÇÇ]
  ¬∑ rw [‚Üê w‚ÇÉ, ‚Üê w‚ÇÇ‚ÇÉ, Category.assoc, ‚Üê p‚ÇÇ‚ÇÉ, p‚ÇÇ‚ÇÉ_p‚ÇÉ]

lemma isPullback‚ÇÇ : IsPullback h.p‚ÇÅ‚ÇÇ h.p‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÇ.p‚ÇÇ h‚ÇÇ‚ÇÉ.p‚ÇÅ := h.chosenPullback.isPullback

@[ext]
lemma hom_ext {Y : C} {œÜ œÜ' : Y ‚ü∂ h.pullback}
    (h‚ÇÅ : œÜ ‚â´ h.p‚ÇÅ = œÜ' ‚â´ h.p‚ÇÅ) (h‚ÇÇ : œÜ ‚â´ h.p‚ÇÇ = œÜ' ‚â´ h.p‚ÇÇ)
    (h‚ÇÉ : œÜ ‚â´ h.p‚ÇÉ = œÜ' ‚â´ h.p‚ÇÉ) : œÜ = œÜ' := by
  apply h.isPullback‚ÇÇ.hom_ext <;> cat_disch

lemma isPullback‚ÇÅ : IsPullback h.p‚ÇÅ‚ÇÇ h.p‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÇ.p‚ÇÅ h‚ÇÅ‚ÇÉ.p‚ÇÅ :=
  .mk' (by simp) (fun _ _ _ h‚ÇÅ h‚ÇÇ ‚Ü¶ h.hom_ext (by simpa using h‚ÇÅ =‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÅ)
      (by simpa using h‚ÇÅ =‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÇ) (by simpa using h‚ÇÇ =‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÇ))
    (fun _ a b w ‚Ü¶ by
      obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ, hœÜ‚ÇÉ‚ü© :=
        h.exists_lift (a ‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÅ) (a ‚â´ h‚ÇÅ‚ÇÇ.p‚ÇÇ) (b ‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÇ) _ rfl
          (by simp) (by simpa using w.symm =‚â´ f‚ÇÅ)
      exact ‚ü®œÜ, by cat_disch, by cat_disch‚ü©)

lemma isPullback‚ÇÉ : IsPullback h.p‚ÇÅ‚ÇÉ h.p‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ.p‚ÇÇ h‚ÇÇ‚ÇÉ.p‚ÇÇ :=
  .mk' (by simp) (fun _ _ _ h‚ÇÅ h‚ÇÇ ‚Ü¶ h.hom_ext (by simpa using h‚ÇÅ =‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÅ)
      (by simpa using h‚ÇÇ =‚â´ h‚ÇÇ‚ÇÉ.p‚ÇÅ) (by simpa using h‚ÇÅ =‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÇ))
    (fun _ a b w ‚Ü¶ by
      obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ, hœÜ‚ÇÉ‚ü© :=
        h.exists_lift (a ‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÅ) (b ‚â´ h‚ÇÇ‚ÇÉ.p‚ÇÅ) (a ‚â´ h‚ÇÅ‚ÇÉ.p‚ÇÇ) _ rfl
          (by simpa using w.symm =‚â´ f‚ÇÉ) (by simp)
      exact ‚ü®œÜ, by cat_disch, by cat_disch‚ü©)

end ChosenPullback‚ÇÉ

end CategoryTheory.Limits

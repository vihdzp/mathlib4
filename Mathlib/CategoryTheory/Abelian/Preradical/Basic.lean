/-
Copyright (c) 2026 Blake Farman. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Blake Farman
-/
module
public import Mathlib.CategoryTheory.Abelian.Basic
public import Mathlib.CategoryTheory.Subobject.MonoOver
public import Mathlib.CategoryTheory.Limits.FunctorCategory.EpiMono

/-!
# Preradicals

A **preradical** on an abelian category `C` is a monomorphism in the functor category `C ‚•§ C`
with codomain `ùü≠ C`, i.e. an element of `MonoOver (ùü≠ C)`.

## Main definitions

* `Preradical C`: The type of preradicals on `C`.
* `Preradical.r`: The underlying endofunctor of a `Preradical`.
* `Preradical.Œπ`: The structure morphism of a `Preradical`.
* `Preradical.IsIdempotent`: The predicate expressing idempotence.

## References

* [Bo Stenstr√∂m, *Rings and Modules of Quotients*][stenstrom1971]
* [Bo Stenstr√∂m, *Rings of Quotients*][stenstrom1975]

## Tags

category theory, preradical, torsion theory
-/

@[expose] public section

universe v u

namespace CategoryTheory.Abelian

variable {C : Type u} [Category.{v} C] [Abelian C]

variable (C) in
/-- A preradical on an abelian category `C` is a monomorphism in `C ‚•§ C` with codomain `ùü≠ C`. -/
abbrev Preradical := MonoOver (ùü≠ C)

namespace Preradical

variable (Œ¶ : Preradical C)

/-- The underlying endofunctor `r : C ‚•§ C` of a preradical `Œ¶`. -/
abbrev r : C ‚•§ C := Œ¶.obj.left

/-- The structure morphism `Œ¶.r ‚ü∂ ùü≠ C` of a preradical `Œ¶`. -/
abbrev Œπ : Œ¶.r ‚ü∂ ùü≠ C := Œ¶.obj.hom

@[simp]
lemma r_map_Œπ_app (X : C) : Œ¶.r.map (Œ¶.Œπ.app X) = Œ¶.Œπ.app (Œ¶.r.obj X) := by
  rw [‚Üê cancel_mono (Œ¶.Œπ.app X)]
  exact Œ¶.Œπ.naturality (Œ¶.Œπ.app X)

/-- A preradical `Œ¶` is idempotent if `Œ¶.r ‚ãô Œ¶.r ‚âÖ Œ¶.r`. -/
class IsIdempotent : Prop where
  isIso_whiskerLeft_r_Œπ : IsIso (Functor.whiskerLeft Œ¶.r Œ¶.Œπ)

attribute [instance] IsIdempotent.isIso_whiskerLeft_r_Œπ

instance [Œ¶.IsIdempotent] (X : C) :
    IsIso (Œ¶.Œπ.app (Œ¶.r.obj X)) :=
  inferInstanceAs (IsIso ((Functor.whiskerLeft Œ¶.r Œ¶.Œπ).app X))

instance [Œ¶.IsIdempotent] (X : C) :
    IsIso (Œ¶.r.map (Œ¶.Œπ.app X)) := by
  rw [r_map_Œπ_app]
  infer_instance

end Preradical

end CategoryTheory.Abelian

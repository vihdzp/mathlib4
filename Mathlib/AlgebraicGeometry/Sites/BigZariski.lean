/-
Copyright (c) 2023 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou, Adam Topaz
-/
module

public import Mathlib.AlgebraicGeometry.Sites.Pretopology
public import Mathlib.CategoryTheory.Sites.Canonical
public import Mathlib.CategoryTheory.Sites.Preserves

/-!
# The big Zariski site of schemes

In this file, we define the Zariski topology, as a Grothendieck topology on the
category `Scheme.{u}`: this is `Scheme.zariskiTopology.{u}`. If `X : Scheme.{u}`,
the Zariski topology on `Over X` can be obtained as `Scheme.zariskiTopology.over X`
(see `CategoryTheory.Sites.Over`.).

TODO:
* If `Y : Scheme.{u}`, define a continuous functor from the category of opens of `Y`
  to `Over Y`, and show that a presheaf on `Over Y` is a sheaf for the Zariski topology
  iff its "restriction" to the topological space `Z` is a sheaf for all `Z : Over Y`.
* We should have good notions of (pre)sheaves of `Type (u + 1)` (e.g. associated
  sheaf functor, pushforward, pullbacks) on `Scheme.{u}` for this topology. However,
  some constructions in the `CategoryTheory.Sites` folder currently assume that
  the site is a small category: this should be generalized. As a result,
  this big Zariski site can considered as a test case of the Grothendieck topology API
  for future applications to Ã©tale cohomology.

-/

@[expose] public section

universe v u

open CategoryTheory Limits Opposite

namespace AlgebraicGeometry

namespace Scheme

/-- The Zariski pretopology on the category of schemes. -/
def zariskiPretopology : Pretopology Scheme.{u} :=
  pretopology @IsOpenImmersion

/-- The Zariski topology on the category of schemes. -/
abbrev zariskiTopology : GrothendieckTopology Scheme.{u} :=
  grothendieckTopology IsOpenImmersion

lemma zariskiTopology_eq : zariskiTopology.{u} = zariskiPretopology.toGrothendieck :=
  Precoverage.toGrothendieck_toPretopology_eq_toGrothendieck.symm

instance subcanonical_zariskiTopology : zariskiTopology.Subcanonical := by
  apply GrothendieckTopology.Subcanonical.of_isSheaf_yoneda_obj
  intro X
  rw [Precoverage.isSheaf_toGrothendieck_iff_of_isStableUnderBaseChange]
  rintro Y S hS x hx
  obtain âŸ¨(ğ“¤ : OpenCover Y), rflâŸ© := exists_cover_of_mem_pretopology hS
  let e : Y âŸ¶ X := ğ“¤.glueMorphisms (fun j => x (ğ“¤.f _) (.mk _)) <| by
    intro i j
    apply hx
    exact Limits.pullback.condition
  refine âŸ¨e, ?_, ?_âŸ©
  Â· rintro Z e âŸ¨jâŸ©
    dsimp [e]
    rw [ğ“¤.Î¹_glueMorphisms]
  Â· intro e' h
    apply ğ“¤.hom_ext
    intro j
    rw [ğ“¤.Î¹_glueMorphisms]
    exact h (ğ“¤.f j) (.mk j)

end Scheme

/-- Zariski sheaves preserve products. -/
lemma preservesLimitsOfShape_discrete_of_isSheaf_zariskiTopology {F : Scheme.{u}áµ’áµ– â¥¤ Type v}
    {Î¹ : Type*} [Small.{u} Î¹] [Small.{v} Î¹] (hF : Presieve.IsSheaf Scheme.zariskiTopology F) :
    PreservesLimitsOfShape (Discrete Î¹) F := by
  apply (config := { allowSynthFailures := true }) preservesLimitsOfShape_of_discrete
  intro X
  have (i : Î¹) : Mono (Cofan.inj (Sigma.cocone (Discrete.functor <| unop âˆ˜ X)) i) :=
    inferInstanceAs <| Mono (Sigma.Î¹ _ _)
  refine Presieve.preservesProduct_of_isSheafFor F ?_ initialIsInitial
      (Sigma.cocone (Discrete.functor <| unop âˆ˜ X)) (coproductIsCoproduct' _) ?_ ?_
  Â· apply hF.isSheafFor
    convert (âŠ¥_ Scheme).bot_mem_grothendieckTopology
    rw [eq_bot_iff]
    rintro Y f âŸ¨g, _, _, âŸ¨iâŸ©, _âŸ©
    exact i.elim
  Â· intro i j
    exact CoproductDisjoint.isPullback_of_isInitial
      (coproductIsCoproduct' <| Discrete.functor <| unop âˆ˜ X) initialIsInitial
  Â· exact hF.isSheafFor _ (sigmaOpenCover _).mem_grothendieckTopology

/-- Let `F` be a locally directed diagram of open immersions, i.e., a diagram of schemes
for which whenever `xáµ¢ âˆˆ Fáµ¢` and `xâ±¼ âˆˆ Fâ±¼` map to the same `xâ‚– âˆˆ Fâ‚–`, there exists
some `xâ‚— âˆˆ Fâ‚—` that maps to `xáµ¢` and `xâ±¼` (e.g, the diagram indexing a coproduct).
Then the colimit inclusions are a Zariski covering. -/
lemma ofArrows_Î¹_mem_zariskiTopology_of_isColimit {J : Type*} [Category J]
    (F : J â¥¤ Scheme.{u}) [âˆ€ {i j : J} (f : i âŸ¶ j), IsOpenImmersion (F.map f)]
    [(F.comp Scheme.forget).IsLocallyDirected] [Quiver.IsThin J] [Small.{u} J]
    (c : Cocone F) (hc : IsColimit c) :
    Sieve.ofArrows _ c.Î¹.app âˆˆ Scheme.zariskiTopology c.pt := by
  let iso : c.pt â‰… colimit F := hc.coconePointUniqueUpToIso (colimit.isColimit F)
  rw [â† GrothendieckTopology.pullback_mem_iff_of_isIso (i := iso.inv)]
  apply GrothendieckTopology.superset_covering _ ?_ ?_
  Â· exact Sieve.ofArrows _ (colimit.Î¹ F)
  Â· rw [Sieve.ofArrows, Sieve.generate_le_iff]
    rintro - - âŸ¨iâŸ©
    exact âŸ¨_, ğŸ™ _, c.Î¹.app i, âŸ¨iâŸ©, by simp [iso]âŸ©
  Â· exact (Scheme.IsLocallyDirected.openCover F).mem_grothendieckTopology

end AlgebraicGeometry

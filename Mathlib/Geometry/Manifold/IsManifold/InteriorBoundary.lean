/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang, Ben Eltschig
-/
module

public import Mathlib.Geometry.Manifold.IsManifold.ExtChartAt

import Mathlib.Analysis.Calculus.LocalExtr.Basic
import Mathlib.Analysis.LocallyConvex.Separation

/-!
# Interior and boundary of a manifold
Define the interior and boundary of a manifold.

## Main definitions
- **IsInteriorPoint x**: `x ‚àà M` is an interior point if, with `œÜ` being the preferred chart at `x`,
  `œÜ x` is an interior point of `œÜ.target`.
- **IsBoundaryPoint x**: `x ‚àà M` is a boundary point if `(extChartAt I x) x ‚àà frontier (range I)`.
- **interior I M** is the **interior** of `M`, the set of its interior points.
- **boundary I M** is the **boundary** of `M`, the set of its boundary points.

## Main results
- `ModelWithCorners.univ_eq_interior_union_boundary`: `M` is the union of its interior and boundary
- `ModelWithCorners.interior_boundary_disjoint`: interior and boundary of `M` are disjoint
- `BoundarylessManifold.isInteriorPoint`: if `M` is boundaryless, every point is an interior point
- `ModelWithCorners.Boundaryless.boundary_eq_empty` and `of_boundary_eq_empty`:
`M` is boundaryless if and only if its boundary is empty

- `isInteriorPoint_iff_of_mem_atlas`: a point is an interior point iff any given chart around it
  sends it to the interior of the model; that is, the notion of interior is independent of choices
  of charts
- `ModelWithCorners.isOpen_interior`, `ModelWithCorners.isClosed_boundary`: the interior is open and
  and the boundary is closed. This is currently only proven for C¬π manifolds.

- `ModelWithCorners.interior_open`: the interior of `u : Opens M` is the preimage of the interior
  of `M` under the inclusion
- `ModelWithCorners.boundary_open`: the boundary of `u : Opens M` is the preimage of the boundary
  of `M` under the inclusion
- `ModelWithCorners.BoundarylessManifold.open`: if `M` is boundaryless, so is `u : Opens M`

- `ModelWithCorners.interior_prod`: the interior of `M √ó N` is the product of the interiors
of `M` and `N`.
- `ModelWithCorners.boundary_prod`: the boundary of `M √ó N` is `‚àÇM √ó N ‚à™ (M √ó ‚àÇN)`.
- `ModelWithCorners.BoundarylessManifold.prod`: if `M` and `N` are boundaryless, so is `M √ó N`

- `ModelWithCorners.interior_disjointUnion`: the interior of a disjoint union `M ‚äî M'`
  is the union of the interior of `M` and `M'`
- `ModelWithCorners.boundary_disjointUnion`: the boundary of a disjoint union `M ‚äî M'`
  is the union of the boundaries of `M` and `M'`
- `ModelWithCorners.boundaryless_disjointUnion`: if `M` and `M'` are boundaryless,
  so is their disjoint union `M ‚äî M'`

## Tags
manifold, interior, boundary

## TODO
- the interior of `M` is dense, the boundary nowhere dense
- the interior of `M` is a boundaryless manifold
- `boundary M` is a submanifold (possibly with boundary and corners):
  follows from the corresponding statement for the model with corners `I`;
  this requires a definition of submanifolds
- if `M` is finite-dimensional, its boundary has measure zero
- generalise lemmas about C¬π manifolds with boundary to also hold for finite-dimensional topological
  manifolds; this will require e.g. the homology of spheres.

-/

@[expose] public section

open Set
open scoped Topology

-- Let `M` be a manifold with corners over the pair `(E, H)`.
variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {H : Type*} [TopologicalSpace H] {I : ModelWithCorners ùïú E H}
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]

namespace ModelWithCorners

variable (I) in
/-- `p ‚àà M` is an interior point of a manifold `M` if and only if its image in the extended chart
lies in the interior of the model space. -/
def IsInteriorPoint (x : M) := extChartAt I x x ‚àà interior (range I)

variable (I) in
/-- `p ‚àà M` is a boundary point of a manifold `M` if and only if its image in the extended chart
lies on the boundary of the model space. -/
def IsBoundaryPoint (x : M) := extChartAt I x x ‚àà frontier (range I)

variable (M) in
/-- The **interior** of a manifold `M` is the set of its interior points. -/
protected def interior : Set M := { x : M | I.IsInteriorPoint x }

lemma isInteriorPoint_iff {x : M} :
    I.IsInteriorPoint x ‚Üî extChartAt I x x ‚àà interior (extChartAt I x).target :=
  ‚ü®fun h ‚Ü¶ (chartAt H x).mem_interior_extend_target (mem_chart_target H x) h,
    fun h ‚Ü¶ OpenPartialHomeomorph.interior_extend_target_subset_interior_range _ h‚ü©

variable (M) in
/-- The **boundary** of a manifold `M` is the set of its boundary points. -/
protected def boundary : Set M := { x : M | I.IsBoundaryPoint x }

lemma isBoundaryPoint_iff {x : M} : I.IsBoundaryPoint x ‚Üî extChartAt I x x ‚àà frontier (range I) :=
  Iff.rfl

/-- Every point is either an interior or a boundary point. -/
lemma isInteriorPoint_or_isBoundaryPoint (x : M) : I.IsInteriorPoint x ‚à® I.IsBoundaryPoint x := by
  rw [IsInteriorPoint, or_iff_not_imp_left, I.isBoundaryPoint_iff, ‚Üê closure_diff_interior,
    I.isClosed_range.closure_eq, mem_diff]
  exact fun h ‚Ü¶ ‚ü®mem_range_self _, h‚ü©

/-- A manifold decomposes into interior and boundary. -/
lemma interior_union_boundary_eq_univ : (I.interior M) ‚à™ (I.boundary M) = (univ : Set M) :=
  eq_univ_of_forall fun x => (mem_union _ _ _).mpr (I.isInteriorPoint_or_isBoundaryPoint x)

/-- The interior and boundary of a manifold `M` are disjoint. -/
lemma disjoint_interior_boundary : Disjoint (I.interior M) (I.boundary M) := by
  by_contra h
  -- Choose some x in the intersection of interior and boundary.
  obtain ‚ü®x, h1, h2‚ü© := not_disjoint_iff.mp h
  rw [‚Üê mem_empty_iff_false (extChartAt I x x),
    ‚Üê disjoint_iff_inter_eq_empty.mp disjoint_interior_frontier, mem_inter_iff]
  exact ‚ü®h1, h2‚ü©

lemma isInteriorPoint_iff_not_isBoundaryPoint (x : M) :
    I.IsInteriorPoint x ‚Üî ¬¨I.IsBoundaryPoint x := by
  refine ‚ü®?_,
    by simpa only [or_iff_not_imp_right] using isInteriorPoint_or_isBoundaryPoint x (I := I)‚ü©
  by_contra! h
  rw [‚Üê mem_empty_iff_false (extChartAt I x x),
    ‚Üê disjoint_iff_inter_eq_empty.mp disjoint_interior_frontier, mem_inter_iff]
  exact h

/-- The boundary is the complement of the interior. -/
lemma compl_interior : (I.interior M)·∂ú = I.boundary M := by
  apply compl_unique ?_ I.interior_union_boundary_eq_univ
  exact disjoint_iff_inter_eq_empty.mp I.disjoint_interior_boundary

/-- The interior is the complement of the boundary. -/
lemma compl_boundary : (I.boundary M)·∂ú = I.interior M := by
  rw [‚Üê compl_interior, compl_compl]

lemma _root_.range_mem_nhds_isInteriorPoint {x : M} (h : I.IsInteriorPoint x) :
    range I ‚àà ùìù (extChartAt I x x) := by
  rw [mem_nhds_iff]
  exact ‚ü®interior (range I), interior_subset, isOpen_interior, h‚ü©

/-- Type class for manifold without boundary. This differs from `ModelWithCorners.Boundaryless`,
which states that the `ModelWithCorners` maps to the whole model vector space. -/
class _root_.BoundarylessManifold {ùïú : Type*} [NontriviallyNormedField ùïú]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
    {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ùïú E H)
    (M : Type*) [TopologicalSpace M] [ChartedSpace H M] : Prop where
  isInteriorPoint' : ‚àÄ x : M, IsInteriorPoint I x

section Boundaryless
variable [I.Boundaryless]

/-- Boundaryless `ModelWithCorners` implies boundaryless manifold. -/
instance : BoundarylessManifold I M where
  isInteriorPoint' x := by
    let r := ((chartAt H x).isOpen_extend_target (I := I)).interior_eq
    have : extChartAt I x = (chartAt H x).extend I := rfl
    rw [‚Üê this] at r
    rw [isInteriorPoint_iff, r]
    exact PartialEquiv.map_source _ (mem_extChartAt_source _)

end Boundaryless

section BoundarylessManifold

/-- The empty manifold is boundaryless. -/
instance BoundarylessManifold.of_empty [IsEmpty M] : BoundarylessManifold I M where
  isInteriorPoint' x := (IsEmpty.false x).elim

lemma _root_.BoundarylessManifold.isInteriorPoint {x : M} [BoundarylessManifold I M] :
    IsInteriorPoint I x := BoundarylessManifold.isInteriorPoint' x

/-- If `I` is boundaryless, `M` has full interior. -/
lemma interior_eq_univ [BoundarylessManifold I M] : I.interior M = univ :=
  eq_univ_of_forall fun _ => BoundarylessManifold.isInteriorPoint

/-- Boundaryless manifolds have empty boundary. -/
lemma Boundaryless.boundary_eq_empty [BoundarylessManifold I M] : I.boundary M = ‚àÖ := by
  rw [‚Üê I.compl_interior, I.interior_eq_univ, compl_empty_iff]

instance [BoundarylessManifold I M] : IsEmpty (I.boundary M) :=
  isEmpty_coe_sort.mpr Boundaryless.boundary_eq_empty

/-- `M` is boundaryless if and only if its boundary is empty. -/
lemma Boundaryless.iff_boundary_eq_empty : I.boundary M = ‚àÖ ‚Üî BoundarylessManifold I M := by
  refine ‚ü®fun h ‚Ü¶ { isInteriorPoint' := ?_ }, fun a ‚Ü¶ boundary_eq_empty‚ü©
  intro x
  change x ‚àà I.interior M
  rw [‚Üê compl_interior, compl_empty_iff] at h
  rw [h]
  trivial

/-- Manifolds with empty boundary are boundaryless. -/
lemma Boundaryless.of_boundary_eq_empty (h : I.boundary M = ‚àÖ) : BoundarylessManifold I M :=
  (Boundaryless.iff_boundary_eq_empty (I := I)).mp h

end BoundarylessManifold

section ChartIndependence

/-- If a function `f : E ‚Üí H` is differentiable at `x`, sends a neighbourhood `u` of `x` to a
closed convex set `s` with nonempty interior and has surjective differential at `x`, it must send
`x` to the interior of `s`. -/
lemma _root_.DifferentiableAt.mem_interior_convex_of_surjective_fderiv
    {E H : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedAddCommGroup H] [NormedSpace ‚Ñù H]
    {f : E ‚Üí H} {x : E} (hf : DifferentiableAt ‚Ñù f x) {u : Set E} (hu : u ‚àà ùìù x) {s : Set H}
    (hs : Convex ‚Ñù s) (hs' : IsClosed s) (hs'' : (interior s).Nonempty) (hfus : Set.MapsTo f u s)
    (hfx : Function.Surjective (fderiv ‚Ñù f x)) : f x ‚àà interior s := by
  contrapose hfx
  have ‚ü®F, hF‚ü© := geometric_hahn_banach_open_point hs.interior isOpen_interior hfx
  -- It suffices to show that `fderiv ‚Ñù f x` sends everything to the kernel of `F`.
  suffices h : ‚àÄ y, F (fderiv ‚Ñù f x y) = 0 by
    have ‚ü®y, hy‚ü© := hs''
    unfold Function.Surjective; push_neg
    refine ‚ü®f x - y, fun z ‚Ü¶ ne_of_apply_ne F ?_‚ü©
    rw [h z, F.map_sub]
    exact (sub_pos.2 <| hF _ hy).ne
  -- This follows from `F ‚àò f` taking on a local maximum at `e.extend I x`.
  have hF' : MapsTo F s (Iic (F (f x))) := by
    rw [‚Üê hs'.closure_eq, ‚Üê closure_Iio, ‚Üê hs.closure_interior_eq_closure_of_nonempty_interior hs'']
    exact .closure hF F.continuous
  have hFœÜ : IsLocalMax (F ‚àò f) x := Filter.eventually_of_mem hu fun y hy ‚Ü¶ hF' <| hfus hy
  have h := hFœÜ.fderiv_eq_zero
  rw [fderiv_comp _ (by fun_prop) hf, ContinuousLinearMap.fderiv] at h
  exact DFunLike.congr_fun h

variable {n : WithTop ‚Ñï‚àû} [IsManifold I n M] {e e' : OpenPartialHomeomorph M H} {x : M}

/-- For any two charts `e`, `e'` around a point `x` in a C¬π manifold, if `e` maps `x` to the
interior of the model space, `e'` does too - in other words, the notion of interior points does not
depend on any choice of charts.

Note that in general, this is actually quite nontrivial; that is why are focusing only on C¬π
manifolds here. For merely topological finite-dimensional manifolds the proof involves singular
homology, and for infinite-dimensional topological manifolds I don't even know if this lemma holds.
-/
lemma mem_interior_range_of_mem_interior_range_of_mem_atlas (hn : n ‚â† 0)
    (he : e ‚àà atlas H M) (he' : e' ‚àà atlas H M) (hex : x ‚àà e.source) (hex' : x ‚àà e'.source)
    (hx : e.extend I x ‚àà interior (e.extend I).target) :
    e'.extend I x ‚àà interior (e'.extend I).target := by
  /- Since transition maps are diffeomorphisms, it suffices to show that if `e'` were to send `x`
  to the boundary of `range I`, the differential of the transition map `œÜ` from `e` to `e'` at `x`
  could not be surjective. -/
  let œÜ := I.extendCoordChange e e'
  have hœÜ : ContDiffOn ùïú n œÜ œÜ.source := contDiffOn_extendCoordChange
    (IsManifold.subset_maximalAtlas he) (IsManifold.subset_maximalAtlas he')
  suffices h : Function.Surjective (fderivWithin ùïú œÜ œÜ.source (e.extend I x)) ‚Üí
      e'.extend I x ‚àà interior (range I) by
    refine e'.mem_interior_extend_target (by simp [hex']) <| h ?_
    exact (isInvertible_fderivWithin_extendCoordChange hn (IsManifold.subset_maximalAtlas he)
      (IsManifold.subset_maximalAtlas he') <| by simp [hex, hex']).surjective
  intro hœÜx'
  /- Reduce the situation to the real case, then apply
  `DifferentiableAt.mem_interior_convex_of_surjective_fderiv`. -/
  wlog _ : IsRCLikeNormedField ùïú
  ¬∑ simp [I.range_eq_univ_of_not_isRCLikeNormedField ‚Äπ_‚Ä∫]
  let _ := IsRCLikeNormedField.rclike ùïú
  let _ : NormedSpace ‚Ñù E := NormedSpace.restrictScalars ‚Ñù ùïú E
  have hœÜx : œÜ.source ‚àà ùìù (e.extend I x) := by
    simp_rw [œÜ, extendCoordChange, PartialEquiv.trans_source, PartialEquiv.symm_source,
      Filter.inter_mem_iff, mem_interior_iff_mem_nhds.1 hx, true_and, e'.extend_source]
    exact e.extend_preimage_mem_nhds hex <| e'.open_source.mem_nhds hex'
  rw [‚Üê ContinuousLinearMap.coe_restrictScalars' (R := ‚Ñù),
    (hœÜ.differentiableOn hn _ (by simp [œÜ, hex, hex'])).restrictScalars_fderivWithin (ùïú := ‚Ñù)
      (uniqueDiffWithinAt_of_mem_nhds hœÜx), fderivWithin_of_mem_nhds <| hœÜx] at hœÜx'
  rw [show e'.extend I x = œÜ (e.extend I x) by simp [œÜ, hex]]
  replace hœÜ := ((hœÜ.restrict_scalars ‚Ñù).differentiableOn hn).differentiableAt hœÜx
  exact hœÜ.mem_interior_convex_of_surjective_fderiv hœÜx I.convex_range I.isClosed_range
    I.nonempty_interior (œÜ.mapsTo.mono_right <| by simp [œÜ, inter_assoc]) hœÜx'

/-- For any two charts `e`, `e'` around a point `x` in a C¬π manifold, `e` maps `x` to the interior
of the model space iff `e'` does. - in other words, the notion of interior points does not
depend on any choice of charts. -/
lemma mem_interior_range_iff_of_mem_atlas (hn : n ‚â† 0) (he : e ‚àà atlas H M) (he' : e' ‚àà atlas H M)
    (hex : x ‚àà e.source) (hex' : x ‚àà e'.source) :
    e.extend I x ‚àà interior (e.extend I).target ‚Üî
    e'.extend I x ‚àà interior (e'.extend I).target := by
  constructor <;> apply mem_interior_range_of_mem_interior_range_of_mem_atlas hn <;> assumption

/-- A point `x` in a C¬π manifold is an interior point if and only if it gets mapped to the interior
of the model space by any given chart - in other words, the notion of interior points does not
depend on any choice of charts. -/
lemma isInteriorPoint_iff_of_mem_atlas (hn : n ‚â† 0) (he : e ‚àà atlas H M) (hx : x ‚àà e.source) :
    I.IsInteriorPoint x ‚Üî e.extend I x ‚àà interior (e.extend I).target := by
  rw [isInteriorPoint_iff]
  exact mem_interior_range_iff_of_mem_atlas hn (chart_mem_atlas H x) he (mem_chart_source H x) hx

/-- A point `x` in a C¬π manifold is a boundary point if and only if it gets mapped to the boundary
of the model space by any given chart - in other words, the notion of boundary points does not
depend on any choice of charts.

Also see `ModelWithCorners.isInteriorPoint_iff_of_mem_atlas`. -/
lemma isBoundaryPoint_iff_of_mem_atlas (hn : n ‚â† 0) (he : e ‚àà atlas H M) (hx : x ‚àà e.source) :
    I.IsBoundaryPoint x ‚Üî e.extend I x ‚àà frontier (e.extend I).target := by
  rw [‚Üê not_iff_not, ‚Üê I.isInteriorPoint_iff_not_isBoundaryPoint,
    I.isInteriorPoint_iff_of_mem_atlas hn he hx, mem_interior_iff_notMem_frontier]
  exact (e.extend I).mapsTo <| e.extend_source (I := I) ‚ñ∏ hx

/-- The interior of any C¬π manifold is open.

This is currently only proven for C¬π manifolds, but holds at least for finite-dimensional
topological manifolds too; see `ModelWithCorners.isInteriorPoint_iff_of_mem_atlas`. -/
protected lemma isOpen_interior (hn : n ‚â† 0) : IsOpen (I.interior M) := by
  refine isOpen_iff_forall_mem_open.2 fun x hx ‚Ü¶ ‚ü®_, ?_, isOpen_extChartAt_preimage (I := I) x
    isOpen_interior, mem_chart_source H x, isInteriorPoint_iff.1 hx‚ü©
  exact fun y hy ‚Ü¶ (I.isInteriorPoint_iff_of_mem_atlas hn (chart_mem_atlas H x) hy.1).2 hy.2

/-- The boundary of any C¬π manifold is closed.

This is currently only proven for C¬π manifolds, but holds at least for finite-dimensional
topological manifolds too; see `ModelWithCorners.isInteriorPoint_iff_of_mem_atlas`. -/
protected lemma isClosed_boundary (hn : n ‚â† 0) : IsClosed (I.boundary M) := by
  rw [‚Üê I.compl_interior, isClosed_compl_iff]
  exact I.isOpen_interior hn

end ChartIndependence

/-! Interior and boundary of open subsets of a manifold. -/
section opens

open TopologicalSpace

/-- For `u : Opens M`, `x : u` is an interior point iff `x.val : M` is. -/
lemma isInteriorPoint_iff_isInteriorPoint_val {u : Opens M} {x : u} :
    I.IsInteriorPoint x ‚Üî I.IsInteriorPoint x.1 := by
  simpa [I.isInteriorPoint_iff, u.chartAt_eq,
    OpenPartialHomeomorph.subtypeRestr, mem_interior_iff_mem_nhds] using
    fun _ _ ‚Ü¶ (chartAt H x.1).extend_preimage_mem_nhds (mem_chart_source H x.1) (u.2.mem_nhds x.2)

/-- For `u : Opens M`, `x : u` is a boundary point iff `x.val : M` is. -/
lemma isBoundaryPoint_iff_isBoundaryPoint_val {u : Opens M} {x : u} :
    I.IsBoundaryPoint x ‚Üî I.IsBoundaryPoint x.1 := by
  simpa [I.isInteriorPoint_iff_not_isBoundaryPoint, not_iff_not] using
    I.isInteriorPoint_iff_isInteriorPoint_val

/-- The interior of `u : Opens M` is the preimage of the interior of `M` under the inclusion. -/
lemma interior_open {u : Opens M} : I.interior u = (‚Üë) ‚Åª¬π' I.interior M := by
  ext1; exact I.isInteriorPoint_iff_isInteriorPoint_val

/-- The boundary of `u : Opens M` is the preimage of the boundary of `M` under the inclusion. -/
lemma boundary_open {u : Opens M} : I.boundary u = (‚Üë) ‚Åª¬π' I.boundary M := by
  simp [‚Üê I.compl_interior, I.interior_open]

/-- Open subsets of boundaryless manifolds are boundaryless. -/
instance BoundarylessManifold.open [BoundarylessManifold I M] (u : Opens M) :
    BoundarylessManifold I u :=
  ‚ü®fun _ ‚Ü¶ I.isInteriorPoint_iff_isInteriorPoint_val.2 BoundarylessManifold.isInteriorPoint‚ü©

end opens

/-! Interior and boundary of the product of two manifolds. -/
section prod

variable
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E']
  {H' : Type*} [TopologicalSpace H']
  {N : Type*} [TopologicalSpace N] [ChartedSpace H' N]
  {J : ModelWithCorners ùïú E' H'} {x : M} {y : N}

set_option backward.isDefEq.respectTransparency false in
/-- The interior of `M √ó N` is the product of the interiors of `M` and `N`. -/
lemma interior_prod :
    (I.prod J).interior (M √ó N) = (I.interior M) √óÀ¢ (J.interior N) := by
  ext p
  have aux : (interior (range ‚ÜëI)) √óÀ¢ (interior (range J)) = interior (range (I.prod J)) := by
    rw [‚Üê interior_prod_eq, ‚Üê range_prodMap, modelWithCorners_prod_coe]
  constructor <;> intro hp
  ¬∑ replace hp : (I.prod J).IsInteriorPoint p := hp
    rw [IsInteriorPoint, ‚Üê aux] at hp
    exact hp
  ¬∑ change (I.prod J).IsInteriorPoint p
    rw [IsInteriorPoint, ‚Üê aux, mem_prod]
    obtain h := Set.mem_prod.mp hp
    rw [ModelWithCorners.interior] at h
    exact h

/-- The boundary of `M √ó N` is `‚àÇM √ó N ‚à™ (M √ó ‚àÇN)`. -/
lemma boundary_prod :
    (I.prod J).boundary (M √ó N) = Set.prod univ (J.boundary N) ‚à™ Set.prod (I.boundary M) univ := by
  let h := calc (I.prod J).boundary (M √ó N)
    _ = ((I.prod J).interior (M √ó N))·∂ú := compl_interior.symm
    _ = ((I.interior M) √óÀ¢ (J.interior N))·∂ú := by rw [interior_prod]
    _ = (I.interior M)·∂ú √óÀ¢ univ ‚à™ univ √óÀ¢ (J.interior N)·∂ú := by rw [compl_prod_eq_union]
  rw [h, I.compl_interior, J.compl_interior, union_comm]
  rfl

/-- If `M` is boundaryless, `‚àÇ(M√óN) = M √ó ‚àÇN`. -/
lemma boundary_of_boundaryless_left [BoundarylessManifold I M] :
    (I.prod J).boundary (M √ó N) = Set.prod (univ : Set M) (J.boundary N) := by
  rw [boundary_prod, Boundaryless.boundary_eq_empty (I := I)]
  have : Set.prod (‚àÖ : Set M) (univ : Set N) = ‚àÖ := Set.empty_prod
  rw [this, union_empty]

/-- If `N` is boundaryless, `‚àÇ(M√óN) = ‚àÇM √ó N`. -/
lemma boundary_of_boundaryless_right [BoundarylessManifold J N] :
    (I.prod J).boundary (M √ó N) = Set.prod (I.boundary M) (univ : Set N) := by
  rw [boundary_prod, Boundaryless.boundary_eq_empty (I := J)]
  have : Set.prod (univ : Set M) (‚àÖ : Set N) = ‚àÖ := Set.prod_empty
  rw [this, empty_union]

/-- The product of two boundaryless manifolds is boundaryless. -/
instance BoundarylessManifold.prod [BoundarylessManifold I M] [BoundarylessManifold J N] :
    BoundarylessManifold (I.prod J) (M √ó N) := by
  apply Boundaryless.of_boundary_eq_empty
  simp only [boundary_prod, Boundaryless.boundary_eq_empty, union_empty_iff]
  -- These are simp lemmas, but `simp` does not apply them on its own:
  -- presumably because of the distinction between `Prod` and `ModelProd`
  exact ‚ü®Set.prod_empty, Set.empty_prod‚ü©

end prod

/-! Interior and boundary of the disjoint union of two manifolds. -/
section disjointUnion

variable {M' : Type*} [TopologicalSpace M'] [ChartedSpace H M'] {n : WithTop ‚Ñï‚àû}
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïú E'] {H' : Type*} [TopologicalSpace H']
  {N N' : Type*} [TopologicalSpace N] [TopologicalSpace N'] [ChartedSpace H' N] [ChartedSpace H' N']

open Topology

lemma interiorPoint_inl (x : M) (hx : I.IsInteriorPoint x) :
    I.IsInteriorPoint (.inl x : M ‚äï M') := by
  rw [I.isInteriorPoint_iff, extChartAt, ChartedSpace.sum_chartAt_inl]
  dsimp
  rw [Sum.inl_injective.extend_apply (chartAt H x)]
  simpa [I.isInteriorPoint_iff, extChartAt] using hx

lemma boundaryPoint_inl (x : M) (hx : I.IsBoundaryPoint x) :
    I.IsBoundaryPoint (.inl x : M ‚äï M') := by
  rw [I.isBoundaryPoint_iff, extChartAt, ChartedSpace.sum_chartAt_inl]
  dsimp
  rw [Sum.inl_injective.extend_apply (chartAt H x)]
  simpa [I.isBoundaryPoint_iff, extChartAt] using hx

lemma interiorPoint_inr (x : M') (hx : I.IsInteriorPoint x) :
    I.IsInteriorPoint (.inr x : M ‚äï M') := by
  rw [I.isInteriorPoint_iff, extChartAt, ChartedSpace.sum_chartAt_inr]
  dsimp
  rw [Sum.inr_injective.extend_apply (chartAt H x)]
  simpa [I.isInteriorPoint_iff, extChartAt] using hx

lemma boundaryPoint_inr (x : M') (hx : I.IsBoundaryPoint x) :
    I.IsBoundaryPoint (.inr x : M ‚äï M') := by
  rw [I.isBoundaryPoint_iff, extChartAt, ChartedSpace.sum_chartAt_inr]
  dsimp
  rw [Sum.inr_injective.extend_apply (chartAt H x)]
  simpa [I.isBoundaryPoint_iff, extChartAt] using hx

-- Converse to the previous direction: if `x` were not an interior point,
-- it had to be a boundary point, hence `p` were a boundary point also, contradiction.
lemma isInteriorPoint_disjointUnion_left {p : M ‚äï M'} (hp : I.IsInteriorPoint p)
    (hleft : Sum.isLeft p) : I.IsInteriorPoint (Sum.getLeft p hleft) := by
  by_contra h
  set x := Sum.getLeft p hleft
  rw [isInteriorPoint_iff_not_isBoundaryPoint x, not_not] at h
  rw [isInteriorPoint_iff_not_isBoundaryPoint p] at hp
  have := boundaryPoint_inl (M' := M') x (by tauto)
  rw [‚Üê Sum.eq_left_getLeft_of_isLeft hleft] at this
  exact hp this

lemma isInteriorPoint_disjointUnion_right {p : M ‚äï M'} (hp : I.IsInteriorPoint p)
    (hright : Sum.isRight p) : I.IsInteriorPoint (Sum.getRight p hright) := by
  by_contra h
  set x := Sum.getRight p hright
  rw [‚Üê mem_empty_iff_false p, ‚Üê (disjoint_interior_boundary (I := I)).inter_eq]
  constructor
  ¬∑ rw [ModelWithCorners.interior, mem_setOf]; exact hp
  ¬∑ rw [ModelWithCorners.boundary, mem_setOf, Sum.eq_right_getRight_of_isRight hright]
    have := isInteriorPoint_or_isBoundaryPoint (I := I) x
    exact boundaryPoint_inr (M' := M') x (by tauto)

lemma interior_disjointUnion :
    ModelWithCorners.interior (I := I) (M ‚äï M') =
      Sum.inl '' (ModelWithCorners.interior (I := I) M)
      ‚à™ Sum.inr '' (ModelWithCorners.interior (I := I) M') := by
  grind [boundaryPoint_inl, boundaryPoint_inr, interior.eq_def, interiorPoint_inl,
    interiorPoint_inr, isInteriorPoint_iff_not_isBoundaryPoint]

lemma boundary_disjointUnion : ModelWithCorners.boundary (I := I) (M ‚äï M') =
      Sum.inl '' (ModelWithCorners.boundary (I := I) M)
      ‚à™ Sum.inr '' (ModelWithCorners.boundary (I := I) M') := by
  simp only [‚Üê ModelWithCorners.compl_interior, interior_disjointUnion, inl_compl_union_inr_compl]

/-- If `M` and `M'` are boundaryless, so is their disjoint union `M ‚äî M'`. -/
instance boundaryless_disjointUnion
    [hM : BoundarylessManifold I M] [hM' : BoundarylessManifold I M'] :
    BoundarylessManifold I (M ‚äï M') := by
  rw [‚Üê Boundaryless.iff_boundary_eq_empty] at hM hM' ‚ä¢
  simp [boundary_disjointUnion, hM, hM']

end disjointUnion

end ModelWithCorners

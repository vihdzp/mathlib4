/-
Copyright (c) 2026 Boyang Hu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Boyang Hu
-/
module

public import Mathlib.GroupTheory.Abelianization.Defs
public import Mathlib.GroupTheory.Sylow
public import Mathlib.GroupTheory.Transfer
public import Mathlib.Data.ZMod.QuotientGroup

/-!
# Focal Subgroup Theorem

This file defines the focal subgroup and proves the Focal Subgroup Theorem.

## Main definitions

* `focalSubgroup H` : The focal subgroup `H*` of a subgroup `H`, generated by elements
  of the form `⁅x, u⁆` where `x ∈ H` and `⁅x, u⁆ ∈ H`.
* `focalSubgroupOf H` : The focal subgroup of `H` considered as a subgroup of `H`,
  i.e., `(focalSubgroup H).subgroupOf H`.
* `transferFocal H` : The transfer homomorphism `V : G →* H ⧸ H*`, defined using the
  abelian quotient `H / H*`.

## Main Theorems

* `transferFocal_eq_pow`:
  The restriction of the transfer map to `H` acts like the power map `x ↦ x ^ [G : H]` mod `H*`.
* `commutator_inf_eq_focalSubgroup`: The **Focal Subgroup Theorem**.
  For a Sylow `p`-subgroup `P` of a finite group `G`, `G' ⊓ P = P*`,
  where `P*` is the focal subgroup of `P`.

## References

* [D. Gorenstein, *Finite Groups*][gorenstein1968]
-/

@[expose] public section

open Function Sylow

namespace Subgroup

section Definitions

variable {G : Type*} [Group G] (H : Subgroup G)

/--
The **Focal Subgroup** of a subgroup `H` (denoted `H*` or `foc(H)`).
It is generated by elements of the form `x⁻¹ * (u * x * u⁻¹)` where both `x` and `x^u` are in `H`.
-/
def focalSubgroup : Subgroup G :=
  closure { g | g ∈ H ∧ ∃ x ∈ H, ∃ u : G, g = ⁅x, u⁆ }

/--
The focal subgroup considered as a subgroup of `H`.
Note that `focalSubgroup H` is always contained in `H`.
-/
def focalSubgroupOf : Subgroup H :=
  (focalSubgroup H).subgroupOf H

theorem focalSubgroup_def :
    focalSubgroup H = closure { g | g ∈ H ∧ ∃ x ∈ H, ∃ u : G, g = ⁅x, u⁆ } := rfl

theorem focalSubgroupOf_def :
    focalSubgroupOf H = (focalSubgroup H).subgroupOf H := rfl

/-- Lemma: The focal subgroup is indeed a subgroup of H. -/
lemma focalSubgroup_le : focalSubgroup H ≤ H := by
  rw [focalSubgroup_def, closure_le]
  rintro g ⟨h, -, -, -, -⟩
  exact h

@[simp] theorem map_focalSubgroupOf : H.focalSubgroupOf.map H.subtype = H.focalSubgroup :=
  map_subgroupOf_eq_of_le H.focalSubgroup_le

/-- Lemma: H* is a normal subgroup of H. -/
instance : Normal (focalSubgroupOf H) := by
  rw [focalSubgroupOf_def, normal_subgroupOf_iff (focalSubgroup_le H)]
  intro n g hn hg
  induction hn using closure_induction with
  | mem z hz =>
    obtain ⟨hzH, x, hxP, u, rfl⟩ := hz
    exact subset_closure ⟨by aesop, g * x * g⁻¹, by aesop, g * u * g⁻¹, by group⟩
  | one => simp
  | mul _ _ _ _ IHa IHb => simpa using mul_mem IHa IHb
  | inv _ _ IH => simpa [mul_assoc] using inv_mem IH

/-- Lemma: The focal subgroup is contained in the commutator subgroup G'. -/
lemma focalSubgroup_le_commutator : focalSubgroup H ≤ _root_.commutator G := by
  rw [focalSubgroup_def, closure_le]
  rintro g ⟨_, x, _, u, rfl⟩
  exact commutator_mem_commutator (mem_top x) (mem_top u)

/--
In the quotient `H / H*`, conjugation by any element of `G` preserves equivalence classes.
If `h ∈ H` and `g⁻¹ * h * g ∈ H`, then `g⁻¹hg ≡ h (mod H*)`.
-/
lemma focalSubgroupOf.mk'_conj_eq {h : G} (hh : h ∈ H) (g : G)
    (hconj : g⁻¹ * h * g ∈ H) :
    (QuotientGroup.mk' (focalSubgroupOf H)) ⟨g⁻¹ * h * g, hconj⟩ =
    (QuotientGroup.mk' (focalSubgroupOf H)) ⟨h, hh⟩ := by
  apply QuotientGroup.eq.mpr
  rw [focalSubgroupOf_def, mem_subgroupOf]
  apply subset_closure
  simp only [coe_mul, coe_inv]
  exact ⟨H.mul_mem (H.inv_mem hconj) hh, _, H.inv_mem hconj, g, by group⟩

theorem focalSubgroupOf_eq_closure :
    focalSubgroupOf H = closure { g : H | ∃ x ∈ H, ∃ u : G, g = ⁅x, u⁆ } := by
  rw [← (map_injective H.subtype_injective).eq_iff, map_focalSubgroupOf,
    MonoidHom.map_closure, focalSubgroup_def]
  congr
  simp
  grind

lemma commutator_le_focalSubgroupOf : _root_.commutator H ≤ focalSubgroupOf H := by
  rw [focalSubgroupOf_eq_closure]
  apply closure_mono
  rintro - ⟨a, -, b, -, -, rfl⟩
  exact ⟨a, a.2, b, rfl⟩

lemma commutator_le_focalSubgroup : ⁅H, H⁆ ≤ focalSubgroup H := by
  grw [← map_subtype_commutator, H.commutator_le_focalSubgroupOf, map_focalSubgroupOf]

instance : IsMulCommutative (H ⧸ focalSubgroupOf H) :=
  ⟨⟨(Normal.quotient_commutative_iff_commutator_le.mpr H.commutator_le_focalSubgroupOf).comm⟩⟩

/-- The transfer homomorphism `V : G → H/H*` from `G` the abelian quotient `H/H*`. -/
noncomputable def transferFocal [H.FiniteIndex] : G →* H ⧸ focalSubgroupOf H :=
  MonoidHom.transfer (QuotientGroup.mk' (focalSubgroupOf H))

/--
The restriction of the transfer map to `H` acts like the power map `x ↦ x^n` mod `H*`,
where `n = [G:H]`.
-/
theorem transferFocal_eq_pow [H.FiniteIndex] (x : H) :
    transferFocal H x = (x : H ⧸ H.focalSubgroupOf) ^ H.index := by
  have : Fintype (Quotient (MulAction.orbitRel (zpowers (x : G)) (G ⧸ H))) :=
    Fintype.ofFinite _
  rw [transferFocal, index_eq_sum_minimalPeriod H x, ← Finset.prod_pow_eq_pow_sum,
    MonoidHom.transfer_eq_prod_quotient_orbitRel_zpowers_quot]
  apply Finset.prod_congr rfl
  intros
  apply focalSubgroupOf.mk'_conj_eq H

end Definitions

section FocalSubgroupTheorem

variable {G : Type*} [Group G] {p : ℕ} [Fact p.Prime] (P : Sylow p G) [P.FiniteIndex]

/-- The power map `y ↦ y^n` is surjective on `P/P*` because `gcd(n, p) = 1`. -/
lemma focalSubgroupOf.pow_index_surjective :
    Surjective fun y : P ⧸ P.focalSubgroupOf ↦ y ^ P.index :=
  ((P.2.to_quotient P.focalSubgroupOf).powEquiv' P.not_dvd_index).surjective

/-- The Transfer homomorphism is surjective from `G` to `P/P*`. -/
lemma transferFocal_surjective : Surjective P.transferFocal := by
  intro x
  obtain ⟨x, rfl⟩ := focalSubgroupOf.pow_index_surjective P x
  obtain ⟨x, rfl⟩ := QuotientGroup.mk'_surjective _ x
  exact ⟨x, P.transferFocal_eq_pow x⟩

/-- Isomorphism theorem: `G / ker(V) ≅ P / P*`. -/
noncomputable def transferFocal.quotientKerMulEquivQuotientFocalSubroupOf :
    G ⧸ P.transferFocal.ker ≃* P ⧸ P.focalSubgroupOf :=
  QuotientGroup.quotientKerEquivOfSurjective P.transferFocal (transferFocal_surjective P)

lemma ker_restrict_transferFocal_eq_focalSubgroupOf :
    (P.transferFocal.restrict P).ker = P.focalSubgroupOf := by
  ext g
  have hQ : IsPGroup p (P ⧸ P.focalSubgroupOf) := P.2.to_quotient P.focalSubgroupOf
  rw [MonoidHom.mem_ker, MonoidHom.restrict_apply, transferFocal_eq_pow]
  simpa using (hQ.powEquiv' P.not_dvd_index).apply_eq_iff_eq (x := g) (y := 1)

lemma ker_transferFocal_inf_eq_focalSubgroup : P.transferFocal.ker ⊓ P = P.focalSubgroup := by
  rw [← subgroupOf_map_subtype, ← MonoidHom.ker_restrict, ← map_focalSubgroupOf]
  exact congr_arg _ (ker_restrict_transferFocal_eq_focalSubgroupOf P)

/--
**The Focal Subgroup Theorem**

For a Sylow p-subgroup P of a finite group G, `P ∩ G' = P*`,
where `P*` is the focal subgroup of `P`.
-/
theorem commutator_inf_eq_focalSubgroup :  _root_.commutator G ⊓ P = P.focalSubgroup := by
  apply le_antisymm
  · apply le_trans ?_ (ker_transferFocal_inf_eq_focalSubgroup P).le
    exact inf_le_inf_right _ (Abelianization.commutator_subset_ker P.transferFocal)
  · exact le_inf P.focalSubgroup_le_commutator P.focalSubgroup_le

end FocalSubgroupTheorem

end Subgroup

end
